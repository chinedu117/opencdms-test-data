$STORAGE:2
      SUBROUTINE AREAQC(ITYPE,RECTYPE,OLDDDS)
C
C   ROUTINE TO READ THE TEMPORARY WORK FILE(TWF) , REFORMAT IT, AND
C     WRITE IT TO ELEMENT DISK FILES (1 PER ELEMENT). IT ALSO FINDS
C     THE STATION AND ELEMENT INFORMATION FOR ALL OF THE DATA AND 
C     DISPLAYS A COLOR-CODED SIMULATED CONTOUR OF THE SCALED VALUES.
C      
$INCLUDE: 'VAL1.INC'
$INCLUDE: 'INDEX.INC'
$INCLUDE: 'ELEMCHKS.INC'
$INCLUDE: 'AREAQC.INC'
C
      CHARACTER*3 RECTYPE
      CHARACTER*2 RDISK
      CHARACTER*16 DDISK
      INTEGER*2 ITYPE,OLDDDS,DDSMAP,ILINE,NUMREC
      LOGICAL FRSTCL
      DATA ILINE /1/, FRSTCL/.TRUE./ 
C      
      IF (RECTYPE.EQ.'U-A') THEN
         CALL WRTMSG(3,119,12,1,1,' ',0)
         RETURN
      END IF
C
C   READ THE TEXT FOR MESSAGES FROM THE MESSAGE FILE
C
      CALL GETMSG(322,MSGLN(1))
      CALL GETMSG(323,MSGLN(2))
      CALL GETMSG(325,MSGLN(3))
      CALL GETMSG(999,MSGLN)
C
C   READ THE EGA COLORS TO BE USED FROM THE DATAQC PARAMETER FILE
C
      IF (FRSTCL) THEN
         FRSTCL = .FALSE.
         DDSMAP = 9999
  50     CONTINUE
         OPEN (9,FILE='P:\DATA\DATAQC.PRM',STATUS='OLD',
     +         FORM='FORMATTED',IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL OPENMSG('P:\DATA\DATAQC.PRM    ','AREAQC      ',IOCHK)
            GO TO 50
         END IF
C
C     SKIP THE LINES WHICH CONTROL COMPUTATION OF MOISTURE VARIABLES 
C     THEN READ IN THE EGA PALETTE COLORS
C
         DO 60 I1 = 1,23
            READ(9,*)
   60    CONTINUE
         READ(9,*) (EGAREG(I1),I1=1,9)
         CLOSE(9)
      END IF
C
C   CALL AQCBNR TO GET THE NEEDED TIME PARAMETERS, READ THE SETUP FILE
C
  100 CONTINUE
      CALL AQCBNR(ITYPE,RECTYPE,DSETID,YEAR,MONTH,DAY,OLDDDS
     +     ,RTNCODE)
      IF (RTNCODE.EQ.'1') THEN
         RETURN
      END IF
C
C  DEFINE THE SEARCH KEY (DDSID+YEAR+MONTH+DAY+HOUR) 
C 
      WRITE(SRCHKEY,'(I3.3,I4,2I2.2,''  '')') DSETID,YEAR,MONTH,DAY
      IF (MONTH.EQ.0) THEN
         SRCHKEY(8:9) = '  '
      END IF
      IF (DAY.EQ.0) THEN
         SRCHKEY(10:11) = '  '
      END IF
      CALL LOCATE(5,0,IERR)
C
C   IF THIS IS A NEW DATASET THEN FIND THE MAPFILE LINKED TO THIS
C   DATATYPE AND DATASET 
C
      IF (DDSMAP.NE.DSETID) THEN
         DDSMAP = DSETID
  120    CONTINUE
         OPEN(41,FILE='P:\DATA\QCMAPS.MPC',STATUS='OLD',ACCESS='DIRECT'
     +       ,RECL=30,IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL OPENMSG('P:\DATA\QCMAPS.MPC    ','AREAQC      ',IOCHK)
            GO TO 120
         END IF   
         WRITE(DDSID,'(I3.3)') DSETID      
         DO 300 I = 1,9999
            READ(41,REC=I,ERR=310) DELMARK,MAPTYPE,MAPDDS,MAPFILE
            IF (DELMARK.NE.'*'.AND.MAPTYPE.EQ.RECTYPE.AND.MAPDDS.EQ
     +            .DDSID) THEN
               GO TO 350
            END IF
  300    CONTINUE
  310    CONTINUE
         CLOSE(41)
         CALL WRTMSG(3,61,12,1,1,' ',0)
         GO TO 100
  350    CONTINUE
         CLOSE(41)
  360    CONTINUE    
C
C   OPEN THE MAP FILE AND READ THE COORDINATES AND LEVELS OF DETAIL OF
C   THE QC AREA
C
         OPEN(40,FILE=MAPFILE,STATUS='OLD',FORM='UNFORMATTED'
     +       ,IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL OPENMSG(MAPFILE,'AREAQC      ',IOCHK)
            GO TO 360
         END IF
         READ(40) XLATMN,XLATMX,XLONMN,XLONMX,ICOAST,IBORDER,ISTATE
     +           ,IRIVER,ILAKE
         CLOSE(40)
      END IF
C
C   OPEN THE INPUT FILES (INDEX, DATA)
C
      CALL OPENFILES(1)
C
      READ(19,REC=1) DELKEY,BGNIDX,NUMIDX
      IF (NUMIDX.EQ.1) THEN
         CALL WRTMSG(3,78,12,1,1,' ',0)
         GO TO 100
      END IF
C
C  CHECK IF THE QC FILES MUST BE BUILT BY CHECKING THE DATES WRITTEN
C  IN THE FIRST RECORD OF THE \DATA\STNINF.AQC FILE (IF IT EXISTS)
C
      LEND = 16
      CALL GETDSC(RDISK,DDISK,LEND)
      STNFILE = 'P:\DATA\STNINF.AQC'
      STNFILE(1:2) = RDISK
      BLDQC = .TRUE.
      OPEN(60,FILE=STNFILE,STATUS='OLD',FORM='UNFORMATTED',IOSTAT=IOCHK) 
      IF (IOCHK.EQ.0) THEN
         READ(60,ERR=400) INTYPE,INDDS,INYEAR,INMON,INDAY
         IF (INDDS.EQ.DSETID.AND.INYEAR.EQ.YEAR.AND.INMON.EQ.MONTH.
     +          AND.INTYPE.EQ.RECTYPE.AND.INDAY.EQ.DAY) THEN
            BLDQC = .FALSE.
         END IF
  400    CONTINUE
         CLOSE(60)
      END IF
C
C   DETERMINE THE COLUMNS WHICH CONTAIN THE AREAQC ELEMENTS
C
      NUMQC = 0
      DO 440 I1 = 1,MAXQCELM
         DO 420 I2 = 1,NUMELEM
            IF (TBLELEM(I2).EQ.AQCELEM(I1).AND.AQCELEM(I1).NE.999)
     +             THEN
               NUMQC = NUMQC + 1
               ELMCOL(NUMQC) = I2
               GO TO 440
            END IF
  420    CONTINUE
  440 CONTINUE
      IF (NUMQC.EQ.0) THEN
         CALL WRTMSG(3,79,12,1,1,' ',0)
         RETURN
      END IF
      ELMFILE = 'P:\DATA\QCELEM.AQC'
      ELMFILE(1:2) = RDISK
      NRECL = 12 + NUMQC*4
C
C   CHECK EACH AREAQC ELEMENT TO SEE IF IT SHOULD BE SCALED
C   AND SET SCLELEM TO THE CHECK NUMBER WHICH SPECIFIES IT
C
      DO 500 I2 = 1,NUMQC
         I1 = ELMCOL(I2)
         DO 450 ICHK = 1,MAXCHK
            IF (CHKTYP(I1,ICHK).EQ.0) THEN
               SCLELEM(I2) = 0
               GO TO 500
            END IF
            IF (CHKTYP(I1,ICHK).EQ.55) THEN
               SCLELEM(I2) = ICHK
               GO TO 500
            ELSE IF (CHKTYP(I1,ICHK).EQ.08) THEN
               SCLELEM(I2) = ICHK + 100
               GO TO 500
            END IF
  450    CONTINUE
  500 CONTINUE            
C
C   IF THE QC FILES DO NOT NEED TO BE REBUILT GIVE THE USER THE OPTION
C
      IF (.NOT.BLDQC) THEN
         CALL LOCATE(4,0,IERR)
         CALL WRTSTR(MSGLN(1),40,14,0)
         CALL LOCATE(4,40,IERR)
         CALL OKREPLY(REPLY,RTNCODE)
         IF (REPLY.EQ.'Y') THEN
            BLDQC = .TRUE.
         END IF
      END IF
C
      RTNCODE = '0'
      KEYQC = '  '
      KEYQC(9:11) = RECTYPE
      WRITE(AMONTH,'(I2.2)') MONTH
      KEYQC(12:13) = AMONTH
C
C   IF WANTED, CALL THE ROUTINE TO BUILD THE QCELEM AND STNINF FILES
C
      IF (BLDQC) THEN      
         OPEN(60,FILE=STNFILE,STATUS='UNKNOWN',FORM='UNFORMATTED')
         WRITE(60) RECTYPE,DSETID,YEAR,MONTH,DAY
         CALL AQC1(NUMREC)
         IF (NUMREC.EQ.0) THEN
            CALL WRTMSG(3,78,12,1,1,' ',0)
            GO TO 100
         END IF
      END IF
C
C   CALL THE ROUTINE TO PLOT THE DATA - IF USER WANTS TO CHANGE DATA
C   VALUES CALL EDIT AND CHANGE THE REQUIRED RECORDS IN THE QCELEM FILE
C 
      IF (RTNCODE.EQ.'0') THEN
  550    CONTINUE
         OPEN(40,FILE=MAPFILE,STATUS='OLD',FORM='UNFORMATTED')
         CALL SETLOC(2,0)
         CALL AQC2(IELEM,ILINE)
         IF (CHGVAL) THEN
            IDKEY(1:8) = STNID
            IDKEY(9:21) = SRCHKEY
            JELEM = IELEM
            JLINE = ILINE
            CALL EDIT(ITYPE,RECTYPE,IDKEY,JELEM,JLINE,OLDDDS)
            CALL CHGDAT
            GO TO 550
         END IF
      END IF
      RETURN
      END
