$STORAGE:2

      SUBROUTINE AQCBNR(ITYPE,RECTYPE,DSETID,YEAR,MONTH,DAY,OLDDDS
     +      ,RTNCODE)
C
C     SUBROUTINE TO DISPLAY THE BANNER AND SOLICIT; STATION ID, DATA
C     SET ID, AND TIME FRAME DATA.  iT THEN READS THE APPROPRIATE
C     SETUP RECORD AND READS THE ELEMENT SCALE FACTORS.
C
C     IT ALSO BUILDS THE INDEX AND DATA FILE NAMES
C
$INCLUDE: 'INDEX.INC'
$INCLUDE: 'VAL1.INC'
C
      CHARACTER*1 RTNCODE
      CHARACTER*2 HOURLBL(24)
      CHARACTER*3 RECTYPE,ITYPE
      CHARACTER*4 FIELD(4)
      INTEGER*2 YEAR,MONTH,DAY,DSETID,OLDDDS
C      
      DATA FIELD /4*'  '/
      RTNCODE = '0'
C
C     SET UP THE SCREEN
C
      CALL SETMOD(3,IERR)
C
C   RETRIEVE THE FIELDS FROM THE USER
C      
   20 CONTINUE
      CALL GTQCBN(RECTYPE,FIELD,RTNCODE)
      IF (RTNCODE.EQ.'1')THEN
         RETURN
      END IF
      READ(FIELD(1),'(I3)') DSETID
      READ(FIELD(2),'(I4)') YEAR
      IF (RECTYPE.EQ.'DLY') THEN
         READ(FIELD(3),'(BN,I2)') MONTH
         DAY = 0
      ELSE IF (RECTYPE.EQ.'MLY'.OR.RECTYPE.EQ.'10D') THEN
         MONTH = 0
         DAY = 0
      ELSE
         READ(FIELD(3),'(BN,I2)') MONTH
         READ(FIELD(4),'(BN,I2)') DAY
      END IF
C
C   IF THIS IS A NEW DATASET, OPEN THE SETUP FILE, LOAD THE ELEMENT
C   CODES AND COLUMN HEADERS AND NAME THE INDEX AND DATA FILES
C   LOAD THE ELEMCHKS QC-CONTROL FILE INFORMATION INTO THE COLUMNS
C   APPROPRIATE FOR EACH ELEMENT
C
      IF (DSETID.NE.OLDDDS) THEN
         OLDDDS = DSETID
         CALL GETSET(DSETID,ITYPE,RECTYPE,HOURLBL,RTNCODE)
         IF (RTNCODE.NE.'0') THEN
            OLDDDS = -99
            GO TO 20
         END IF
         CALL GETCHK(RECTYPE)
      END IF
      RETURN
      END
************************************************************************
$PAGE
      SUBROUTINE GTQCBN(RECTYPE,FIELD,RTNCODE)
C
C     SUBROUTINE TO DISPLAY THE BANNER LINE AND RETRIEVE ACCEPTABLE
C     RESPONSES
C
      CHARACTER*1 RTNCODE
      CHARACTER*2 RTNFLAG
      CHARACTER*3 RECTYPE
      CHARACTER*4 FIELD(4)
      CHARACTER*8 FORMNAME(3)
C
      DATA FORMNAME /'AQC2FLDS','AQC3FLDS','AQC4FLDS'/
      RTNCODE = '0'
C
20    CONTINUE
      CALL LOCATE (0,0,IERR)
      IF (RECTYPE.EQ.'MLY'.OR.RECTYPE.EQ.'10D') THEN
         IFORM = 1
      ELSE IF (RECTYPE.EQ.'DLY') THEN
         IFORM = 2
      ELSE
         IFORM = 3
      END IF
      CALL GETFRM(FORMNAME(IFORM),'  ',FIELD,4,RTNFLAG)
      IF (RTNFLAG.EQ.'4F')THEN
         RTNCODE = '1'
         RETURN
      END IF
      IF (FIELD(1).EQ.'    ') THEN
         CALL WRTMSG(3,101,12,1,1,' ',0)
         GO TO 20
      END IF
      CALL CLRMSG(3)
      CALL CLRMSG(2)
      RETURN
      END
