$STORAGE:2
C
      SUBROUTINE SRTIDX(IDXNAM,RTNCODE)
C
C   ROUTINE TO "SORT" THE IDX FILE WHOSE NAME HAS BEEN PASSED TO IT
C   IT ASSUMES THAT THE FILE IS IN SORT EXCEPT THAT SOME RECORDS HAVE
C   BEEN MARKED AS DELETED.  THEREFORE, THIS ROUTINE SIMPLY SPLITS THE
C   INDEX FILE INTO DELETED AND NON-DELETED RECORDS AND THEN MERGES THEM
C   BACK TOGETHER
C
      CHARACTER*22 TMP1, TMP2
      CHARACTER*21 INKEY
      CHARACTER*16 DDISK
      CHARACTER*22 IDXNAM
      CHARACTER*2 RDISK
      CHARACTER*1 RTNCODE
      INTEGER*2 DELKEY,RECNUM,BGNIDX,NUMIDX,I 
C
C   OPEN THE INPUT INDEX AND TEMPORARY FILES
C
      WRITE(*,20)
   20 FORMAT(/,'   Re-sorting the index file')
      RTNCODE = '0'
   30 CONTINUE
      OPEN (19,FILE=IDXNAM,STATUS='OLD',ACCESS='DIRECT',
     +      FORM='UNFORMATTED',RECL=25,IOSTAT=IOCHK)
      IF(IOCHK.NE.0) THEN
         CALL OPENMSG(IDXNAM,'SRTIDX      ',IOCHK)
         GO TO 30
      END IF   
      LEND = 2
      CALL GETDSC(RDISK,DDISK,LEND)
      TMP1 = '  \DATA\TMP1.TMP'
      TMP1(1:2) = RDISK
      TMP2 = '  \DATA\TMP2.TMP'
      TMP2(1:2) = RDISK
      OPEN (61,FILE=TMP1,STATUS='UNKNOWN',FORM='UNFORMATTED'
     +    ,IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         CALL OPENMSG(TMP1,'SRTIDX      ',IOCHK)
         STOP 2
      END IF
      
      OPEN (62,FILE=TMP2,STATUS='UNKNOWN',FORM='UNFORMATTED')
C
C  COPY THE DELETED AND UNDELETED RECORDS INTO 2 FILES
C
      DO 100 I = 2,9999
         READ(19,REC=I,ERR=110) DELKEY,INKEY,RECNUM
         IF (DELKEY.EQ.1) THEN
            WRITE(61) DELKEY,INKEY,RECNUM
         ELSE
            WRITE(62) DELKEY,INKEY,RECNUM
         END IF
  100 CONTINUE
  110 CONTINUE
C
C   WRITE THE DELETED RECORDS BACK TO THE INDEX FILE
C
      REWIND(61)
      REWIND(62)
      DO 200 I = 2,9999
         READ(61,END=210) DELKEY,INKEY,RECNUM
         WRITE(19,REC=I) DELKEY,INKEY,RECNUM
  200 CONTINUE
  210 CONTINUE
      BGNIDX = I 
C
C   WRITE THE UNDELETED RECORDS BACK 
C
      DO 300 I = BGNIDX,9999
         READ(62,END=310) DELKEY,INKEY,RECNUM
         WRITE(19,REC=I) DELKEY,INKEY,RECNUM
  300 CONTINUE
  310 CONTINUE
C
C   RESET THE FIRST RECORD IN THE INDEX FILE
C
      NUMIDX = I - 1
      DELKEY = 0
      WRITE(19,REC=1) DELKEY,BGNIDX,NUMIDX
C
      CLOSE(19)
      CLOSE(61,STATUS='DELETE')
      CLOSE(62,STATUS='DELETE')
      WRITE(*,350)
  350 FORMAT('   Index file re-sorted',/)
c
      RETURN
      END
