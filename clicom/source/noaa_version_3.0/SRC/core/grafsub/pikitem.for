$STORAGE:2
      SUBROUTINE PIKITEM(HEIGHT,NBRITEM,HELPLEVEL,XHLP,YHLP,OUTCHAR)
C--------------------------
C     THIS ROUTINE DETERMINES THE ITEM NUMBER SELECTED WITHIN A GRAPHICS 
C     MENU USING EITHER THE MOUSE OR CURSOR KEYS.
C-------------------------
C     INPUT:  HEIGHT..... HEIGHT OF A LINE OF TEXT IN WORLD COORDINATES
C             NBRITEM.... ABSOLUTE VALUE = NUMBER OF ITEMS IN CURRRENT MENU
C                         >0 = NEW MENU -- NO HIGHLIGHT PRESENT
C                         <0 = OLD MENU -- HIGHLIGHT PRESENT     
C             HELPLEVEL.. GRAPHICS HELP FILE NUMBER
C             XHLP,YHLP.. LOWER LEFT WINDOW CORNER IN NORMALIZED DEVICE COORDS
C             OUTCHAR.... STARTING POSITION OF HIGHLIGHT -- BLANK INDICATES
C                         STARTING POSITION IS FIRST CHOICE
C     OUTPUT: OUTCHAR.... ITEM NUMBER SELECTED OR ESCAPE/F4 KEY ENTRY
C
  
      CHARACTER*2 OUTCHAR, INCHAR, CHRNUM
      CHARACTER*1 RTNCODE
      REAL        XPOS, YPOS, ZPOS, XLOW, YLOW, XHIGH, YHIGH, YFACTOR,
     +            YCENTER, CURSSTEP, MOUSSTEP
      INTEGER     NCHOICE, PRVCHOI, HELPLEVEL  
C      
C       ** LOCAL COMMON TO SAVE SPACE IN D-GROUP
C
      COMMON /PIKSAV/ XPOS, YPOS, ZPOS, XLOW, YLOW, XHIGH, YHIGH, 
     +        YFACTOR, YCENTER, CURSSTEP, MOUSSTEP, NCHOICE, PRVCHOI,
     +        INCHAR, CHRNUM, RTNCODE
C
C   SET UP SELECTION HIGHLIGHT
C
C   FIND CURRENT WORLD COORDS (INSIDE THE WINDOW) AND SET VERTICAL SENSITIVITY
C   VALUES FOR CURSOR AND MOUSE.  THEY DETERMINE IF THE MOVEMENT OF THE CURSOR
C   OR MOUSE IS SUFFICIENT TO TRIGGER A CHANGE. SINCE CURSOR MOVEMENT IS FIXED
C   AT 1/200TH OF THE WORLD BY RDLOC, THE CURSTEP VALUE IS SET TO A VALUE
C   WHICH ALWAYS TRIGGERS MOVEMENT EVERY TIME A CURSOR KEY IS PRESSED.
C   ALSO DETERMINE SIZE OF THE TITLE AND OTHER TEXT IN THESE COORDINATES.
C
      CALL INQWOR(XLOW,YLOW,XHIGH,YHIGH)
      CURSSTEP = (YHIGH - YLOW) / 1000.
      MOUSSTEP = (YHIGH - YLOW) / 25.
      YCENTER = (YHIGH + YLOW) / 2.0
      CALL SETXOR(1)
      CALL SETCOL(1)
      CALL SETHAT(1)
C
      NUMCHOICE = ABS(NBRITEM)
      NEWFLG = MAX0(0,MIN0(1,NBRITEM))
C            
      WRITE(CHRNUM,'(I1,1X)') NUMCHOICE
      IF (OUTCHAR(2:2).EQ.' '.AND.OUTCHAR.GE.'1 '.AND.
     +                            OUTCHAR.LE.CHRNUM) THEN
         READ(OUTCHAR,'(I1,1X)') NCHOICE            
         PRVCHOI = NCHOICE
      ELSE
         NCHOICE = 1
         PRVCHOI = 1
      ENDIF      
      IF (NEWFLG.EQ.1) THEN
         YPOS = YHIGH + HEIGHT*.05 - (NCHOICE+1)*HEIGHT
         CALL BAR(XLOW,YPOS,XHIGH,YPOS+HEIGHT)
      ENDIF      
C
C   SET LOCATOR POSITION, IF ANY, AND READ IT BACK TO ACCOUNT FOR ROUND OFF
C
C      XPOS = XLOW
C      YPOS = YCENTER
C      CALL ORGLOC(XPOS,YPOS)
C      CALL RDLOC(XLOW,YCENTER,INCHAR,RTNCODE)
C      XPOS = XLOW
C      YPOS = YCENTER
      CALL RDLOC(XPOS,YPOS,INCHAR,RTNCODE)
      XPOS = XLOW
      YPOS = YCENTER
      CALL ORGLOC(XLOW,YCENTER)
      CALL RDLOC(XPOS,YPOS,INCHAR,RTNCODE)
      IF (RTNCODE .EQ. '1') THEN
         YCENTER = YPOS
      ENDIF
C
C   ALLOW USER TO MOVE HIGHLIGHT OR ENTER NUMBER TO SELECT THE
C   CHOICE WANTED 
C
190   CONTINUE      
         CALL RDLOC(XPOS,YPOS,INCHAR,RTNCODE)
         IF (INCHAR(2:2).EQ.' '.AND.INCHAR.GE.'1 '.AND.
     +                              INCHAR.LE.CHRNUM) THEN
            OUTCHAR = INCHAR            
            READ(OUTCHAR,'(I1,1X)') NCHOICE            
            IF (NCHOICE.NE.PRVCHOI) THEN
               ZPOS = YHIGH + HEIGHT*.05 - (PRVCHOI+1)*HEIGHT
               CALL BAR(XLOW,ZPOS,XHIGH,ZPOS+HEIGHT)
               ZPOS = YHIGH + HEIGHT*.05 - (NCHOICE+1)*HEIGHT
               CALL BAR(XLOW,ZPOS,XHIGH,ZPOS+HEIGHT)
               PRVCHOI = NCHOICE
            END IF
            GO TO 200
C
C    IF ENTER, F4, OR ESC ARE PRESSED - EXIT
C
         ELSE IF (INCHAR.EQ.'RE') THEN
            WRITE(OUTCHAR,'(I1,1X)') NCHOICE            
            GO TO 200
         ELSE IF(INCHAR.EQ.'ES'.OR.INCHAR.EQ.'4F') THEN
            OUTCHAR = 'ES'
            GO TO 200
C
C     IF F1 PRESSED - CALL HELP
C
         ELSE IF (INCHAR.EQ.'1F') THEN
            CALL GRAFHELP(HELPLEVEL,XHLP,YHLP)
            CALL SETCOL(1)
            CALL SETHAT(1)
            GO TO 190
         ELSE IF (INCHAR.NE.'  '.AND.INCHAR.NE.'XX') THEN
            CALL BEEP
            GO TO 190
         END IF
C
C    IF A NEW SELECTION IS MADE, MOVE HIGHLIGHT 
C
         IF (RTNCODE.EQ.'1') THEN
            YFACTOR = MOUSSTEP
         ELSE
            YFACTOR = CURSSTEP
         END IF
         IF (YPOS.NE.YCENTER) THEN
            IF (YPOS.GT.YCENTER+YFACTOR) THEN
               NCHOICE = NCHOICE - 1
               XPOS = XLOW
               YPOS = YCENTER
               CALL ORGLOC(XPOS,YPOS)
            ELSE IF (YPOS.LT.YCENTER-YFACTOR) THEN
               NCHOICE = NCHOICE + 1
               XPOS = XLOW
               YPOS = YCENTER
               CALL ORGLOC(XPOS,YPOS)
            END IF
            IF (NCHOICE.GT.NUMCHOICE) THEN
               NCHOICE = NUMCHOICE
            ELSE IF (NCHOICE.LT.1) THEN
               NCHOICE = 1
            END IF
         END IF
         IF (NCHOICE.NE.PRVCHOI) THEN
            ZPOS = YHIGH + HEIGHT*.05 - (PRVCHOI+1)*HEIGHT
            CALL BAR(XLOW,ZPOS,XHIGH,ZPOS+HEIGHT)
            ZPOS = YHIGH + HEIGHT*.05 - (NCHOICE+1)*HEIGHT
            CALL BAR(XLOW,ZPOS,XHIGH,ZPOS+HEIGHT)
            PRVCHOI = NCHOICE
         END IF
         GO TO 190
  200 RETURN
      END
