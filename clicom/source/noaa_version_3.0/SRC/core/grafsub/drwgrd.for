$STORAGE:2
      SUBROUTINE DRWXGRD(XGRDCLR,XGRDTYP,XGRDTHK,YAXSID,TICSZNW)
C
C       ** OBJECTIVE:  DRAW HORIZONTAL GRID LINES AT MAJOR Y-AXIS TIC MARKS 
C
C       ** INPUT:
C             XGRDCLR....GRID LINE COLOR NUMBER
C             XGRDTYP....GRID LINE PATTERN FLAG
C             XGRDTHK....GRID LINE THICKNESS IN PIXELS
C             YAXSID.....FLAG TO INDICATE THE AXIS WHOSE DIVISIONS WILL BE
C                        USED FOR GRID     'L'=LEFT AXIS   'R'=RIGHT AXIS
C             TICSZNW....TIC MARK LENGTH IN NORMALIZED WORLD COORDINATES
C    
$INCLUDE:  'PLTSPEC.INC'
C
      INTEGER *2 XGRDCLR,XGRDTYP,XGRDTHK
      CHARACTER*1 YAXSID
      REAL*4 TICSZNW
C
C       ** DEFINE ATTRIBUTES FOR GRID LINE
C    
      IF (XGRDTYP.LE.0) GO TO 100
      CALL DEFHLN(XGRDCLR,XGRDTYP,XGRDTHK)
      CALL XSZNW2W(TICSZNW,XTICSZW,YTICSZW)
C
C       ** INITIAL START AND END LOCATIONS FOR GRID LINES
C      
      IF (ABS(YTYLOC-YMIN) .LT. .0001) THEN
         NA = 2
         YLOC = YTYLOC
      ELSE
         NA = 1
         YLOC = YTYLOC-DYTIC
      ENDIF
      IF (ABS(YMAX-(YTYLOC+FLOAT(NYTIC-1)*DYTIC)) .LT. .0001) THEN
         NB = NYTIC-1
      ELSE
         NB = NYTIC
      ENDIF
      IF (YAXSID.EQ.'L') THEN
C        .. LEFT AXIS DIVISIONS USED -- LEAVE SPACE FOR LEFT AXIS TIC MARKS
         XLOCA = XMIN+XTICSZW
         XLOCB = XMAX
      ELSE
C        .. RIGHT AXIS DIVISIONS USED -- LEAVE SPACE FOR RIGHT AXIS TIC MARKS
         XLOCA = XMIN
         XLOCB = XMAX-XTICSZW
      ENDIF      
C
C       ** DRAW GRID LINES
C      
      DO 20 N=NA,NB
         YLOC = YLOC + DYTIC   
         CALL MOVABS(XLOCA,YLOC)
         CALL LNABS(XLOCB,YLOC)
   20 CONTINUE
C
  100 RETURN
      END            
      SUBROUTINE DRWYGRD(YGRDCLR,YGRDTYP,YGRDTHK,TICSZNW)
C
C       ** OBJECTIVE:  DRAW VERTICAL GRID LINES AT MAJOR X-AXIS TIC MARKS 
C
C       ** INPUT:
C             YGRDCLR....GRID LINE COLOR NUMBER
C             YGRDTYP....GRID LINE PATTERN FLAG
C             YGRDTHK....GRID LINE THICKNESS IN PIXELS
C             TICSZNW....TIC MARK LENGTH IN NORMALIZED WORLD COORDINATES
C    
$INCLUDE:  'PLTSPEC.INC'
C
      INTEGER *2 YGRDCLR,YGRDTHK,YGRDTYP
C
C       ** DEFINE ATTRIBUTES FOR GRID LINE
C    
      IF (YGRDTYP.LE.0) GO TO 100
      CALL DEFHLN(YGRDCLR,YGRDTYP,YGRDTHK)
      CALL XSZNW2W(TICSZNW,XTICSZW,YTICSZW)
C
C       ** INITIAL START AND END LOCATIONS FOR GRID LINES
C      
      IF (ABS(XTXLOC-XMIN) .LT. .0001) THEN
         NA = 2
         XLOC = XTXLOC
      ELSE
         NA = 1
         XLOC = XTXLOC-DXTIC
      ENDIF
      IF (ABS(XMAX-(XTXLOC+FLOAT(NXTIC-1)*DXTIC)) .LT. .0001) THEN
         NB = NXTIC-1
      ELSE
         NB = NXTIC
      ENDIF
C
C       ** DRAW GRID LINES
C      
      DO 20 N=NA,NB
         XLOC = XLOC + DXTIC   
         CALL MOVABS(XLOC,YMIN+YTICSZW)
         CALL LNABS(XLOC,YMAX)
   20 CONTINUE
C
  100 RETURN
      END
