$STORAGE:2
      SUBROUTINE CKBKGND(SPECNAM,CPYBKGND)
C
C       ** OBJECTIVE:  CHECK PLOT SPECIFICATIONS TO DETERMINE IF
C                      SAVED BACKGROUND CAN BE COPIED TO SCREEN
C       ** INPUT:
C             SPECNAM.....NAME (<= 8 CHARACTERS) OF FILE CONTAINING PLOT
C                         SPECIFICATIONS IN EFFECT AT THE TIME THE GRAPH
C                         BACKGROUND SCREEN WAS SAVED
C       ** OUTPUT:
C             CPYBKGND....FLAG TO INDICATE IF A SAVED BACKGROUND CAN
C                         BE RESTORED
C                         0=DRAW NEW BACKGROUND   1=RESTORE BACKGROUND     
C
C       ** NOTE:  THIS ROUTINE SHOULD ONLY BE CALLED FROM GRAPHICS.  IT
C                 SHOULD NEVER BE CALLED FROM AREA QC
C
C       .. ARGUMENTS
      CHARACTER*(*) SPECNAM
      LOGICAL       CPYBKGND
C
C       .. PARAMETERS AND COMMONS
$INCLUDE:  'GRFPARM.INC'
$INCLUDE:  'GRAFVAR.INC'
$INCLUDE:  'CURRPLT.INC'
C
C       .. LOCAL VARIABLES
      INTEGER*2    IGPHSCR,BKCLSCR,MPRJSCR,MCODSCR(5)
      REAL*4       VPGASCR(8),LTLNSCR(4),ZOOMSCR
      CHARACTER*20 SPECFIL
      LOGICAL      EOFFLG
C
C       ** OPEN FILE CONTAINING PLOT SPECIFICATIONS IN EFFECT AT THE TIME
C          THE GRAPH BACKGROUND SCREEN WAS SAVED
C
      NCHR = MIN0(LNG(SPECNAM),8)
      SPECFIL = 'O:\DATA\'//SPECNAM(1:NCHR)//'.SPC'
      OPEN (51,FILE=SPECFIL,STATUS='UNKNOWN',FORM='FORMATTED'
     +     ,MODE='READ',IOSTAT=IOCHK)
C
C       .. SET FLAG TO RESTORE OLD BACKGROUND; TURN ON EOF FLAG
      CPYBKGND=.TRUE.
      EOFFLG=.TRUE.
C
C       ** READ PLOT SPECFICATIONS FOR SAVED SCREEN AND
C          COMPARE TO CURRENT VALUES
C
C       .. GRAPH TYPE, BACKGROUND COLOR, VIEWPORT AND PLOT AREA COORDINATES
      READ(51,*,END=20) IGPHSCR
      READ(51,*,END=20) BKCLSCR 
      READ(51,*,END=20) (VPGASCR(I),I=1,8)
C
      CPYBKGND = CPYBKGND .AND. (IGPHSCR.EQ.IGRAPH)
      CPYBKGND = CPYBKGND .AND. (BKCLSCR.EQ.BKGNCLR)
      DO 10 I=1,8
         CPYBKGND = CPYBKGND .AND. (VPGASCR(I).EQ.VPGA(I))
  10  CONTINUE    
C      
      IF (IGPHSCR.EQ.2) THEN
C
C          .. MAP:  LAT/LON LIMITS, MAP PROJECTION, LINE THICKNESS, LINE
C                   PATTERN, AND MAP DETAIL AND COLOR 
         READ(51,*,END=20) (LTLNSCR(I),I=1,4)
         READ(51,*,END=20) MPRJSCR,MTHKSCR,MTYPSCR           
         READ(51,*,END=20) (MCODSCR(I),I=1,5)           
         DO 12 I=1,4
            CPYBKGND = CPYBKGND .AND. (LTLNSCR(I).EQ.LTLN(I))
  12     CONTINUE    
         CPYBKGND = CPYBKGND .AND. (MPRJSCR.EQ.NDECRT(4))
         CPYBKGND = CPYBKGND .AND. (MTHKSCR.EQ.YGRDTHK)
         CPYBKGND = CPYBKGND .AND. (MTYPSCR.EQ.YGRDTYP(1))
         DO 14 I=1,5
            CPYBKGND = CPYBKGND .AND. (MCODSCR(I).EQ.MPCODE(I))
  14     CONTINUE    
      ELSE IF (IGPHSCR.EQ.3 .AND. ZOOMED) THEN
C
C          .. SKEWT WITH ZOOM:  MINIMUM PRESSURE ON ZOOM SCALE      
         READ(51,*,END=20) ZOOMSCR
         CPYBKGND = CPYBKGND .AND. (ZOOMSCR.EQ.LFTSCALE(2,2))
      ENDIF
      EOFFLG=.FALSE.
   20 CONTINUE   
C
C       .. BACKGROUND WILL NOT BE RESTORED IF EOF ENCOUNTERED ON PLOT
C          SPECIFICATION FILE   
      IF (EOFFLG) CPYBKGND=.FALSE.
C
C       .. CLOSE PLOT SPECIFICATION FILE
      CLOSE(51)
C
      RETURN
      END   
      SUBROUTINE RDBKGND(NAMEXT,RTNCODE)
C
C       ** OBJECTIVE:  READ AND COPY SAVED BACKGROUND TO SCREEN
C
C       ** INPUT:
C             NAMEXT....NAME (<= 8 CHARACTERS) AND EXTENSION (.XXX) OF FILE
C                       CONTAINING SAVED SCREEN
C       ** OUTPUT:
C             RTNCODE...ERROR FLAG -- '0'=NO ERROR  '1'=ERROR IN OPENING FILE
C
C       .. ARGUMENTS
      CHARACTER*(*) NAMEXT
      CHARACTER*1  RTNCODE
C
C       .. LOCAL VARIABLES
      CHARACTER*20 SCRNFIL
      CHARACTER*22 DELIMFIL
C
C       ** CHECK IF FILE CONTAINING SAVED SCREEN EXISTS -- USE FIRST 8
C          CHARACTERS IF NAME IS > 8 CHARACTERS
C
      RTNCODE = '0'
      NCHR=LNG(NAMEXT)-4
      NCHRNM=MIN0(NCHR,8)
      SCRNFIL ='O:\DATA\'//NAMEXT(1:NCHRNM)//NAMEXT(NCHR+1:)
      OPEN(51,FILE=SCRNFIL,STATUS='OLD',IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         RTNCODE='1'
      ENDIF
      CLOSE(51)
C
C       ** RESTORE SAVED SCREEN
C
      IF (RTNCODE.EQ.'0') THEN
         CALL DELIMSTR(SCRNFIL,DELIMFIL)
         CALL GREAD(DELIMFIL)
      ENDIF   
C
      RETURN
      END               
      SUBROUTINE WRBKGND(WRSPEC,NAMEXT)
C
C       ** OBJECTIVE:  
C       ** INPUT:
C             WRSPEC....FLAG TO INDICATE WHETHER CURRENT PLOT SPECIFICATIONS
C                       SHOULD BE WRITTEN TO FILE.  PLOT SPECIFICATIONS ARE
C                       WRITTEN IN GRAPHICS BUT NOT IN AREA-QC.
C                       T=WRITE  F=DO NOT WRITE
C             NAMEXT....NAME (<= 8 CHARACTERS) AND EXTENSION (.XXX) OF FILE
C                       CONTAINING SAVED SCREEN
C
C       .. ARGUMENTS
      LOGICAL WRSPEC
      CHARACTER*(*) NAMEXT
C      
C       .. PARAMETERS AND COMMONS
$INCLUDE:  'GRFPARM.INC'
$INCLUDE:  'GRAFVAR.INC'
C
C       .. LOCAL VARIABLES
      CHARACTER*20 NAMFIL
      CHARACTER*22 DELIMFIL
C
C       ** WRITE CURRENT PLOT SPECIFICATIONS FOR GRAPHICS BUT NOT AREA-QC
C
      IF (WRSPEC) THEN
C
C          .. OPEN FILE CONTAINING PLOT SPECIFICATIONS     
         NCHRNM=MIN0(LNG(NAMEXT)-4,8)
         NAMFIL = 'O:\DATA\'//NAMEXT(1:NCHRNM)//'.SPC'
         OPEN (51,FILE=NAMFIL,STATUS='UNKNOWN',FORM='FORMATTED'
     +        ,MODE='WRITE',IOSTAT=IOCHK)
C      
C          .. GRAPH TYPE, BACKGROUND COLOR, VIEWPORT AND PLOT AREA COORDINATES
         WRITE(51,500) IGRAPH
         WRITE(51,500) BKGNCLR 
         WRITE(51,518) (VPGA(I),I=1,8)
         IF (IGRAPH.EQ.2) THEN
C
C             .. MAP:  LAT/LON LIMITS, MAP PROJECTION (NDECRT(4)), LINE
C                      THICKNESS, LINE PATTERN, AND MAP DETAIL AND COLOR 
            WRITE(51,505) (LTLN(I),I=1,4)
            WRITE(51,500) NDECRT(4),YGRDTHK,YGRDTYP(1)           
            WRITE(51,500) (MPCODE(I),I=1,5)           
         ELSE IF (IGRAPH.EQ.3) THEN
C
C             .. SKEWT WITH ZOOM:  MINIMUM PRESSURE ON ZOOM SCALE      
            WRITE(51,516) LFTSCALE(2,2)
         ENDIF
C
C          .. CLOSE PLOT SPECIFICATION FILE          
         CLOSE(51)
      ENDIF
C      
C       ** SAVE CURRENT SCREEN BACKGROUND
      NCHR=LNG(NAMEXT)-4
      NCHRNM=MIN0(NCHR,8)
      NAMFIL ='O:\DATA\'//NAMEXT(1:NCHRNM)//NAMEXT(NCHR+1:)
      CALL DELIMSTR(NAMFIL,DELIMFIL)
      CALL GWRITE(DELIMFIL)
C
C       ** FORMAT STMTS
C
  500 FORMAT(6(I3,:,','))
  505 FORMAT(4(F8.2,:,','))
  516 FORMAT(F10.3)
  518 FORMAT(8(F6.4,:,','))
C
      RETURN
      END               
   