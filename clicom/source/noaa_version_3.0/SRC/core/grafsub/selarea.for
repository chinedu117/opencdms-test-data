$STORAGE:2 
      SUBROUTINE SELAREA(I4WRK,MXI4WRK,LOWLAT,HILAT,LOWLON,HILON,
     +                   STNLIST,LATLIST,LONLIST,MAXROW,RTNFLAG)
C
C       ** OBJECTIVE:   ALLOWS THE USER TO SPECIFY AN AREA TO BE MAPPED BY
C                       SETTING LATITUDE AND LONGITUDE BOUNDARIES.  IT THEN 
C                       RETURNS THE BOUNDARY COORDINATES, A LIST OF CURRENTLY
C                       DEFINED STATIONS WHOSE LAT,LONS FALL WITHIN THE 
C                       BOUNDARIES GIVEN, AND A LIST OF THE LATITUDES AND
C                       LONGITUDES OF EACH OF THE STATIONS FOUND.
C                       THE STATIONS ARE SELECTED FROM EITHER THE STNGEOG.INF
C                       FILE OR A FILE CONTAINING A LIST OF USER SPECIFIED
C                       STATIONS
C       ** INPUT:
C             I4WRK.........INTEGER*4 WORK ARRAY
C             MXI4WRK.......SIZE OF WORK ARRAY
C             LOWLAT,HILAT..LATITUDE RANGE WANTED (FLOATING PT)
C             LOWLON,HILON..LONGITUDE RANGE WANTED (FLOATING PT)
C             MAXROW........MAXIMUM NUMBER OF STATIONS ALLOWED
C       ** OUTPUT:
C             STNLIST.......LIST OF THE STATION-ID'S FOUND 
C                           (CHAR*8 DIMENSIONED MAXROW)
C             LATLIST.......LATITUDES OF THE STATIONS FOUND (REAL (MAXROW))L
C             LONLIST.......LONGITUDES OF THE STATIONS FOUND (REAL (MAXROW))L
C             RTNFLAG.......'4F' IF USER HAS PRESSED F4 OR NO STATIONS FOUND 
C
      INTEGER*2    MXI4WRK,MAXROW
      INTEGER*4    I4WRK(MXI4WRK)
      REAL*4       LOWLAT,HILAT,LOWLON,HILON,
     +             LATLIST(MAXROW),LONLIST(MAXROW)
      CHARACTER*8  STNLIST(MAXROW)
      CHARACTER*2  RTNFLAG
C      
      INTEGER*4    BDATE,EDATE,NRECG
      CHARACTER*24 STNABRV
      CHARACTER*20 COUNTRY,LSTFIL
      CHARACTER*8  STNID,PREVID,LSTNAM
      CHARACTER*8  INLON
      CHARACTER*7  INLAT
      CHARACTER*1  RTNCODE
      LOGICAL      LSTFLG
C
      CALL POSLIN(IROW,ICOL)      
C
C       ** ASK USER FOR THE LAT LON BOUNDARIES OF THE AREA WANTED
C
   10 CONTINUE
      CALL LOCATE(IROW,ICOL,IERR)
      CALL MAPCOORD(0,LOWLAT,HILAT,LOWLON,HILON,RTNFLAG)
      IF (RTNFLAG.EQ.'4F') THEN
         RETURN
      END IF
C
C       ** GET THE NAME OF A FILE CONTAINING A USER SPECIFIED LIST OF STATIONS
C          DEFAULT LIST NAME IS STNGEOG 
C
      LSTNAM = 'STNGEOG'
   15 CONTINUE
      CALL LOCATE(IROW+9,ICOL,IERR)
      MAXLST=MIN0(MAXROW,MXI4WRK)
      CALL GTSTNLST(I4WRK,LATLIST,MXI4WRK,MAXROW,MAXLST,LSTNAM,RTNCODE)
      IF (RTNCODE.NE.'0') GO TO 10
      LSTFLG=.FALSE.
      NRECL=0
C
C       **  OPEN THE STATION SELECTION FILE; GET NUMBER OF RECORDS IN FILE
C
      IF (LSTNAM.NE.'STNGEOG') THEN
         LSTFIL = 'O:\DATA\'//LSTNAM
         NCHR = LNG(LSTFIL)
         LSTFIL(NCHR+1:) = '.LST'
         OPEN (51,FILE=LSTFIL,STATUS='OLD',
     +            ACCESS='DIRECT',RECL=36,IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            NCHR=LNG(LSTFIL)
            CALL WRTMSG(3,44,12,1,1,LSTFIL,NCHR)
            GO TO 15
         ENDIF   
         READ(51,REC=1) NRECL
C         
C          .. CHECK IF RECORD NUMBERS AND STATION ID'S SPECIFIED IN THE LIST
C             FILE AGREE WITH THE STNGEOG.INF FILE;  ADJUST RECORD NUMBERS
C             IN LIST FILE SO THE FILES AGREE.  REJECT USER LIST FILE IF A 
C             RECORD DOES NOT HAVE A MATCH IN THE STNGEOG.INF FILE
         RTNCODE='0'
         CALL CKRECNBR(RTNCODE)
         IF (RTNCODE.NE.'0') THEN
            RTNCODE='0'
            CALL GTRECNBR(RTNCODE)
            IF (RTNCODE.NE.'0') THEN 
               CALL WRTMSG(3,582,12,1,1,' ',0)
               GO TO 15
            ENDIF   
         ENDIF   
         LSTFLG=.TRUE.
      ENDIF
C
C       ** OPEN THE STNGEOG.INF FILE AND INITIALIZE
C
20    OPEN (35,FILE='P:\DATA\STNGEOG.INF',STATUS='OLD',
     +      ACCESS='DIRECT',RECL=80,SHARE='DENYWR',MODE='READ'
     +      ,IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         CALL OPENMSG('P:\DATA\STNGEOG.INF   ','GRAFINIT    ',IOCHK)
         GO TO 20
      END IF
      READ(35,REC=1) NRECG
      INDEX = 0
      PREVID = 'XXXXXXXX'
C
C       ** IF STATIONS ARE SELECTED FROM THE STNGEOG.INF FILE, READ THROUGH
C          THE FILE SEQUENTIALLY.  IF STATIONS ARE SELECTED FROM THE USER 
C          LIST FILE, READ THE USER LIST FILE SEQUENTIALLY AND THEN READ
C          THE EQUIVALENT RECORD FROM THE STNGEOG FILE.  IDENTIFY ALL STATIONS
C          WHICH FALL WITHIN THE BOUNDARIES SPECIFIED, AND WRITE THEIR ID'S AND
C          LATEST LAT LON COORDINATES INTO THE OUTPUT ARRAYS.
C
      CALL WRTMSG(10,279,12,0,0,' ',0)
      IF (LSTFLG) THEN
         NRECRD=NRECL
      ELSE
         NRECRD=MIN0(NRECG,32000)
      ENDIF            
      DO 100 KREC = 2,NRECRD+1
         IF (LSTFLG) THEN
            READ(51,REC=KREC) IRECG,STNID,STNABRV
         ELSE
            IRECG=KREC
         ENDIF   
         READ(35,END=110,REC=IRECG) STNID,BDATE,EDATE,STNABRV,COUNTRY,
     +                              INLAT,INLON
         CALL LAT2REAL(INLAT,INLON,XLAT,XLON)
         IF (XLAT.GE.LOWLAT.AND.XLAT.LE.HILAT.AND.XLON.GE.LOWLON.AND.
     +         XLON.LE.HILON) THEN
            IF (STNID.NE.PREVID) THEN
               INDEX = INDEX + 1
               IF (INDEX.GT.MAXROW) THEN
                  CALL WRTMSG(3,184,12,1,1,STNID,8)
                  GO TO 110
               END IF
            END IF
            STNLIST(INDEX) = STNID
            LATLIST(INDEX) = XLAT
            LONLIST(INDEX) = XLON
            CALL LOCATE(15,20,IERR)
            CALL WRTSTR(STNID,8,11,0)
         END IF
         PREVID = STNID
100   CONTINUE
110   CONTINUE
C
C      ** CLOSE STNGEOG.INF, WRITE WARNING IF NO STATIONS FOUND AND RETURN
C
      IF (LSTFLG) CLOSE(51)
      CLOSE(35)
      IF (INDEX.EQ.0) THEN
         CALL WRTMSG(3,376,12,1,1,' ',0)
         RTNFLAG = '4F'
      END IF
      CALL CLRMSG(10)
      RETURN
      END
