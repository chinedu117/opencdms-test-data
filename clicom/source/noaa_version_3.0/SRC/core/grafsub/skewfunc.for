$STORAGE:2
      REAL FUNCTION SKEWTY(PRES)
C
C---- DETERMINE Y-COORDINATE ON SKEW-T, LOG P DIAGRAM GIVEN PRESSURE
C---- (MB). Y-ORIGIN AT P=1000 MB
C
      REAL PRES
      SKEWTY=132.182-44.061*ALOG10(PRES)
      RETURN
      END

      REAL FUNCTION SKEWTX(TEMP,Y)
C
C---- DETERMINE X-COORDINATE ON SKEW-T, LOG P DIAGRAM GIVEN TEMPERATURE
C---- (C) AND Y-COORDINATE FROM FUNCTION SKEWTY. X-ORIGIN AT T=0C
C
      REAL TEMP,Y
      SKEWTX=0.54*TEMP+0.90692*Y
      RETURN
      END

      REAL FUNCTION TDA(O,P)
C
C     THIS FUNCTION RETURNS THE TEMPERATURE TDA(CELSIUS) ON A DRY
C     ADIABAT AT PRESSURE P (MILLIBARS).  THE DAY ADIABAT IS GIVEN BY
C     POTENTIAL TEMPERATURE O (CELSIUS).  THE COMPUTATION IS BASED
C     ON POISSON'S EQUATION.
C
      OK  = O+273.15
      TDAK= OK*((P*.001)**.286)
      TDA = TDAK-273.15
      RETURN
      END

      REAL FUNCTION TMR(W,P)
C
C     THIS FUNCTION RETURNS THE TEMPERATURE (CELSIUS) ON A MIXING
C     RATIO LINE W (G/KG) AT PRESSURE P (MB).  THE FORMULA IS
C     GIVEN IN TABLE 1 ON PAGE 7 OF STIPANUK (1973).
C
C     INITIALIZE CONSTANTS
C
      DATA C1/.0498646455/, C2/2.4082965/, C3/7.07475/
      DATA C4/38.9114/,     C5/.0915/,     C6/1.2035/
C
      X   = ALOG10(W*P/(622.0+W))
      TMRK= 10.**(C1*X+C2)-C3+C4*((10.**(C5*X)-C6)**2.)
      TMR = TMRK-273.15
      RETURN
      END
      REAL FUNCTION SATLFT(THW,P)
C
C     INPUT:  THW = WET-BULB POTENTIAL TEMPERATURE (CELSIUS).
C             THW DEFINES A MOIST ADIABAT.
C             P = PRESSURE (MILLIBARS).
C     OUTPUT: SATLFT = TEMPERATURE (CELSIUS) WHERE THE MOIST
C             ADIABAT CROSSES P.
C
      DATA CTA, AKAP /273.15,0.28541/
C
C     CTA = DIFFERENCE BETWEEN KELVIN AND CELSIUS TEMPERATURES
C     AKAP= (GAS CONSTANT FOR DRY AIR)/(SPECIFIC HEAT AT CONSTANT
C                                       PRESSURE FOR DRY AIR)
C
C     THE ALGORITHM BELOW CAN BEST BE UNDERSTOOD BY REFERRING TO A
C     SKEW-T/LOG P CHART.  IT WAS DEVISED BY HERMAN WOBUS, A MATHEMATI-
C     CIAN FORMERLY AT THE NAVY WEATHER RESEARCH FACILITY BUT NOW 
C     RETIRED.  THE VALUE RETURNED BY SATLFT CAN BE CHECKED BY 
C     REFERRING TO TABLE 78, PP.319-322, SMITHSONIAN METEOROLOGICAL
C     TABLES, BY ROLAND LIST (6TH REVISED EDITION).
C
      IF(P.NE.1000.) GO TO 5
      SATLFT = THW
      RETURN
    5 CONTINUE
C
C     COMPUTE TONE, THE TEMPERATURE WHERE THE DRY ADIABAT WITH VALUE
C     THW (CELSIUS) CROSS P.
C
      PWRP = (P/1000.)**AKAP
      TONE = (THW+CTA)*PWRP-CTA
C
C     CONSIDER THE MOIST ADIABAT EW1 THRU TONE AT P.  USING THE DEFINI-
C     TION OF THE WOBUS FUNCTION (SEE DOCUMENTATION ON WOBF), IT CAN
C     SHOWN THAT EONE = EW1-THW.
C
      EONE = WOBF(TONE)-WOBF(THW)
      RATE = 1.
      GO TO 15
C
C     IN THE LOOP BELOW, THE ESTIMATE OF SATLFT IN ITERATIVELY IMPROVED
C
   10 CONTINUE
C
C     RATE IS THE RATIO OF A CHANGE IN T TO THE CORRESPONDING CHANGE
C     IN E.  ITS INITIAL VALUE WAS SET TO 1 ABOVE.
C
      RATE = (TTWO-TONE)/(ETWO-EONE)
      TONE = TTWO
      EONE = ETWO
   15 CONTINUE
C
C     TTWO IS AN IMPROVED ESTIMATE OF SATLFT.
C
      TTWO = TONE-EONE*RATE
C
C     PT IS THE POTENTIAL TEMPERATURE (CELSIUS) CORRESPONDING TO TTWO AT P
C
      PT = (TTWO+CTA)/PWRP-CTA
C
C     CONSIDER THE MOIST ADIABAT EW2 THRU TTWO AT P. USING THE DEFINI-
C     TION OF THE WOBUS FUNCTION, IT CAN BE SHOWN THAT ETWO = EW2-THW.
C
      ETWO = PT+WOBF(TTWO)-WOBF(PT)-THW
C
C     DLT IS THE CORRECTION TO BE SUBTRACTED FROM TTWO.
C
      DLT = ETWO*RATE
      IF (ABS(DLT).GT.0.1)  GO TO 10
      SATLFT = TTWO-DLT
      RETURN
      END

      REAL FUNCTION WOBF(T)
C
C-------------------------------------------------------------------------
C  LET WBPTS = WET-BULB POTENTIAL TEMPERATURE FOR SATURATED AIR AT
C  TEMPERATURE T (CELSIUS).  LET WBPTD = WET-BULB POTENTIAL TEMPERATURE
C  FOR COMPLETELY DRY AIR AT THE SAME TEMPERATURE T.  THE WOBUS 
C  FUNCTION WOBF (IN DEGREES CELSIUS) IS DEFINED BY
C           WOBF(T) = WBPTS - WBPTD
C  ALTHOUGH WBPTS AND WBPTD ARE FUNCTIONS OF BOTH PRESSURE AND TEMPER-
C  ATURE, THEIR DIFFERENCE IS A FUNCTION OF TEMPERATURE ONLY.
C
C  TO UNDERSTAND WHY, CONSIDER A JPARCEL OF DRY AIR AT TEMPERATURE
C  T AND PRESSURE P.  THE THERMODYNAMIC STATE OF THE PARCEL IS RE-
C  PRESENTED BY A POINT ON A PSUEDOADIABATIC CHART.  THE WET-BULB
C  POTENTIAL TEMPERATURE CURCE (MOIST ADIABAT) PASSING THROUGH THIS
C  POINT IS WBPTS.  NOWT IS THE EQUIVALENT TEMPERATURE FOR ANOTHER
C  PARCEL SATURATED AT SOME LOWER TEMPERATURE TW, BUT AT THE SAME
C  PRESSUER P.  TO FIND TW, ASCEND ALONG THE DRY ADIABAT THROUGH 
C  (T,P).  AT A GREAT HEIGHT, THE DRY ADIABAT AND SOME MOIST ADIABAT
C  WILL NEARLY COINCIDE.  DESCEND ALONG THIS MOIST ADIABAT BACK TO P.
C  THE PARCEL TEMPERATURE IS NOW TW.  THE WET-BULB POTENTIAL TEMPERATURE
C  CURVE (MOIST ADIABAT) THROUGH (TW,P) IS WBPTD.  THE DIFFERENCE
C  (WBPTS-WBPTD) IS PROPORTIONAL TO THE HEAT IMPARTED TO A PARCEL
C  SATURATED AT TEMPERATURE TW IF ALL ITS WATER VAPOR WERE CONDENSED.
C  SINCE THE AMOUNT OF WATER VAPOR A PARCEL CAN HOLD DEPENDS UPON
C  TEMPERATURE ALON, (WBPTD-WBPTS) MUST DEPEND ON TEMPERATURE ALONE.
C
C  THE WOBUS FUNCTION IS USEFUL FOR EVALUATING SEVERAL THERMODYNAMIC
C  QUANTITIES.  BY DEFINATION:
C               WOBF(T) = WBPTS-WBPTD                  (1)
C  IF T IS AT 1000 MB, THEN T IS A POTENTIAL TEMPERATURE PT AND
C  WBPTS =PT.  THUS,
C               WOBF(PT) = PT-WBPTD                    (2)
C  IF T IS AT THE CONDENSATION LEVEL, THEN T IS THE CONDENSATION 
C  TEMPERATURE TC AND WBPTS IS THE WET-BULB POTENTIAL TEMPERATURE
C  WBPT.  THUS,
C               WOBF(TC) = WBPT-WBPTD                  (3)
C  IF WBPTD IS ELIMINATED FROM (2) AND (3), THERE RESULTS
C               WBPT = PT(WOBF(PT) + WOBF(TC)
C  IF WBPTD IS ELIMINATED FROM (1) AND (3), THERE RESULTS
C               WBPTS = PT - WOBF(PT) + WOBF(T)
C
C  IF T IS AN EQUIVALENT POTENTIAL TEMPERATURE EPT (IMPLYING THAT
C  THE AIR AT 1000 MB IS COMPLETELY DRY), THEN WBPTS = EPT AND
C  WBPTD = WBPT.  THUS,
C               WOBF(EPT) = EPT - WBPT
C  THIS FORM IS THE BASIS FOR A POLYNOMIAL APPROXIMATION TO WOBF.
C  IN TABLE 78 ON PP.319-322 OF THE SMITHSONIAN METEOROLOGICAL TABLES
C  BY ROLAND LIST (6TH REVISED EDITION), ONE FINDS WET-BULB POTENTIAL
C  TEMPERATURES AND THE CORRESPONDING EQUIVALENT POTENTIAL TEMPERATURES
C  LISTED TOGETHER.  HERMAN WOBUS, A MATHEMATICIAN FORMERLY AT THE NAVY
C  WEATHER RESEARCH FACILITY, NORFOLK, VA. AND NOW RETIRED, COMPUTED
C  THE COEFFICIENTS FOR THE POLYNOMIAL APPROXIMATION FROM NUMBERS
C  IN THIS TABLE.--- T.W. SCHLATTER, NOAA/ERL/PROFS OFFICE AUG-81
C
C
      X = T-20.
      IF (X.GT.0.0) GO TO 10
      POL = 1.0                + X*(-8.8416605E-03
     1     +X*( 1.4714143E-04  + X*(-9.6719890E-07
     2     +X*(-3.2607217E-08  + X*(-3.8598073E-10)))))
      WOBF = 15.130/POL**4
      RETURN
   10 CONTINUE
      POL = 1.0                + X*( 3.6182989E-03
     1     +X*(-1.3603273E-05  + X*( 4.9618922E-07
     2     +X*(-6.1059365E-09  + X*( 3.9401551E-11
     3     +X*(-1.2588129E-13  + X*( 1.6688280E-16)))))))
      WOBF = 29.930/POL**4+0.96*X-14.8
      RETURN
      END
C
      REAL FUNCTION INTERP(Y1,Y3,X1,X2,X3)
      REAL                 Y1,Y3,X1,X2,X3
      INTERP = Y1 + ( (Y3-Y1) * ( (X2-X1) / (X3-X1) ))
      RETURN
      END
