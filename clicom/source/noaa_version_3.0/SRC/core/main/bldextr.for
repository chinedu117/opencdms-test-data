      PROGRAM BLDEXTR
C
C    THIS PROGRAM READS A DATAEASE ELEMENT EXTREMES RECORD
C       AND WRITES THE INFORMATION TO TO A DIRECT FILE FOR USE BY
C       THE CLICOM QC ROUTINES.
C       THE INPUT DATA IS READ AS AN UNFORMATTED BINARY FILE,
C       READING ALL INPUT INTO THE CHARACTER ARRAY INREC.  THE 
C       VARIABLES WITHIN THE RECORD ARE EQUIVALENCED TO THE 
C       APPROPRIATE POSITION WITHIN INREC.
C
C  LOCAL VARIABLES
      CHARACTER*78 MSGLN1, MSGLN2, MSGLN3
      CHARACTER*75 INREC2
      CHARACTER*32 INFILE,OUTFIL
      CHARACTER*24 SRTKEY
      CHARACTER*16 DDISK
      CHARACTER*22 FILNAME
      CHARACTER*14 MSGTX2
      CHARACTER*8 STNID,MSGTXT ,INMAXDATE,INMINDATE,CHRMEAN,CHRSD
      CHARACTER*4 INBYEAR,INEYEAR,NULL4,CHRMAX,CHRMIN,CHRCHG
      CHARACTER*3 INELEM
      CHARACTER*2 RDISK,CHRYEAR
      CHARACTER*1 INREC(75),NULL,JCHAR
      CHARACTER*1 INMONTH,INOBS,INSOURCE,INMAXFLAG,INMINFLAG
      INTEGER*2 ICHK,IOBS 
      INTEGER*2 HEADER(8)
      INTEGER*2 ELEM,MONTH,SOURCE,BYEAR,EYEAR,YEARS
     +         , MAXFLAG, MINFLAG
      REAL*4    MAXVALUE,MINVALUE,MAXCHNG
      INTEGER*4 MAXDATE,MINDATE,INULL,NUMREC,I1,NCOUNT
      REAL*8 MEANVALUE,STNDRDDEV
C
C    EQUIVALENCE THE INPUT VARIABLES TO THE INPUT RECORD STRING
C
      EQUIVALENCE (HEADER,INREC(1)),(STNID,INREC(6)),
     +            (INOBS,INREC(14)),(INELEM,INREC(15)),
     +            (INMONTH,INREC(18)),(INSOURCE,INREC(19)),
     +            (INBYEAR,INREC(20)),(INEYEAR,INREC(24)),
     +            (INMAXDATE,INREC(34)),(INMAXFLAG,INREC(42)),
     +            (INMINDATE,INREC(47)),(INMINFLAG,INREC(55))

      EQUIVALENCE (INREC(1),INREC2),(INULL,NULL4),(CHRYEAR,YEARS)
     +           ,(CHRMAX,MAXVALUE),(CHRMIN,MINVALUE)
     +           ,(CHRMEAN,MEANVALUE),(CHRSD,STNDRDDEV)
     +           ,(CHRCHG,MAXCHNG)
     
      NULL = CHAR(0)
      INULL = 0
C
      CALL GETMSG(207,MSGLN1)
      CALL GETMSG(208,MSGLN2)
      CALL GETMSG(209,MSGLN3)
      CALL GETMSG(999,MSGLN3)
      WRITE(*,*) MSGLN1
      WRITE(*,*) MSGLN2
      WRITE(*,*) ' ' 
C
C   FIND THE NAME OF THE DATAEASE STN ELEMENT EXTREMES FILE
C
      CALL FNDFIL('STN ELEMENT EXTREMES    ',FILNAME,NUMREC)
      IF (FILNAME.EQ.'      ') THEN
         STOP
      END IF
      LEND = 16
      CALL GETDSC(RDISK,DDISK,LEND)
      INFILE(1:LEND) = DDISK
      ILEN = LEND + 13
      INFILE(LEND+1:LEND+1) = '\'
      INFILE(LEND+2:ILEN) = FILNAME(3:14)
C
C   SORT THE STN ELEMENT FILE
C   SORT KEYS: 1--STNID+OBSTYPE  2--MONTH  3--ELEMENT CODE
C
      CALL COLOAD
      SRTKEY = '3,A,6,9,A,18,1,A,15,3'
      OUTFIL = '  \DATA\TMP1.TMP'
      OUTFIL(1:2) = RDISK
      OPEN (63,FILE=OUTFIL,STATUS='UNKNOWN',FORM='BINARY',IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         CALL OPENMSG(OUTFIL,'BLDEXTR-1   ',IOCHK)
         STOP ' '
      ELSE
         CLOSE(63)
      END IF
      IF (NUMREC.GT.0) THEN
         CALL SORT (INFILE,ILEN,OUTFIL,16,75,SRTKEY)
      END IF
C
      WRITE(*,*) MSGLN3 
    5 CONTINUE
      OPEN(28,FILE=OUTFIL,STATUS='OLD',FORM='BINARY',IOSTAT=IOCHK)
      IF(IOCHK.NE.0) THEN
         CALL OPENMSG(OUTFIL,'BLDEXTR     ',IOCHK)
         GO TO 5
      ENDIF
C
      OPEN(31,FILE='P:\DATA\ELEM.LIM',STATUS='OLD',
     +         ACCESS='DIRECT',RECL=62,IOSTAT=IOCHK)
      IF (IOCHK.EQ.0) THEN
C          .. DELETE FILE IF IT EXISTS
         CLOSE(31,STATUS='DELETE')
      ELSE IF (IOCHK.NE.6416) THEN
C          .. EXIT PROGRAM IF ERROR IS ANYTHING EXCEPT 'FILE NOT FOUND'
         CALL OPENMSG('P:\DATA\ELEM.LIM','BLDEXTR     ',IOCHK)
         STOP ' '
      END IF
C       .. START WITH AN EMPTY FILE      
      OPEN(31,FILE='P:\DATA\ELEM.LIM',STATUS='NEW',
     +         ACCESS='DIRECT',RECL=62)

      NCOUNT = 0
      ICHK = 0
      DO 100 I1 = 2,999999
   10    CONTINUE
         READ(28,END=200) INREC
         IF(HEADER(1).EQ.13 .OR. HEADER(1).EQ.15) GO TO 10
         DO 20 I2 = 1,8
            IF (STNID(I2:I2).EQ.NULL) THEN
               STNID(I2:I2) = ' '
            END IF
   20    CONTINUE
         IOBS = ICHAR(INOBS)
         READ(INELEM,'(I3)',ERR=30) ELEM 
         GO TO 40
   30    CONTINUE
         WRITE(MSGTXT,'(4X,I4)') NCOUNT
         MSGTXT(1:3) = INELEM
         MSGTXT(4:4) = ','
         CALL WRTMSG(4,109,12,1,1,MSGTXT,8) 
         ELEM = -9999
   40    CONTINUE 
         MONTH = ICHAR(INMONTH)
         SOURCE = ICHAR(INSOURCE)
         JCHAR = INBYEAR(2:2)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128.OR.INBYEAR.EQ.NULL4) THEN
            BYEAR = -9999
         ELSE
            READ(INBYEAR,'(I4)',ERR=900) BYEAR
         END IF 
         JCHAR = INEYEAR(2:2)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128.OR.INEYEAR.EQ.NULL4) THEN
            EYEAR = -9999
         ELSE
            READ(INEYEAR,'(I4)',ERR=900) EYEAR
         END IF
         CHRYEAR = INREC2(28:29)
         JCHAR = CHRYEAR(2:2)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128.OR.YEARS.EQ.0) THEN
            YEARS = -9999
         END IF 
         CHRMAX = INREC2(30:33)
         JCHAR = CHRMAX(4:4)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128) THEN
            MAXVALUE = -9999.
            MAXDATE = -9999
            MAXFLAG = -9999
         ELSE
            MAXFLAG = 0
            MAXFLAG = ICHAR(INMAXFLAG)
            READ(INMAXDATE,'(I8)',ERR=50) MAXDATE
            GO TO 60
   50       MAXDATE = -9999
            MAXFLAG = -9999
   60       CONTINUE
         END IF
         CHRMIN = INREC2(43:46)
         JCHAR = CHRMIN(4:4)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128) THEN
            MINVALUE = -9999.
            MINDATE = -9999
            MINFLAG = -9999
         ELSE
            MINFLAG = ICHAR(INMINFLAG)
            READ(INMINDATE,'(I8)',ERR=70) MINDATE
            GO TO 80
   70       MINDATE = -9999
            MINFLAG = -9999
   80       CONTINUE
         END IF
         CHRMEAN = INREC2(56:63)
         JCHAR = CHRMEAN(8:8)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128) THEN
            MEANVALUE = -9999.
         END IF
         CHRSD = INREC2(64:71)
         JCHAR = CHRSD(8:8)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128) THEN
            STNDRDDEV = -9999.
         END IF
         CHRCHG = INREC2(72:75)
         JCHAR = CHRCHG(4:4)
         ICHK = ICHAR(JCHAR)
         IF (ICHK.EQ.128) THEN
            MAXCHNG = -9999
         END IF
         NCOUNT = NCOUNT + 1
         WRITE(31,REC=I1) STNID,IOBS,MONTH,ELEM,SOURCE,BYEAR,
     +        EYEAR,YEARS,MAXVALUE,MAXDATE,MAXFLAG,MINVALUE,MINDATE,
     +        MINFLAG,MEANVALUE,STNDRDDEV,MAXCHNG
  100 CONTINUE
  200 CONTINUE
      WRITE(31,REC=1) NCOUNT  
      CLOSE(28,STATUS='DELETE')
      CLOSE(31)
      WRITE(*,250) NCOUNT
  250 FORMAT(/,I5,' Stn Element Extremes Records Written',//)
      STOP ' '
  900 CONTINUE
      MSGTX2 = STNID
      MSGTX2(10:12) = INELEM
      CALL WRTMSG(4,97,12,1,0,MSGTX2,14)
      WRITE(*,*) INBYEAR, INEYEAR
      STOP ' '
      END
