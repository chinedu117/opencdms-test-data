$STORAGE:2
C
C     PROGRAM WRTDEKEY
C
C     ** OBJECTIVE:  PROGRAM CONSTRUCTS THE KEYSTROKE TABLE THAT USE TO
C                    RE-INDEX THE DATAEASE CLIMATE DATA FORMS FOR THE CLICOM
C                    SYSTEM.  BASED ON THE DATA-TYPE TO BE INDEXED THE PROGRAM
C                    WILL WRITE INTO A FILE WITH APPRORIATE KEYTROKES NEED TO
C                    DRIVE THE INDEXING.
C                    
C     ** NOTE: **    THIS PROGRAM MUST BE PASSED THE ABBREVIATION FOR THE TYPE 
C                    OF DATA (MLY,DLY...ect) TO BE INDEXED ON THE COMMAND LINE
C                    (e.g. : WRTDEKEY DLY).  IF AN ARGUMENT IS NOT PASSED, OR
C                    AN INVALID ARGUMENT IS PASSED, THE PROGRAM WILL TERMINATE
C                    WITH A DOS ERRORLEVEL 2, AND THE RE-INDEXING WILL NOT 
C                    PERFORMED.
C
      INTERFACE TO SUBROUTINE CMDLIN(ADDRES,LENGTH,RESULT)
      INTEGER*4 ADDRES[VALUE],LENGTH[VALUE]
      CHARACTER*1 RESULT
      END
C
C
      PROGRAM WRTDEKEY
C
      CHARACTER*1  CHARCODE(450)
      INTEGER*1    ICHRCODE(450)
      CHARACTER*3  RECTYPE,DATATYPE,DEVERS,TBLRECTYP(10)
      CHARACTER*22 FILENAME
      CHARACTER*30 MSGTXT
      INTEGER*2    NUMCHAR
C
      CHARACTER*64 RESULT
      INTEGER*4    PSP,PSPNCHR,OFFSET
      DATA TBLRECTYP/'MLY','10D','DLY','SYN','HLY','15M','U-A','NML',
     +               'DLX','10X'/
C
C     *** FOLLOWING STATEMENT REQUIRED FOR FORTRAN 3.3 - NOT ALLOWED IN FTN 4
c
C         EXTERNAL WRTDEKEY
C
C     *** LOCATE SEGMENTED ADDRESS OF THE BEGINNING OF THIS PROGRAM
C
      OFFSET = #00100000
      PSP = LOCFAR(WRTDEKEY)
C
C     *** COMPUTE THE BEGINNING OF THE PROGRAM SEGMENT PREFIX (PSP)
C
      PSP = (PSP - MOD(PSP,#10000)) - OFFSET 
C
C     *** LOCATE POSITION OF COMMAND PARAMETERS WITHIN THE PSP
C
      PSPNCHR = PSP + #80
      PSP = PSP + #81
C
C     *** PASS THE ADDRESS OF THE COMMAND PARAMTERS TO CMDLIN WHICH DECODES
C         THE COMMAND AND RETURNS IT AS RESULT.
C
      CALL CMDLIN(PSP,PSPNCHR,RESULT)
C
C     *** PULL THE COMMAND (REC-TYPE) OUT OF THE RESULT
C
      IF (RESULT(1:3).EQ.'   ') THEN
          CALL LOCATE(24,0,IERR)
          STOP 2
      END IF
      RECTYPE = RESULT(1:3)
C
C     *** CONVERT LOWERCASE LETTERS TO UPPERCASE LETTERS
C
      DO 10 I=1,3
         IF (RECTYPE(I:I).LT.'a' .OR. RECTYPE(I:I).GT.'z') GO TO 10
         IVAL = ICHAR(RECTYPE(I:I)) - 32
         RECTYPE(I:I) = CHAR(IVAL)
  10  CONTINUE            
      DO 20 I = 1,10
         IF (RECTYPE.EQ.TBLRECTYP(I)) THEN
            GO TO 30
         END IF
  20  CONTINUE
      CALL CLS
      CALL WRTMSG(3,299,12,1,0,'WRTDEKEY',8)
      CALL WRTMSG(2,298,12,0,1,': WRTDEKEY DLY)',15)
      CALL LOCATE(24,0,IERR)
      STOP 2
  30  CONTINUE
C 
C     *** DETERMINE WHICH VERSION OF DATAEASE IS IN USE AND OPEN THE KEYSTROKE
C         TABLE FILE TO BE USED FOR RE-INDEXING.
C
      CALL GETDEASE(DEVERS)
      IF (DEVERS.EQ.'4.0') THEN
C
C     *** OPEN THE KEY STROKES FILE FOR DEASE VERSION 4.0 
C
  40     CONTINUE
         FILENAME='P:\DATA\DE4KEYS.DAT'
         OPEN (67,FILE=FILENAME,STATUS='OLD',FORM='FORMATTED',
     +         IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL CLS
            CALL OPENMSG('P:\DATA\DE4KEYS.DAT    ','WRTDEKEY    ',IOCHK)
            GO TO 40
         END IF
      ELSE
C
C     *** OPEN THE KEY STROKES FILE FOR DEASE VERSION 2.5
C
  50     CONTINUE
         FILENAME='P:\DATA\DE2KEYS.DAT'
         OPEN (67,FILE=FILENAME,STATUS='OLD',FORM='FORMATTED',
     +         IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL CLS
            CALL OPENMSG('P:\DATA\DE2KEYS.DAT    ','WRTDEKEY    ',IOCHK)
            GO TO 50
         END IF
      END IF
C
C     *** SKIP THE LINES WHICH ARE COMMENTS IN THE FILE
C
      DO 60 I = 1,9
         READ(67,'(A60)',ERR=300)
  60  CONTINUE
C
C     *** FIND THE KEYSTROKES TABLE FOR THE DATATYPE TO BE INDEXED
C
      DO 70 I = 1,10
      READ(67,*,ERR=300)DATATYPE,NUMCHAR  
      IF (DATATYPE(1:3).EQ.RECTYPE(1:3)) THEN
          GO TO 80
      END IF  
C
C     *** DETERMINE THE NUMBER OF LINES NEED TO SKIP
C
      IF (MOD(NUMCHAR,20).NE.0) THEN
          NLINE = (NUMCHAR/20) + 1
      ELSE
          NLINE = NUMCHAR/20
      END IF
      DO 75 K = 1,NLINE
         READ(67,'(A60)',ERR=300)
  75  CONTINUE
  70  CONTINUE
  80  CONTINUE
      READ(67,'(20(Z2,1X))',ERR=300)(ICHRCODE(J),J=1,NUMCHAR)
C
C     *** OPEN THE TEMPORARY OUTPUT FILE AND WRITE NECESSARY KEY STROKES TO
C         DRIVE THE INDEXING OF DATA TYPE WANTED.
C
      FILENAME='P:\DATA\INDXKEYS.DAT'
      OPEN (68,FILE=FILENAME,STATUS='NEW',ACCESS='DIRECT',
     +      FORM='UNFORMATTED',RECL=NUMCHAR,IOSTAT=IOCHK) 
      IF(IOCHK.NE.0) THEN
         CALL CLS
         WRITE(MSGTXT,'(A20,2X,I4)') FILENAME,IOCHK
         CALL WRTMSG(4,157,12,1,1,MSGTXT,26)
         CALL LOCATE(24,0,IERR)
         STOP 2            
      END IF
      DO 150 K=1,NUMCHAR
         CHARCODE(K)=CHAR(ICHRCODE(K))
  150 CONTINUE
      WRITE(68)(CHARCODE(J),J=1,NUMCHAR)
      CLOSE(68)
      CLOSE(67)
      STOP ' '
C
C     *** ERROR READING FILE ***
C  
  300 CONTINUE
      CALL CLS
      CALL WRTMSG(4,191,12,1,1,FILENAME,22)
      CALL LOCATE(24,0,IERR)
      STOP 2            
      END
