$STORAGE:2
      SUBROUTINE BGNPLT(GANWLF,GANWRT,GANWBT,GANWTP,XSCALE,YSCALE
     +                 ,XORIGIN,YORIGIN) 
C
C       ** OBJECTIVE:  SET WORLD COORDINATES SO THAT PLOTTING AREA IS 
C                      MAPPED TO (XMIN,YMIN),(XMAX,YMAX)
C                      SAVE PLOT SPECIFICATIONS.
C
C       ** INPUT:
C             GANWLF........X-COORDINATE OF LEFT EDGE OF PLOT AREA
C             GANWRT........X-COORDINATE OF RIGHT EDGE OF PLOT AREA
C             GANWBT........Y-COORDINATE OF BOTTOM EDGE OF PLOT AREA
C             GANWTP........Y-COORDINATE OF TOP EDGE OF PLOT AREA
C             XSCALE(1).....MINIMUM VALUE OF X
C                   (2).....MAXIMUM VALUE OF X
C             YSCALE(1).....MINIMUM VALUE OF Y
C                   (2).....MAXIMUM VALUE OF Y
C             XORIGIN.......VALUE OF X AT THE Y AXIS
C             YORIGIN.......VALUE OF Y AT THE X AXIS
C
C       ** NOTE:  GRAPH AXIS(GA) COORDINATES ARE GIVEN IN NORMALIZED WORLD
C                 COORDINATES.  THE VALUES FOR THE VIEWPORT CAN RANGE
C                 BETWEEN 0 AND 1.  THE PLOT AREA IS A PORTION OF THE
C                 VIEWPORT. 
C
C     
      REAL *4 GANWLF,GANWRT,GANWBT,GANWTP,XSCALE(2),YSCALE(2)  
     +       ,XORIGIN,YORIGIN  
C
$INCLUDE:  'PLTSPEC.INC'
C
C       ** SAVE PLOT SPECIFICATIONS
C
      XMIN = XSCALE(1)
      XMAX = XSCALE(2)
      YMIN = YSCALE(1)
      YMAX = YSCALE(2)
      XORG = XORIGIN
      YORG = YORIGIN     
      TICLBW = 0.
C      
C       ** SCALE AND SET WORLD COORDINATES
C
C       ** CALCULATE THE SIZE OF THE PLOTTING AREA 
C
      XPLOT = GANWRT - GANWLF
      YPLOT = GANWTP - GANWBT          
C
C       ** SCALE WORLD COORDINATES SO THAT PLOTTING AREA IS MAPPED
C          TO (XMIN,YMIN),(XMAX,YMAX)
C
      XW1 = XMIN - (GANWLF*(XMAX-XMIN)/XPLOT)      
      XW2 = XMAX + ((1.-GANWRT)*(XMAX-XMIN)/XPLOT)      
      YW1 = YMIN - (GANWBT*(YMAX-YMIN)/YPLOT)      
      YW2 = YMAX + ((1.-GANWTP)*(YMAX-YMIN)/YPLOT)      
C
C       ** SET WORLD COORDINATES
C
      CALL SETWOR(XW1,YW1,XW2,YW2)
C
      RETURN
      END      
      SUBROUTINE GETWASP(WASP)
C
C       ** OBJECTIVE:  CALCULATE WORLD ASPECT RATIO -- USED TO SCALE AN 
C                      X-DISTANCE TO AN EQUIVALENT Y-DISTANCE
C
C       ** OUTPUT:
C             WASP....WORLD ASPECT RATIO 
C      
C **DEBUG 3-28-94
      COMMON/DEVHNDL/IHNDLSCR,IHNDLVRI,SCRNASP,VRIASP,DEVASP
      INTEGER *2 HORPIX,VERPIX
C      
      CALL INQASP(ASP)
      CALL INQDRA(HORPIX,VERPIX)
      CALL INQWOR(XW1,YW1,XW2,YW2)
      CALL INQVIE(XD1,YD1,XD2,YD2)
      WASP = (XD2-XD1)/(YD2-YD1)
      WASP = WASP * (YW2-YW1)/(XW2-XW1)
      WASP = WASP *DEVASP*FLOAT(HORPIX+1)/FLOAT(VERPIX+1)      
C
      RETURN
      END

      