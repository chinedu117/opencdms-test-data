$STORAGE:2
      SUBROUTINE PLTTITL(TITL,NLTITL,TLLOC,TLFONT,TLHTNW,TLASP,TLCLR)
C
C       ** OBJECTIVE:  DRAW TITLE, SUBTITLE, AND FREE TEXT 
C
C       ** INPUT:
C             TITL.......TEXT FOR TITLE/SUBTITLE/FREE TEXT
C             NLTITL.....ID NUMBER OF TITLE LINE 
C                        1=TITLE LINE
C                        2=SUBTITLE LINE
C                        3=FREE TEXT LINE
C             TLLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TITLE 
C                        A VALUE OF -99999. INDICATES DEFAULT VALUES
C                  (2)...Y-COORDINATE OF BOTTOM CENTER OF TITLE 
C             TLFONT.....FONT ID
C             TLHTNW.....HEIGHT OF TITLE CHARACTERS IN NORMALIZED WORLD
C                        COORDINATE UNITS
C             TLASP......ASPECT RATIO OF TITLE CHARACTERS
C             TLCLR......COLOR ID OF TITLE CHARACTERS
C       ** OUTPUT:
C             TLLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TITLE 
C                  (2)...Y-COORDINATE OF BOTTOM CENTER OF TITLE 
C       ** NOTE:  VALUES ARE RETURNED FOR TLLOC ONLY IF DEFAULT VALUES
C                 WERE CALCULATED.  COORDINATES OF TLLOC ARE IN NORMALIZED
C                 WORLD COORDINATES.
C
$INCLUDE:  'PLTSPEC.INC'
C
      CHARACTER *(*) TITL
      REAL *4 TLLOC(2),TLHTNW
      INTEGER *2 TLFONT,TLCLR
C      
      XCN = TLLOC(1)
      YCN = TLLOC(2)
C
C       ** ELIMINATE FREE TEXT THAT IS BLANK
C
      IF (NLTITL.EQ.3) THEN
         IF (TITL.EQ.' ') THEN
            GO TO 100
         ENDIF   
         LIMPOSN=1
      ELSE
         LIMPOSN=3   
      ENDIF      
C
C       ** DEFINE ATTRIBUTES OF TITLE CHARACTERS 
C
      ANG = 0.
      CALL DEFHST(TLFONT,TLCLR,ANG,TLASP,TLHTNW,TLHTW)
C
C       ** GET POSITION OF CURRENT TITLE LINE IN WORLD COORDINATE UNITS
C
      IF (XCN.EQ.-99999.) THEN
C
C          .. CALCULATE DEFAULT POSITION
         CALL INQWOR(XW1,YW1,XW2,YW2)
         IF (NLTITL.EQ.3) THEN
C             .. FREE TEXT       
            XCW = XMIN + .5*(XMAX-XMIN) 
            YCW = YMIN + .5*TLHTW
         ELSE
C             .. TITLE AND SUBTITLE         
            XCW = XMIN + .5*(XMAX-XMIN)
            YCW = YW2-.5*(YW2-YMAX)
            IF (NLTITL.EQ.2) YCW=YCW-TLHTW
         ENDIF   
C
C          .. SAVE X,Y POSITION OF CURRENT TITLE LINE      
         CALL W2NW(XCW,YCW,TLLOC(1),TLLOC(2))
      ELSE
C
C          .. USER SPECIFIED POSITION
C             CONVERT NORMALIZED WORLD TO WORLD COORDINATE UNITS      
         CALL NW2W(XCN,YCN,XCW,YCW)   
      ENDIF
C
C       **  PLOT CURRENT TITLE LINE
C      
      CALL DRWTXT(TITL,XCW,YCW,LIMPOSN,ANG)
C
  100 RETURN
      END
      SUBROUTINE PLTXMGN(MGNTXT,ISIDE,TXTLOC,TXTFONT,TXTHTNW,
     +                   TXTASP,TXTCLR)
C
C       ** OBJECTIVE:  DRAW MARGIN TEXT AT TOP OR BOTTOM OF PLOT
C
C       ** INPUT:
C             MGNTXT......MARGIN TEXT
C             ISIDE.......+1=DRAW MARGIN TEXT AT TOP OF PLOT
C                         +3=DRAW MARGIN TEXT AT BOTTOM OF PLOT
C             TXTLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TEXT 
C                         A VALUE OF -99999. INDICATES DEFAULT VALUES
C                   (2)...Y-COORDINATE OF BOTTOM CENTER OF TEXT 
C             TXTFONT.....FONT ID FOR MARGIN CHARACTERS
C             TXTHTNW.....HEIGHT OF MARGIN CHARACTERS IN NORMALIZED WORLD
C                         COORDINATE UNITS
C             TXTASP......ASPECT RATIO OF MARGIN CHARACTERS
C             TXTCLR......COLOR ID OF MARGIN CHARACTERS
C       ** OUTPUT:
C             TXTLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TEXT 
C                   (2)...Y-COORDINATE OF BOTTOM CENTER OF TEXT 
C       ** NOTE:  VALUES ARE RETURNED FOR TXTLOC ONLY IF DEFAULT VALUES WERE
C                 CALCULATED.  COORDINATES OF TXTLOC ARE IN 
C                 NORMALIZED WORLD COORDINATE UNITS.
C
C
$INCLUDE:  'PLTSPEC.INC'
C
      REAL *4 TXTLOC(2)
      INTEGER *2 TXTFONT,TXTCLR  
      CHARACTER *(*) MGNTXT
C
C       ** DEFINE ATTRIBUTES OF MARGIN CHARACTERS 
C
      ANG = 0.
      CALL DEFHST(TXTFONT,TXTCLR,ANG,TXTASP,TXTHTNW,TXTHTW)
C
C       ** SET X AND Y POSITION OF MARGIN TEXT
C
      XCN = TXTLOC(1)
      YCN = TXTLOC(2)
      YHT  = TXTHTW
      IF (XCN.EQ.-99999.) THEN
C
C          .. DEFAULT X POSITION -- CENTER MARGIN TEXT IN X DIRECTION        
         XCW = XMIN + .5*(XMAX-XMIN)
         IF (ISIDE.EQ.1) THEN
C
C             .. ISIDE=1 -- DEFAULT Y POSITION FOR TOP MARGIN TEXT
            YCW = YMAX+2.*YHT
         ELSE   
C
C             .. ISIDE=3 -- DEFAULT Y POSITION FOR BOTTOM MARGIN TEXT
            YCW = YMIN-3.*YHT
         ENDIF
C
C          .. SAVE X,Y POSITION OF MARGIN TEXT      
         CALL W2NW(XCW,YCW,TXTLOC(1),TXTLOC(2))
      ELSE
C
C          .. USER SPECIFIED POSITION
C             CONVERT NORMALIZED WORLD TO WORLD COORDINATE UNITS      
         CALL NW2W(XCN,YCN,XCW,YCW)
      ENDIF
C
C       ** PLOT MARGIN TEXT
C
      CALL DRWTXT(MGNTXT,XCW,YCW,3,ANG)
C
      RETURN
      END
      SUBROUTINE PLTYMGN(MGNTXT,ISIDE,TXTLOC,TXTFONT,TXTHTNW,
     +                   TXTASP,TXTCLR) 
C
C       ** OBJECTIVE:  DRAW MARGIN TEXT TO LEFT OR RIGHT OF PLOT.  TEXT IS
C                      ROTATED 90 DEGREES.
C
C       ** INPUT:
C             MGNTXT...... MARGIN TEXT
C             ISIDE.......+2=DRAW MARGIN TEXT TO RIGHT OF PLOT
C                         +4=DRAW MARGIN TEXT TO LEFT OF PLOT
C             TXTLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TEXT 
C                   (2)...Y-COORDINATE OF BOTTOM CENTER OF TEXT 
C                         A VALUE OF -99999. INDICATES DEFAULT VALUES
C             TXTFONT.....FONT ID FOR MARGIN CHARACTERS
C             TXTHTNW.....HEIGHT OF MARGIN CHARACTERS IN NORMALIZED WORLD
C                         COORDINATE UNITS
C             TXTASP......ASPECT RATIO OF MARGIN CHARACTERS
C             TXTCLR......COLOR ID OF MARGIN CHARACTERS
C       ** OUTPUT:
C             TXTLOC(1)...X-COORDINATE OF BOTTOM CENTER OF TEXT 
C                   (2)...Y-COORDINATE OF BOTTOM CENTER OF TEXT 
C       ** NOTE:  VALUES ARE RETURNED FOR TXTLOC ONLY IF DEFAULT VALUES WERE
C                 CALCULATED.  COORDINATES OF TXTLOC ARE IN 
C                 NORMALIZED WORLD COORDINATE UNITS.
C
$INCLUDE:  'PLTSPEC.INC'
C
      REAL *4 TXTLOC(2)
      INTEGER *2 TXTFONT,TXTCLR
      CHARACTER *(*) MGNTXT
C
C       ** DEFINE ATTRIBUTES OF MARGIN CHARACTERS 
C
      ANG = 90.
      CALL DEFHST(TXTFONT,TXTCLR,ANG,TXTASP,TXTHTNW,TXTHTW)
C
C       ** SET X AND Y POSITION OF MARGIN TEXT
C
      XCN = TXTLOC(1)
      YCN = TXTLOC(2)
      XHT  = TXTHTW
      IF (YCN.EQ.-99999.) THEN
C
C          .. DEFAULT Y POSITION -- CENTER MARGIN TEXT IN Y DIRECTION        
         YCW = YMIN + .5*(YMAX-YMIN)   
      ELSE
C
C          .. USER SPECIFIED Y POSITION
C             CONVERT NORMALIZED WORLD TO WORLD COORDINATE UNITS      
         CALL NW2W(0.,YCN,TEMP,YCW)
      ENDIF
      IF (XCN.EQ.-99999.) THEN
         TICWID=TICLBW
         IF (TICWID.EQ.0) TICWID=XHT
         IF (ISIDE.EQ.2) THEN
C
C             .. ISIDE=2 -- DEFAULT X POSITION FOR RIGHT MARGIN TEXT
            XCW = XMAX+TICLBW+1.50*XHT
         ELSE   
C
C             .. ISIDE=4 -- DEFAULT X POSITION FOR LEFT MARGIN TEXT
            XCW = XMIN-(TICLBW+.50*XHT)
         ENDIF
      ELSE
C
C          .. USER SPECIFIED X POSITION
C             CONVERT NORMALIZED WORLD TO WORLD COORDINATE UNITS      
         CALL NW2W(XCN,0.,XCW,TEMP)
      ENDIF
      IF (XCN.EQ.-99999. .OR. YCN.EQ.-99999.) THEN
C
C          .. SAVE X,Y POSITION OF MARGIN TEXT      
         CALL W2NW(XCW,YCW,TXTLOC(1),TXTLOC(2))
      ENDIF   
C
C       ** PLOT MARGIN TEXT
C      
      CALL DRWTXT(MGNTXT,XCW,YCW,3,ANG)
C
      RETURN
      END
