$STORAGE:2

      SUBROUTINE RDLOC(XPOS,YPOS,RTNCHAR,RTNCODE)
C
C   ROUTINE READS THE KEYBOARD AND/OR THE MOUSE AND RETURNS THE
C   CURRENT X Y COORDINATES SET BY THEM.  IF A NON-ARROW KEY IS
C   PRESSED IT IS RETURNED AS RTNCHAR.  MOUSE BUTTONS 1 (LEFT) AND 
C   2 (RIGHT) ARE INTERPRETED AS ENTER AND F4/ESC RESPECTIVELY
C
C   -----> ROUTINE WORKS IN HALO GRAPHICS MODES ONLY <-----
C
C   PASSED VARIABLES: 
C
C      XPOS,YPOS...INPUT  - STARTING POINTER POSITION
C                  OUTPUT - FINAL POINTER POSITION
C      RTNCHAR.....OUTPUT - KEYBOARD CHARACTER ENTERED
C      RTNCODE.....OUTPUT - '0' IF KEYBOARD, '1' IF MOUSE INPUT
 
      REAL XLOW,XHIGH,YLOW,YHIGH,XPOS,YPOS
      CHARACTER*1 RTNCODE
      CHARACTER*2 INCHAR, RTNCHAR,BUTTON
      LOGICAL FIRSTCALL,MOUSE
      DATA FIRSTCALL /.TRUE./, BUTTON /'XX'/
C
C   DETERMINE THE CURRENT WORLD COORDINATE LIMITS AND SET CURSOR 
C   MOVEMENT FACTOR TO 1/200TH OF THE WORLD.
C
      CALL INQWOR(XLOW,YLOW,XHIGH,YHIGH)
      YFACTOR = (YHIGH - YLOW) / 200.
      XFACTOR = (XHIGH - XLOW) / 200.
C
C   INITIALIZE MOUSE IF THIS IS THE FIRST CALL TO THIS ROUTINE
C
      IF (FIRSTCALL) THEN
         FIRSTCALL = .FALSE.
         CALL SETLOC(2,8)
         CALL ORGLOC((XHIGH-XLOW)/2.,(YHIGH-YLOW)/2.)
         CALL READLO(XPOS1,YPOS1,ISTAT)
         CALL MAPWTD(XPOS1,YPOS1,IPOS,JPOS)
         IF (IPOS.EQ.0.AND.JPOS.EQ.0) THEN
            MOUSE = .FALSE.
         ELSE
            MOUSE = .TRUE.
         ENDIF
         CALL ORGLOC(XPOS,YPOS)
      ENDIF     
C
C   CHECK FOR KEYBOARD CHARACTER - CALCULATE NEW POSITION IF AN ARROW
C   KEY IS ENTERED.
C
      RTNCHAR = 'XX'
      CALL KEYCHK(KEYHIT)
      IF (KEYHIT.EQ.1) THEN
         RTNCODE = '0'
         CALL GETCHAR(1,INCHAR)
         YCOL = XPOS
         YROW = YPOS
         IF (INCHAR.EQ.'UA') THEN
            YROW = YROW + YFACTOR
         ELSE IF (INCHAR.EQ.'DA') THEN
            YROW = YROW - YFACTOR
         ELSE IF (INCHAR.EQ.'LA') THEN
            YCOL = YCOL - XFACTOR
         ELSE IF (INCHAR.EQ.'RA') THEN
            YCOL = YCOL + XFACTOR
         ELSE IF (INCHAR.EQ.'UP') THEN
            YROW = YROW + YFACTOR
            YCOL = YCOL + XFACTOR
            RTNCHAR = 'YY'
         ELSE IF (INCHAR.EQ.'DP') THEN
            YROW = YROW - YFACTOR
            YCOL = YCOL + XFACTOR
            RTNCHAR = 'YY'
         ELSE IF (INCHAR.EQ.'HO') THEN
            YROW = YROW + YFACTOR
            YCOL = YCOL - XFACTOR
            RTNCHAR = 'YY'
         ELSE IF (INCHAR.EQ.'EN') THEN
            YROW = YROW - YFACTOR
            YCOL = YCOL - XFACTOR
            RTNCHAR = 'YY'
         ELSE
            RTNCHAR = INCHAR
         ENDIF
C
C     CHECK THAT NEW LOCATION IS WITHIN WORLD COORDINATE LIMITS
C
         IF (YCOL.LT.XLOW) THEN
            YCOL = XLOW
         ELSE IF (YCOL.GT.XHIGH) THEN
            YCOL = XHIGH
         ENDIF
         IF (YROW.LT.YLOW) THEN
            YROW = YLOW
         ELSE IF (YROW.GT.YHIGH) THEN
            YROW = YHIGH
         ENDIF
C
C     MOVE MOUSE POINTER LOCATION TO NEW POSITION SET FROM KEYBOARD
C
         IF (YCOL.NE.XPOS.OR.YROW.NE.YPOS) THEN
            CALL ORGLOC(YCOL,YROW)
            XPOS = YCOL
            YPOS = YROW
         ENDIF
C
C     IF KEYBOARD BUFFER CONTAINS ANOTHER CHARACTER FLUSH THE BUFFER
C
50       CONTINUE
            CALL KEYCHK(KEYHIT)
            IF (KEYHIT.EQ.1) THEN
               CALL GETCHAR(1,INCHAR)
               GO TO 50
            ENDIF
C
C   NO INPUT FROM KEYBOARD - READ LOCATION FROM THE MOUSE
C
      ELSE IF (MOUSE) THEN
         RTNCODE = '1'
         RTNCHAR = 'XX'
         CALL READLO(XPOS,YPOS,ISTAT)
C
C      INTERPRET MOUSE BUTTON 1 (LEFT) AS ENTER, BUTTON 2 (RIGHT) AS F4/ESC.
C      ONLY REGISTER BUTTONS AS PUSHED AFTER THEY ARE RELEASED.
C
         IF (ISTAT.GT.127) THEN
            IF (BUTTON.NE.'XX') THEN
               IF(IAND(ISTAT,4).NE.4.AND.IAND(ISTAT,2).NE.2) THEN
                  RTNCHAR = BUTTON
                  BUTTON = 'XX'
               ENDIF   
            ELSE
               IF(IAND(ISTAT,4).EQ.4) THEN
                  BUTTON = 'RE'
               ELSE IF (IAND(ISTAT,2).EQ.2) THEN
                  BUTTON = '4F'
               ENDIF   
C--- PROCESS THE 3RD BUTTON ON A 3-BUTTON MOUSE (BUTTONS 1 & 2 TOGETHER ON A
C--- 2-BUTTON MOUSE) NOT IN USE AT THIS TIME 1-FEB-91
C               IF (IAND(ISTAT,4).EQ.4.AND.IAND(ISTAT,2).EQ.2) THEN
C                  BUTTON = '??'
C               ENDIF
            ENDIF
         ENDIF
      ENDIF
      RETURN
      END
      