$STORAGE:2
      PROGRAM DEFSCRN
C
C   ROUTINE TO DEFINE OR MODIFY, OR DELETE DATA SCREENS USED FOR 
C   BATCH FILE MENUS.  (WRITTEN BY THE WRTSCRN PROGRAM AND/OR THE
C   SAVRES SUBROUTINE.)
C
      CHARACTER*80 BLANK
      CHARACTER*78 MSGLIN
      CHARACTER*64 HELP1,HELP2,SCRNFILE,MESSAG(3)
      CHARACTER*41 DESCREC
      CHARACTER*32 SCRNDESC
      CHARACTER*8 SCRNNAME
      CHARACTER*2 INCHAR,RTNFLAG,SPACE,NOUP,NOLO,YESUP,YESLO
      CHARACTER*1 REPLY,RTNCODE
      INTEGER*2 FGCOLOR,BGCOLOR,HLDCHR,HLDFGC,HLDBGC
     +         ,STRTCOL,ENDCOL,STRTROW,ENDROW,MSGLEN(3)
      LOGICAL NEWSCR,INBOX
C
      DATA BLANK /'          '/
      DATA SPACE /'  '/
      DATA HELP1,HELP2 /'P:\HELP\DEFSCRN1.HLP','P:\HELP\DEFSCRN2.HLP'/

      SPACE(2:2) = CHAR(0)
      INBOX = .FALSE.
      CALL GETYN(1,2,YESUP,YESLO)
      CALL GETYN(2,2,NOUP,NOLO)
      CALL GETMSG(375,MSGLIN)
      CALL PARSE1(MSGLIN,78,3,64,MESSAG,RTNCODE)
      CALL GETMSG(373,MSGLIN)
      CALL GETMSG(999,MSGLIN)
      DO 60 IMSG = 1,3
         DO 50 I = 64,1,-1
            IF (MESSAG(IMSG)(I:I).NE.' ') THEN
               MSGLEN(IMSG) = I
               GO TO 60
            END IF
50       CONTINUE
60    CONTINUE
C
   10 CONTINUE
      CALL ACTPAG(0,IERR)
      FGCOLOR = 14
      BGCOLOR = 0
C
C   ASK FOR THE SCREEN NAME OR SELECT FROM LIST OF EXISTING SCREENS
C
  20  CONTINUE
      CALL CLS
      ICOL = 40 - MSGLEN(1)/2
      CALL LOCATE(2,ICOL,IERR)
      CALL WRTSTR(MESSAG(1),MSGLEN(1),11,0)
      CALL WRTFNC(9)
      SCRNNAME = '        '
      SCRNDESC = ' '   
      CALL LOCATE(5,0,IERR)
      CALL WRTSTR(MSGLIN,78,14,0)
25    CONTINUE      
      ICNTRL = 1
      CALL LOCATE(6,0,IERR)
      CALL WRTSTR(MESSAG(2),MSGLEN(2),14,0)
      CALL WRTSTR(': ',2,14,0)
      CALL GETSTR(0,SCRNNAME,8,15,1,RTNFLAG)
      IF (RTNFLAG.EQ.'1F') THEN
         CALL DSPWIN(HELP1)
         GO TO 25
      ELSE IF (RTNFLAG.EQ.'1S') THEN
         OPEN (51,FILE='P:\FORM\FTNSCRN.IDX',STATUS='OLD'
     +       ,FORM='BINARY',ACCESS='DIRECT',RECL=43)
         DESCREC = ' '
         CALL VALWIN(51,DESCREC,41,3,IFLAG)
         CLOSE(51)
         SCRNNAME = DESCREC(1:8)
         SCRNDESC = DESCREC(10:41)
         IF (SCRNNAME.EQ.' ') THEN
            GO TO 25
         ELSE IF (IFLAG.EQ.1) THEN
            CALL DELDSCRN(SCRNNAME)
            GO TO 20
         ELSE
            ICNTRL = 2
         END IF
      ELSE IF (RTNFLAG.EQ.'4F') THEN
         STOP ' '
      ELSE IF(SCRNNAME.EQ.' ') THEN
         GO TO 25
      END IF
C
C   BUILD SCREEN FILE NAME AND CHECK IF IT EXISTS.  IF IT WAS SELECTED
C   FROM THE FTNSCRN INDEX FILE AND IT DOES NOT EXIST, DELETE IT FROM
C   THE INDEX FILE.
C
      SCRNFILE = 'P:\FORM\AAAAAAAA.SCR'
      SCRNFILE(9:16) = SCRNNAME
      DO 30 I = 9,16
         IF (SCRNFILE(I:I).EQ.' ') THEN
            SCRNFILE(I:I) = 'A'
         END IF
   30 CONTINUE
      OPEN(16,FILE=SCRNFILE,STATUS='OLD',FORM='BINARY',IOSTAT=IOCHK)
      IF (IOCHK.EQ.0) THEN
         NEWSCR = .FALSE.
         CLOSE(16)
      ELSE
         NEWSCR = .TRUE.
         CLOSE(16)
         IF (ICNTRL.EQ.2) THEN
            CALL WRTMSG(4,182,12,0,0,' ',0)
            CALL WRTMSG(3,183,12,1,1,' ',0)
            CALL DELDSCRN(SCRNNAME)
            GO TO 20
         END IF
      END IF
C
C   SCREEN NOT FOUND - NEW SCREEN TO BE DEFINED. INITIALIZE.
C
C   ASK THE USER FOR DEFAULT BACKGROUND COLOR
C
      IF (NEWSCR) THEN
  120    CONTINUE
         CALL LOCATE(21,45,IERR)
         CALL WRTMSG(4,240,14,0,0,' ',0)
         CALL GETCHAR(0,INCHAR)
         INBG = 0
         INFG = 14
         IF (INCHAR.EQ.YESUP .OR. INCHAR.EQ.YESLO) THEN
            CALL SETCLR(INFG,INBG)
            IF (INFG.GE.0) THEN
               BGCOLOR = INBG
            END IF
         ELSE IF (INCHAR.NE.NOUP .AND. INCHAR.NE.NOLO) THEN
            CALL BEEP
            GO TO 120
         END IF
      END IF
C
C   DRAW THE FORM FOR DEFINITION OR MODIFICATION
C
      CALL CLS
      IF (BGCOLOR.NE.0) THEN
          DO 180 J1 = 0,24
             CALL LOCATE(J1,0,IERR)
             CALL WRTSTR(BLANK,80,FGCOLOR,BGCOLOR)
  180     CONTINUE
      END IF
      CALL LOCATE(0,0,IERR)
      IF (ICNTRL.EQ.2) THEN
         I2ROW = 0
         I2COL = 0
         CALL SAVRES(1,3,0,SCRNFILE,0,IERR)
      END IF
C
      STRTROW = 0
      STRTCOL = 0
      ENDCOL = 79
      ENDROW = 24
      IROW = STRTROW
      ICOL = STRTCOL
C
C -----------  READ THE FORM DEFINITION FROM THE USER ----------------
C
      CALL CLTEXT(BGCOLOR,0,IERR)
  200 CONTINUE
      CALL LOCATE(IROW,ICOL,IERR)
      CALL GETCHAR(1,INCHAR)
      CALL CHKGRF(INCHAR)
C
C   A NORMAL CHARACTER HAS BEEN ENTERED
C
      IF (INCHAR(2:2).EQ.' ') THEN
         JCHAR = ICHAR(INCHAR(1:1))
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
         ICOL = ICOL + 1
C
C   OR A CONTROL CHARACTER HAS BEEN ENTERED
C
      ELSE
         IF (INCHAR.EQ.'LA'.OR.INCHAR.EQ.'BS') THEN
            ICOL = ICOL - 1
         ELSE IF (INCHAR.EQ.'RA') THEN
            ICOL = ICOL + 1
         ELSE IF (INCHAR.EQ.'UA') THEN
            IROW = IROW - 1
         ELSE IF (INCHAR.EQ.'DA') THEN
            IROW = IROW + 1
         ELSE IF (INCHAR.EQ.'RE') THEN
            ICOL = ENDCOL + 1
         ELSE IF (INCHAR.EQ.'HO') THEN
            ICOL = STRTCOL
            IROW = STRTROW
         ELSE IF (INCHAR.EQ.'EN') THEN
            ICOL = ENDCOL
            IROW = ENDROW
C
C     F1 - HELP
C
         ELSE IF (INCHAR.EQ.'1F') THEN
            CALL DSPWIN(HELP2)
C
C     F2 - SAVE THE CURRENT SCREEN
C
         ELSE IF (INCHAR.EQ.'2F') THEN
            IF (.NOT.NEWSCR) THEN
               CALL CHGNAM(SCRNNAME,SCRNFILE,MESSAG(3),MSGLEN(3)
     +                    ,RTNFLAG)
               IF (RTNFLAG.EQ.'4F') THEN
                  RTNFLAG = ' '
                  GO TO 200
               END IF
            END IF
            CALL SAVSCRN(SCRNNAME,SCRNFILE,SCRNDESC)
            GO TO 10
C
C     F3 - SET THE CURRENT CHARACTER COLOR
C
         ELSE IF (INCHAR.EQ.'3F') THEN
            INFG = FGCOLOR
            INBG = BGCOLOR
            CALL SETCLR(INFG,INBG)
            IF (INFG.GE.0) THEN
               FGCOLOR = INFG
               BGCOLOR = INBG
            END IF
C
C     F4 - ABANDON THE CURRENT FORM
C
         ELSE IF (INCHAR.EQ.'4F') THEN
            CALL ACTPAG(1,IERR)
            CALL CLS
            CALL LOCATE(0,0,IERR)
            CALL WRTMSG(2,228,12,1,0,' ',0)
            CALL LOCATE(23,28,IERR)
            CALL OKREPLY(REPLY,RTNCODE)
            IF (REPLY.EQ.'Y'.AND.RTNCODE.EQ.'0') THEN
               GO TO 10
            ELSE
               CALL ACTPAG(0,IERR)
            END IF
C
C     F5 - DRAW A BOX ON THE FORM
C
         ELSE IF (INCHAR.EQ.'5F') THEN
            CALL CBOX(INBOX,FGCOLOR,BGCOLOR)
C
C   DELETE A CHARACTER IN THE CURRENT LINE
C
         ELSE IF (INCHAR.EQ.'DE') THEN
            IF (ICOL.LT.ENDCOL) THEN
               DO 250 I1 = ICOL+1, ENDCOL
                  CALL LOCATE(IROW,I1,IERR)
                  CALL QRTEXT(HLDCHR,HLDBGC,HLDFGC)
                  CALL LOCATE(IROW,I1-1,IERR)
                  CALL CHRWRT(HLDCHR,HLDBGC,HLDFGC,1)
  250          CONTINUE
               CALL LOCATE(IROW,ENDCOL,IERR)
               CALL CWRITE(SPACE,FGCOLOR,0)
            ELSE
               CALL CWRITE(SPACE,FGCOLOR,0)
            END IF
C
C   INSERT A CHARACTER IN THE CURRENT LINE
C
         ELSE IF (INCHAR.EQ.'IN') THEN
            IF (ICOL.LT.ENDCOL) THEN
               DO 290 I1 = ENDCOL-1,ICOL,-1
                  CALL LOCATE(IROW,I1,IERR)
                  CALL QRTEXT(HLDCHR,HLDBGC,HLDFGC)
                  CALL LOCATE(IROW,I1+1,IERR)
                  CALL CHRWRT(HLDCHR,HLDBGC,HLDFGC,1)
  290          CONTINUE
               CALL LOCATE(IROW,ICOL,IERR)
               CALL CWRITE(SPACE,FGCOLOR,0)
            ELSE
               CALL CWRITE(SPACE,FGCOLOR,0)
            END IF 
C
C      F7 - DELETE THE CURRENT LINE 
C        
         ELSE IF (INCHAR.EQ.'7F') THEN
            CALL SCROLL(1,1,IROW,STRTCOL,ENDROW,ENDCOL)
C
C      F8 - INSERT A NEW LINE
C        
         ELSE IF (INCHAR.EQ.'8F') THEN
            CALL SCROLL(0,1,IROW,STRTCOL,ENDROW,ENDCOL)
C
C   OTHERWISE AN INVALID CHARACTER HAS BEEN ENTERED
C  
         ELSE
            CALL BEEP
         END IF
      END IF
C
C   CHECK THE CURSOR POSITION AND WRAP IF REQUIRED
C
      IF (ICOL.LT.STRTCOL) THEN
         ICOL = ENDCOL
         IROW = IROW - 1
      ELSE IF (ICOL.GT.ENDCOL) THEN
         ICOL = STRTCOL
         IROW = IROW + 1
      END IF
      IF (IROW.LT.STRTROW) THEN
          IROW = ENDROW
      ELSE IF (IROW.GT.ENDROW) THEN
          IROW = STRTROW
      END IF
C
      GO TO 200
      END
*************************************************************************
$PAGE
       SUBROUTINE CHGNAM(SCRNNAME,SCRNFILE,MESSAG,MSGLEN,RTNFLAG)
C
C   ROUTINE TO ASK IF A MODIFIED SCREEN SHOULD BE WRITTEN UNDER A NEW
C       NAME 
C
      CHARACTER*64 SCRNFILE,MESSAG
      CHARACTER*20 FILENAME
      CHARACTER*14 NEWFILE
      CHARACTER*8 SCRNNAME,NEWNAME,BLANK
      CHARACTER*2 INCHAR,RTNFLAG,NOUP,NOLO,YESUP,YESLO
      DATA BLANK /'        '/
C
      CALL ACTPAG(1,IERR)
      CALL CLS 
      CALL GETYN(1,2,YESUP,YESLO)
      CALL GETYN(2,2,NOUP,NOLO)
  50  CONTINUE
      FILENAME = 'P:\FORM\AAAAAAAA.SCR'
      NEWFILE = ' :AAAAAAAA.SCR'
      CALL LOCATE(20,50,IERR)
      CALL WRTMSG(5,230,14,0,0,' ',0)
      CALL GETCHAR(0,INCHAR)
      IF (INCHAR.EQ.YESUP .OR. INCHAR.EQ.YESLO)  THEN
         NEWNAME = BLANK
         CALL WRTSTR('  ',2,14,0)
         CALL WRTSTR(MESSAG,MSGLEN,14,0)
         CALL WRTSTR(': ',2,14,0)
         CALL GETSTR(0,NEWNAME,8,15,1,RTNFLAG)
         IF (NEWNAME.EQ.BLANK.OR.RTNFLAG.EQ.'4F') THEN
            GO TO 50
         ELSE
            DO 60 I = 1,8
               IF (NEWNAME(I:I).EQ.BLANK) THEN
                  GO TO 80
               END IF
               INAME = I
   60       CONTINUE
   80       CONTINUE
            FILENAME (9:9+INAME-1) = NEWNAME(1:INAME) 
            NEWFILE (3:3+INAME-1) = NEWNAME(1:INAME) 
         END IF
      ELSE IF (INCHAR.EQ.NOUP .OR. INCHAR.EQ.NOLO) THEN
         CALL ACTPAG(0,IERR)
         RTNFLAG = ' '
         RETURN
      ELSE IF (INCHAR.EQ.'4F') THEN
         CALL ACTPAG(0,IERR)
         RTNFLAG = '4F'
         RETURN
      ELSE
         CALL BEEP
         GO TO 50
      END IF
C
C   MAKE SURE THE NEW SCREEN NAME DOES NOT ALREADY EXIST
C
      OPEN (16,FILE=FILENAME,STATUS='OLD',IOSTAT=IOCHK
     +        ,SHARE='DENYWR',MODE='READ')
      IF (IOCHK.EQ.0) THEN
          CALL WRTMSG(1,229,12,1,0,' ',0)
          CLOSE(16)
          GO TO 50
      ELSE
         SCRNNAME = NEWNAME
         SCRNFILE = FILENAME
      END IF
      CALL ACTPAG(0,IERR)
      RTNFLAG = ' '
      RETURN
      END
************************************************************************
      SUBROUTINE SAVSCRN(SCRNNAME,SCRNFILE,SCRNDESC)
C
C   ROUTINE TO SAVE THE CURRENT SCREEN TO DISK
C
      CHARACTER*80 MSGLIN(3)
      CHARACTER*64 SCRNFILE
      CHARACTER*43 INREC
      CHARACTER*32 SCRNDESC
      CHARACTER*8 SCRNNAME
      CHARACTER*2 RTNFLAG
      CHARACTER*1 CHRRTN,LNFEED,RTNCODE
      CHRRTN = CHAR(13)
      LNFEED = CHAR(10)
C
C   ASK THE USER FOR THE DESCRIPTION OF THIS FORM AND ENTER IT INTO
C   THE FORTRAN FORM DEFINITION INDEX FILE.
C
      CALL GETMSG(366,MSGLIN(3))
      CALL GETMSG(999,MSGLIN(3))
      CALL PARSE1(MSGLIN(3),80,2,80,MSGLIN(1),RTNCODE)
      DO 20 I2 = 50,1,-1
         IF (MSGLIN(2)(I2:I2).NE.' ') THEN
            MSGLEN = I2 + 1
            GO TO 30
         END IF
20    CONTINUE
30    CONTINUE            
      CALL ACTPAG(1,IERR)
      CALL CLRMSG(1)
      CALL CLRMSG(2)
      CALL CLRMSG(3)
      CALL LOCATE(22,1,IERR)
      CALL WRTSTR(MSGLIN(2),MSGLEN,14,0)
      CALL GETSTR(1,SCRNDESC,32,15,1,RTNFLAG)
      CALL ACTPAG(0,IERR)
      CALL SAVRES(0,3,0,SCRNFILE,0,IERR)
C
C   SAVE OR UPDATE THIS ENTRY IN THE FTNSCREEN INDEX FILE
C
      OPEN (51,FILE='P:\FORM\FTNSCRN.IDX',STATUS='UNKNOWN'
     +     ,FORM='BINARY',ACCESS='DIRECT',RECL=43)
      IDEL = 0
      DO 150 I = 1,9999
         READ(51,REC=I,ERR=160) INREC
         IF (INREC(1:8).EQ.SCRNNAME) THEN
            INREC(10:41) = SCRNDESC
            IDEL = 0
            GO TO 200
         ELSE IF (INREC(1:8).EQ.'********') THEN
            IDEL = I
         END IF
150   CONTINUE
160   CONTINUE
      WRITE(INREC,'(A8,1X,A32,2A1)') SCRNNAME,SCRNDESC,CHRRTN,LNFEED
C
200   CONTINUE
      IF (IDEL.GT.0) THEN
         WRITE(51,REC=IDEL) INREC
      ELSE
         WRITE(51,REC=I) INREC
      END IF
      CLOSE (51)
C
      RETURN
      END
***********************************************************************
      SUBROUTINE DELDSCRN (SCRNNAME)
C
C   ROUTINE TO DELETE A FORTRAN SCREEN DEFINITION FILE AND REMOVE ITS ENTRY 
C   FROM THE FTNSCRN INDEX FILE.
C
      CHARACTER*43 INREC
      CHARACTER*64 SCRNFILE
      CHARACTER*8 SCRNNAME
C      
C  BUILD SCREEN FILE NAME,  OPEN IT, AND DELETE IT
C
      SCRNFILE = 'P:\FORM\AAAAAAAA.SCR'
      SCRNFILE(9:16) = SCRNNAME
      DO 30 I = 9,16
         IF (SCRNFILE(I:I).EQ.' ') THEN
            SCRNFILE(I:I) = 'A'
         END IF
   30 CONTINUE
      OPEN(16,FILE=SCRNFILE,STATUS='OLD',FORM='BINARY'
     +       ,MODE='WRITE',IOSTAT=ICHK)
      IF (ICHK.NE.6416.AND.ICHK.NE.0) THEN
         CALL OPENMSG(SCRNFILE,'DELDSCRN     ',ICHK)
      END IF
      CLOSE(16,STATUS='DELETE')
C
C   FIND AND REMOVE THE ENTRY FROM THE FTNSCRN INDEX FILE
C      
      OPEN (51,FILE='P:\FORM\FTNSCRN.IDX',STATUS='OLD',FORM='BINARY'
     +     ,ACCESS='DIRECT',RECL=43)
      DO 100 I = 1,9999
         READ(51,REC=I,ERR=110) INREC
         IF (INREC(1:8).EQ.SCRNNAME) THEN
            INREC(1:8) = '********'              
            INREC(10:41) = 'THIS RECORD DELETED'
            WRITE(51,REC=I) INREC
            GO TO 110
         END IF
100   CONTINUE
110   CONTINUE
      CLOSE(51)
      SCRNNAME = ' '
      RETURN
      END
             