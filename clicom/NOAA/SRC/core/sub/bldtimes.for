$STORAGE:2

      SUBROUTINE BLDTIMES(ITYPE,STRTTIME,ENDTIME,MONNAME,COLHDR)
C
C   ROUTINE TO GENERATE ALL OF THE POSSIBLE TIMES BETWEEN STRTTIME AND
C   ENDTIME (DEPENDING UPON ITYPE), CONVERT THEM TO TEXT DATES, AND LOAD
C   THEM INTO COLHDR STARTING IN POSITION 3.  A MAXIMUM OF 22 TIMES CAN
C   BE GENERATED. 
C
C   ---> THIS ROUTINE IS ASSOCIATED WITH GRAFINIT. <---
C
C   INPUT:
C     ITYPE.....CLICOM INTEGER TYPE (1-7)
C     STRTTIME..STARTING DATE/TIME - YEAR,MONTH,DAY,HOUR (CHAR*10)
C     ENDTIME...ENDING DATE/TIME - YEAR,MONTH,DAY,HOUR (CHAR*10)
C     MONNAME...NAMES OF MONTHS (CHAR*12 DIMENSIONED 13)
C   OUTPUT:
C     COLHDR....EXPANDED TEXT DATES FOR ALL OF THE TIMES BETWEEN
C               STRTTIME AND ENDTIME. (CHAR*28 DIMENSIONED 24)
C
      PARAMETER (MXELEM=24)
      CHARACTER*28 COLHDR(MXELEM)
      CHARACTER*12 MONNAME(13)
      CHARACTER*10 STRTTIME, ENDTIME, DATETIME
      INTEGER*2  SYEAR,SMON,SDAY,SHOUR,EYEAR,EMON,EDAY,EHOUR
C
      READ(STRTTIME,'(I4,3I2)') SYEAR,SMON,SDAY,SHOUR
      READ(ENDTIME,'(I4,3I2)') EYEAR,EMON,EDAY,EHOUR
      NYEAR = EYEAR - SYEAR

      IF (ITYPE.LE.2) THEN
C          .. MONTHLY,10 DAY DATA - CALCULATE NUMBER OF VALUES FROM MONTH RANGE
         NVAL =  EMON - SMON + 1
      ELSE IF (ITYPE.EQ.3) THEN
C             .. DAILY DATA -- CALCULATE NUMBER OF VALUES FROM DAY RANGE
         NVAL = EDAY - SDAY + 1
      ELSE 
C             .. SYNOPTIC, HOURLY, 15 MINUTE DATA -- CALCULATE NUMBER OF 
C                VALUES FROM HOUR RANGE WHICH IS OF THE FORM 1,2,3,ETC.
         NVAL =  EHOUR - SHOUR + 1
      END IF
      NVAL = MIN0(MAX0(1,NVAL),22)
C
      WRITE (DATETIME,'(I4,3I2.2)') SYEAR,SMON,SDAY,SHOUR
      CALL SPELDATE(ITYPE,0,DATETIME,MONNAME,COLHDR(3),28)
      DO 100 IVAL = 4,NVAL+2
         IF (ITYPE.LE.2) THEN
C          .. MONTHLY,10 DAY DATA 
            SMON = SMON + 1 
         ELSE IF (ITYPE.EQ.3) THEN
C             .. DAILY DATA
            SDAY = SDAY + 1
         ELSE 
C             .. SYNOPTIC, HOURLY, 15 MINUTE DATA
            SHOUR = SHOUR + 1
         END IF
         WRITE (DATETIME,'(I4,3I2.2)') SYEAR,SMON,SDAY,SHOUR
         CALL SPELDATE(ITYPE,0,DATETIME,MONNAME,COLHDR(IVAL),28)
100   CONTINUE
      RETURN
      END
