$STORAGE:2
      SUBROUTINE BANNER(ICNTRL,ITYPE,RECTYPE,IDKEY,OLDDDS,HOURLBL,
     +                  RTNCODE)
C
C     SUBROUTINE TO DISPLAY THE BANNER AND SOLICIT; STATION ID, DATA
C     SET ID, AND TIME FRAME DATA AND LOAD; STATION HISTORY DATA,
C     VALIDATION PARAMETERS AND EXISTING OBSERVATIONS.
C
C     IT ALSO OPENS THE INDEX AND DATA FILES (10,20 RESPECTIVELY)
C
$INCLUDE: 'INDEX.INC'
$INCLUDE: 'VAL1.INC'
$INCLUDE: 'ELEMCHKS.INC'
C
      CHARACTER*1 REPLY,RTNCODE,RECCODE
      CHARACTER*2 AMONTH,DELALPHA,HOURLBL(24)
      CHARACTER*3 RECTYPE
      CHARACTER*7 LAT
      CHARACTER*8 LON
      CHARACTER*8 STNID,SRCHID,FIELD(6),MESSAGE
      CHARACTER*11 KEYELEM
      CHARACTER*13 KEYQC
      CHARACTER*20 COUNTRY
      CHARACTER*21 IDKEY
      CHARACTER*24 STNABRV
C
      INTEGER*2 YEAR,MONTH,DAY,HOUR,DSETID,OLDDDS,ICNTRL
     +    ,NUMFLD(7)
      INTEGER*4 SRCHDATE,TEMP
      REAL ELEV
C
      DATA NUMFLD /3,3,4,5,5,5,6/
C
      RTNCODE = '0'
C
C    SET THE FIELDS TO THE DEFAULT VALUES
C
      READ(IDKEY,'(A8,A3,A4,3A2)') (FIELD(I1),I1=1,6)
C
C   IF EDIT IS CALLED BY AREAQC THEN ICNTRL = 1 INDICATES THAT
C   SELECTION CRITERIA ARE PASSED AND SHOULD NOT BE REQUESTED
C   BY THE USER - ALSO DO NOT CLOSE STNGEOG.INF
C
      IF (ICNTRL.EQ.1) THEN
         STNID = FIELD(1)
         READ (FIELD(3),'(I4)') YEAR
         MONTH = 99
         DAY = 99
         IF (NUMFLD(ITYPE).GE.4)THEN
            READ (FIELD(4),'(I2)') MONTH
         END IF
         IF (NUMFLD(ITYPE).GE.5)THEN
            READ (FIELD(5),'(I2)') DAY
         END IF
         CALL WRTBAN(IDKEY,RECTYPE,RTNCODE)
         GO TO 90 
      END IF
C
C     SET UP THE SCREEN
C
      CALL CLS
      CALL SETMOD(3,IERR)
C
C   RETRIEVE THE FIELDS FROM THE USER
C      
   20 CONTINUE
      IF (ICNTRL.EQ.1) THEN
         RTNCODE = '1'
         RETURN
      END IF
      CALL GETBAN(NUMFLD(ITYPE),STNID,DSETID,YEAR,MONTH,DAY,HOUR
     +     ,FIELD,RECCODE,RTNCODE)
      IF (RTNCODE.EQ.'1')THEN
         RETURN
      END IF
C
C   IF THIS IS A NEW DATASET THEN READ THE SETUP FILE, LOAD THE ELEMENT
C   CODES AND COLUMN HEADERS, AND NAME THE DATA AND INDEX FILES.
C   LOAD THE ELEMCHKS QC-CONTROL FILE INFORMATION INTO THE COLUMNS
C   APPROPRIATE FOR EACH ELEMENT
C
      IF (DSETID.NE.OLDDDS) THEN
         OLDDDS = DSETID
         CALL GETSET(DSETID,ITYPE,RECTYPE,HOURLBL,RTNCODE)
         IF (RTNCODE.NE.'0') THEN
            OLDDDS = -10
            GO TO 20
         END IF
         CALL GETCHK(RECTYPE)
      END IF
C
C   IF WANT NEXT OR PREVIOUS RECORD CALL FNDNXT
C
      IF (RECCODE.NE.'1') THEN
         CALL FNDNXT(IDKEY,STNID,RECCODE,RTNCODE)
         IF (RTNCODE.NE.'0')THEN
            GO TO 20
         ELSE
            READ (IDKEY,'(A8,A3,A4,3A2)') (FIELD(I1),I1=1,6)
            STNID = FIELD(1)
            READ (FIELD(3),'(I4)') YEAR
            MONTH = 0
            DAY = 0
            IF (NUMFLD(ITYPE).GE.4)THEN
                READ (FIELD(4),'(I2)') MONTH
            END IF
            IF (NUMFLD(ITYPE).GE.5)THEN
               READ (FIELD(5),'(I2)') DAY
            END IF
            CALL WRTBAN(IDKEY,RECTYPE,RTNCODE)
         END IF
      END IF
C
C   READ THE STATION FILE TO GET THE STATION NAME AND INSURE IT
C      WAS OPEN DURING THIS TIME.
C
  90  CONTINUE
      SRCHID = STNID
      TEMP = MONTH * 100
      TEMP = TEMP + DAY
      SRCHDATE = 10000
      SRCHDATE = INT4(YEAR) * SRCHDATE
      SRCHDATE = SRCHDATE + TEMP
      RTNCODE = 'O'
      CALL RDGEOG(SRCHID,SRCHDATE,STNABRV,COUNTRY,LAT,LON,ELEV,RTNCODE)
      MESSAGE = ' '
      IF (RTNCODE.EQ.'1')THEN
         MSGNUM = 75
      ELSE IF (RTNCODE.EQ.'2')THEN
         MSGNUM = 74
         MESSAGE = SRCHID
      ELSE
         MSGNUM = 85
      END IF
      IF (RTNCODE.NE.'0')THEN
         CALL WRTMSG(4,MSGNUM,12,1,0,MESSAGE,8)
   95    CONTINUE
         CALL LOCATE(22,5,IERR)
         CALL OKREPLY(REPLY,IERR)
         CALL CLRMSG(4)
         CALL CLRMSG(3)
         IF (REPLY.EQ.'N')THEN
            GO TO 20
         ELSE IF (REPLY.NE.'Y')THEN
            GO TO 95
         END IF
      END IF
      CALL LOCATE(2,5,IERR)
      CALL WRTSTR('Station = ',10,14,0)
      CALL LOCATE(2,15,IERR)
      CALL WRTSTR(STNABRV,24,14,0)
C
C  IF THE RECTYPE IS UPPER AIR DO NOT DO THE FOLLOWING TWO
C  LOADS AS STN-ELEMENT LIMITS ARE NOT ALLOWED IN THE RAOBQC SUBROUTINE.
C
      IF (RECTYPE.NE.'U-A')THEN
C
C          ** FOR ALL DATA TYPES EXCEPT UPPER AIR READ THE ELEMENT FILE 
C             TO GET THE ELEMENT INFORMATION
C
         KEYELEM(1:8) = STNID
         KEYELEM(9:11) = RECTYPE
         CALL RDELEM(KEYELEM,SRCHDATE,RTNCODE)
         IF (RTNCODE.EQ.'1'.AND.ICNTRL.EQ.0)THEN
            GO TO 20
         END IF
      END IF
C
C       ** READ THE ELEM.LIM FILE TO GET THE EDITING LIMITS
C          ROUTINE RDLIMS WILL PREVENT THE DATA TYPES MLY, 10D, AND U-A
C          FROM READING THE FILE.  STATION LIMIT CHECKS ARE NOT ALLOWED
C          FOR THESE DATA TYPES.
C
      KEYQC(1:8) = STNID
      KEYQC(9:11) = RECTYPE
      WRITE(AMONTH,'(I2)')MONTH
      KEYQC(12:13) = AMONTH
      CALL RDLIMS(KEYQC,'E',RTNCODE)
      IF (RTNCODE.EQ.'1'.AND.ICNTRL.EQ.0)THEN
         GO TO 20
      END IF
C
C   SET THE KEY VALUES TO THE BANNER VALUES
C
  100 CONTINUE
      DELALPHA = '01'
      DELKEY = 2
      WRITE(IDKEY,'(A8,A3,A4,3A2)') (FIELD(I1),I1=1,6)
C
C   CLOSE THE STNGEOG FILE
C
      REPLY = 'C'
      CALL RDGEOG(SRCHID,SRCHDATE,STNABRV,COUNTRY,LAT,LON,ELEV,REPLY)
      RETURN
      END
************************************************************************
$PAGE
      SUBROUTINE GETBAN(NUMFLD,STNID,DSETID,YEAR,
     +                 MONTH,DAY,HOUR,FIELD,RECCODE,RTNCODE)
C
C     SUBROUTINE TO DISPLAY THE BANNER LINE AND RETRIEVE ACCEPTABLE
C     RESPONSES
C
      CHARACTER*1 RTNCODE,RECCODE
      CHARACTER*2 RTNFLAG
      CHARACTER*8 STNID
C
      INTEGER*2 NUMFLD,YEAR,MONTH,DAY,HOUR,DSETID
      CHARACTER*8 FIELD(6)
      CHARACTER*8 FORMNAME(6)
      CHARACTER*64 HELPFILE
C
      DATA FORMNAME /3*'BNR3FLDS','BNR4FLDS','BNR5FLDS','BNR6FLDS'/
      DATA HELPFILE /'P:\HELP\KE-QC-2.HLP'/
C
      RTNCODE = '0'
      RECCODE = '1'
C
20    CONTINUE
      RTNFLAG = 'BN'
      CALL LOCATE (0,0,IERR)
      CALL GETFRM(FORMNAME(NUMFLD),HELPFILE,FIELD,8,RTNFLAG)
C
C   CHECK FOR FUNCTION KEYS sF1 OR F4
C
      IF (RTNFLAG.EQ.'4F')THEN
         RTNCODE = '1'
         RETURN
      END IF
C
C   MAKE SURE THE DATASET-ID HAS BEEN FILLED IN BEFORE TRYING TO FIND
C   NEXT AND PREVIOUS RECORDS
C
      IF (FIELD(2).EQ.'   ') THEN
         CALL WRTMSG(3,101,12,1,1,' ',0)
         GO TO 20
      END IF
      STNID = FIELD(1)
      READ (FIELD(2),'(I3)')DSETID
      IF (RTNFLAG.EQ.'3F')THEN
         RECCODE = '2'
         RETURN
      ELSE IF (RTNFLAG.EQ.'3S')THEN
         RECCODE = '0'
         RETURN
      END IF
C
C     DECODE AND VALIDATE THE REPLIES
C
      CALL CLRMSG(3)
      CALL CLRMSG(2)
      IF (FIELD(1).EQ.'   ') THEN
         CALL WRTMSG(3,103,12,1,1,' ',0)
         GO TO 20
      END IF
      READ (FIELD(3),'(BN,I4)')YEAR
      IF (YEAR.LT.1700) THEN
         CALL WRTMSG(3,102,12,1,1,' ',0)
         GO TO 20
      END IF
      MONTH = 0
      DAY = 0
      HOUR = 0
      IF (NUMFLD.GE.4) THEN 
         READ (FIELD(4),'(BN,I2)')MONTH
         WRITE(FIELD(4),'(I2.2)') MONTH
      END IF
      IF (NUMFLD.GE.5)THEN
         READ(FIELD(5),'(BN,I2)')DAY
         WRITE(FIELD(5),'(I2.2)') DAY
      END IF
      IF (NUMFLD.GE.6)THEN
         READ(FIELD(6),'(BN,I2)')HOUR
         WRITE(FIELD(6),'(I2.2)') HOUR
      END  IF
C
      RETURN
      END
