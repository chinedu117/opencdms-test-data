$STORAGE:2
C
      SUBROUTINE RDELEM(KEYELEM,SRCHDATE,RTNCODE)
C
C   ROUTINE TO DO A BINARY SEARCH OF THE STNELEM.INF FILE TO FIND ALL
C   ELEMENTS SPECIFIED IN THE CURRENT SETUP FILE FOR THE STATION  AND
C   OBS-TYPE SPECIFIED IN KEY-ELEM AND VALID DURING SRCHDATE.
C
C   KEYELEM = STATION-ID, OBS-TYPE
C
$INCLUDE: 'VAL1.INC'
C
      CHARACTER*1 RTNCODE
      CHARACTER*2 CHRDAY,CHRMON
      CHARACTER*3 OBSTYPE,OBSTBL(7)
      CHARACTER*8 STNID,CHRDATE
      CHARACTER*11 KEYELEM
      CHARACTER*10 SRCHKY,HLDKEY
      CHARACTER*11 SRCH2KEY, LOWLIM2,HILIM2
C
      INTEGER*2 ELEM,DIGITIZED,SCHEDULE,INSPCOBS,INRELELEM,INRELSHIP,
     +          IOCHK
      INTEGER*4 NUMREC,ITOPHLD,IBOTHLD,ITOP,IBOT,K
      INTEGER*4 BDATE,EDATE,SRCHDATE,IFACTOR,DATE1,DATE2,DATE3
      LOGICAL STNFOUND,NOELEM,TWBFLG,NOTWB
      DATA OBSTBL/'MLY','10D','DLY','SYN','HLY','15M','U-A'/
C
   20 CONTINUE
      OPEN (32,FILE='P:\DATA\STNELEM.INF',STATUS='OLD',ACCESS='DIRECT',
     +      RECL=30,SHARE='DENYWR',MODE='READ',IOSTAT=IOCHK)
         IF(IOCHK.NE.0) THEN
            CALL OPENMSG('P:\DATA\STNELEM.INF   ','READRTNS    ',IOCHK)     
            GO TO 20
         END IF   
C
C  SET ALL SPECOBS, RELELEM, AND RELOPER TO -8888.  IF AN ELEMENT IS NOT FOUND 
C  IN THE STNELEM FILE, THIS DEFAULT WILL INDICATE THEY SHOULD NOT BE REPORTED
C
      DO 30 I = 1,NUMELEM
         SPECOBS(I) = -8888
         RELELEM(I) = -8888
         RELOPER(I) = -8888
   30 CONTINUE
C
C   INITIALIZE THE BINARY SEARCH LOOP VARIABLES
C
      READ(KEYELEM,'(A8,A3)') STNID,OBSTYPE
      DO 50 I = 1,7
         IF (OBSTBL(I).EQ.OBSTYPE) THEN
            ITYPE = I
            GO TO 55
         END IF
   50 CONTINUE
   55 CONTINUE
      WRITE(SRCHKY,'(A8,I2.2)') STNID,ITYPE
      NOELEM = .TRUE.
      STNFOUND = .FALSE.
      READ(32,REC=1) NUMREC
      ITOPHLD = NUMREC + 2
      IBOTHLD = 1
      RTNCODE = '0'
C
C   DETERMINE AMOUNT OF DATE TO USE
C
      WRITE(CHRDATE,'(I8.8)') SRCHDATE
      CHRDAY = CHRDATE(7:8)
      CHRMON = CHRDATE(5:6)
      IFACTOR = 1
      IF (CHRDAY.EQ.'00'.OR.CHRDAY.EQ.'99') THEN
         IFACTOR = 100
      END IF
      IF (CHRMON.EQ.'00'.OR.CHRMON.EQ.'99') THEN
         IFACTOR = 10000
      END IF
      DATE1 = SRCHDATE / IFACTOR
      NOTWB = .FALSE.
C
C   BEGIN THE LOOP TO SEARCH FOR ALL ELEMENTS IN THE SETUP FILE
C
      DO 400 IELEM = 1,NUMELEM
         IF (TBLELEM(IELEM).EQ.102) THEN
            TWBFLG = .TRUE.
            NOTWB  = .TRUE.
         ENDIF   
         WRITE(SRCH2KEY,'(I3.3,I8)')  TBLELEM(IELEM),DATE1
C
C   BEGIN THE BINARY SEARCH LOOP FOR THE CURRENT STN/OBSTYPE/ELEM/DATE
C 
         IBOT = IBOTHLD
         ITOP = ITOPHLD
  100    CONTINUE
C
C      FIRST LOCATE THE STATION/OBSTYPE
C
         IF (ITOP-IBOT.GT.1) THEN
            K = (ITOP+IBOT)/2
            READ(32,REC=K)STNID,IOBS,ELEM,BDATE,EDATE,
     +                 DIGITIZED,SCHEDULE,INSPCOBS,INRELELEM,
     +                 INRELSHIP
            WRITE(HLDKEY,'(A8,I2.2)') STNID,IOBS
            IF (SRCHKY.LT.HLDKEY) THEN
               ITOP = K
               ITOPHLD = K
               GO TO 100
            ELSE IF (SRCHKY.GT.HLDKEY) THEN
               IBOT = K
               IBOTHLD = K
               GO TO 100
            END IF
         ELSE
            IF (SRCHKY.NE.HLDKEY) THEN
               IF (STNFOUND) THEN
                  GO TO 400
               ELSE
                  GO TO 500
               END IF
            END IF
         END IF
C
C      STATION / OBSTYPE HAS BEEN FOUND - LOCATE THE PROPER ELEM/DATE
C
         STNFOUND = .TRUE.
         DATE2 = BDATE / IFACTOR
         DATE3 = EDATE / IFACTOR
         WRITE(LOWLIM2,'(I3.3,I8)') ELEM,DATE2
         WRITE(HILIM2,'(I3.3,I8)') ELEM,DATE3
         IF (ITOP-IBOT.GT.1) THEN
            IF (SRCH2KEY.LT.LOWLIM2) THEN
               ITOP = K
               GO TO 100
            ELSE IF (SRCH2KEY.GT.HILIM2) THEN
               IBOT = K
               GO TO 100
            END IF
         ELSE
            IF (SRCH2KEY.LT.LOWLIM2.OR.SRCH2KEY.GT.HILIM2) THEN
               GO TO 400
            END IF
         END IF
C
C      PROPER ELEM/DATE HAS BEEN FOUND - FINISHED
C
         IF (TWBFLG) NOTWB=.FALSE.
         NOELEM = .FALSE.
         SPECOBS(IELEM) = INSPCOBS
         RELELEM(IELEM) = INRELELEM
         RELOPER(IELEM) = INRELSHIP
C 
  400 CONTINUE
C  
  500 CONTINUE
      IF (.NOT.STNFOUND) THEN
C          .. STATION/OBS-TYPE IS NOT FOUND
         CALL WRTERR(76,RTNCODE)
         IF (RTNCODE.EQ.'1') GO TO 550
      ENDIF   
      IF (NOELEM) THEN
C          .. NO ELEMENTS IN KEY ENTRY FORM FOUND
         CALL WRTERR(59,RTNCODE)
         IF (RTNCODE.EQ.'1') GO TO 550
      END IF
      IF (NOTWB) THEN
C          .. ELEMENT 102 IN KEY ENTRY FORM BUT NOT FOUND -- ASSUME ASPIRATED
         CALL WRTERR(610,RTNCODE)
      END IF
  550 CONTINUE
      CLOSE(32)
      RETURN
      END
      SUBROUTINE WRTERR(NMSG,RTNCODE)
C
      CHARACTER*1 RTNCODE,REPLY
C
      CALL WRTMSG(4,NMSG,12,1,0,' ',0)
   20 CALL LOCATE(22,5,IERR)
      CALL OKREPLY(REPLY,RTNCODE)
      CALL CLRMSG(4)
      CALL CLRMSG(3)
      IF (REPLY.EQ.'N')THEN
         RTNCODE = '1'
      ELSE IF (REPLY.NE.'Y')THEN
         GO TO 20
      END IF
      RETURN
      END            