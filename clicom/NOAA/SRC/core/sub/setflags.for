$STORAGE:2
C
      SUBROUTINE SETFLAGS(FLAGHOLD,ILINE)
C
C  THIS ROUTINE SCANS THE FLAGHOLD ARRAY AND FLAGARRAY AND UPDATES
C  FLAGARRAY WITH THE FIRST MOST SERIOUS ERROR FLAG FOUND. UNLESS 
C  PASSWD = DE AND THE VALUE HAS BEEN REKEYED OR THE PASSWD = QC AND
C  HAS BEEN ACCEPTED BY A VALIDATOR
C
C   FLAGARRAY(*,*,1) HAS VALUES AS FOLLOWS:
C      ' ' = UNCHECKED;
c      A,a = OK;
c      B,b = MINOR ERROR (LIGHT BLUE)
C      C,c = SIGNIFICANT(SIG) ERROR (RED);
C      D   = SIG ERROR REENTERED IN DATA ENTRY;
c      e   = SIG ERROR ACCEPTED
c   UPPERCASE INDICATES FLAG SET BY DATA ENTRY
C   LOWERCASE INICATES SET BY VALIDATOR
C
$INCLUDE: 'VAL1.INC'
$INCLUDE: 'ELEMCHKS.INC'
C
      CHARACTER*1 FLAGHOLD(MAXELEM,MAXCHK,2)
      INTEGER*2 ICOL
      LOGICAL FLAGFND
C
      DO 200 ICOL = 1,NUMELEM
         FLAGFND = .FALSE.
         DO 50 J = 1,MAXCHK
            IF (FLAGHOLD(ICOL,J,1).EQ.'C'.OR.
     +             FLAGHOLD(ICOL,J,1).EQ.'c') THEN
               FLAGFND = .TRUE.
            END IF
  50     CONTINUE
C
C   IF VALUE HAS BEEN REKEYED OR ACCEPTED AND ERRORS STILL FOUND THEN
C   LEAVE THE SEVERITY FLAG AS REKEYED OR ACCEPTED - ELSE USE THE 
C   FIRST AND HIGHEST SEVERITY FOUND WITHIN THE FLAGHOLD ARRAY
C
         IF (FLAGFND.AND.(FLAGARRAY(ICOL,ILINE,1).EQ.'e'.OR.
     +        (FLAGARRAY(ICOL,ILINE,1).EQ.'D'.AND.PASSWD.EQ.'DE'))) THEN
            DO 120 ICHK = 1,MAXCHK
               IF (FLAGHOLD(ICOL,ICHK,1).EQ.'C'.OR.
     +              FLAGHOLD(ICOL,ICHK,1).EQ.'c') THEN
                  FLAGARRAY(ICOL,ILINE,2) = FLAGHOLD(ICOL,ICHK,2)
                  GO TO 200
               END IF
  120       CONTINUE
         ELSE      
            FLAGARRAY(ICOL,ILINE,1) = FLAGHOLD(ICOL,1,1)
            FLAGARRAY(ICOL,ILINE,2) = FLAGHOLD(ICOL,1,2)
            DO 150 ICHK = 2,MAXCHK
               IF (FLAGARRAY(ICOL,ILINE,1).LT.FLAGHOLD(ICOL,ICHK,1))THEN
                  FLAGARRAY(ICOL,ILINE,1) = FLAGHOLD(ICOL,ICHK,1)
                  FLAGARRAY(ICOL,ILINE,2) = FLAGHOLD(ICOL,ICHK,2)
               END IF
  150       CONTINUE
         END IF
  200 CONTINUE
      RETURN
      END
