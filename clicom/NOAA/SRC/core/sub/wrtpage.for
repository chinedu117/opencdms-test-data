$STORAGE:2

      SUBROUTINE WRTPAGE(RECTYPE,HOURLBL,STRTELEM,STRTLINE)
C
C   ROUTINE TO WRITE A COMPLETE DATA ENTRY PAGE (THE VALUE PORTION) 
C      TO THE SCREEN.
C
      PARAMETER (MAXY = 16)
      INTEGER*2 STRTELEM,STRTLINE
      CHARACTER*3 RECTYPE
      CHARACTER*2 HOURLBL(*)
C
C  CLEAR THE VALUE AREA OF THE SCREEN BY SCROLLING UP THE ENTIRE WINDOW
C
      IROW = 4
      CALL CLTEXT(0,0,IERR)
      CALL SCROLL(1,MAXY+1,IROW,0,MAXY+IROW,79)
C
C  WRITE THE HEADER AND VALUE LINES
C
      CALL WRTHDR(STRTELEM)
      DO 200 J = 1,MAXY
         IROW = IROW + 1
         ILINE = STRTLINE + J - 1
         CALL WRTLINE(RECTYPE,HOURLBL,STRTELEM,ILINE,IROW)
  200 CONTINUE
      RETURN
      END

$PAGE
***********************************************************************

      SUBROUTINE WRTLINE(RECTYPE,HOURLBL,STRTELEM,ILINE,IROW)
C
C   ROUTINE TO WRITE A LINE OF THE VALUE PORTION OF THE DATA-ENTRY
C     FORM
C
      PARAMETER (MAXX = 10)
$INCLUDE:'VAL1.INC'
      CHARACTER*3 OUTCHAR,RECTYPE
      CHARACTER*2 HOURLBL(*)
      INTEGER*2 STRTELEM,IELEM,ILINE,IROW

      IF (ILINE.GT.NUMLINE) THEN
         RETURN
      END IF 
      CALL LOCATE(IROW,1,IERR)
C
C        ** WRITE THE ROW LABEL FOR KEY ENTRY FORM      
C
      IF (RECTYPE.EQ.'HLY' .OR. RECTYPE.EQ.'SYN') THEN
C          .. HOURLY OR SYNOPTIC DATA -- USE LABELS READ FROM DATAQC.PRM
         OUTCHAR = ' '// HOURLBL(ILINE)
      ELSE
         WRITE(OUTCHAR,'(I3)') ILINE
      END IF
      CALL WRTSTR(OUTCHAR,3,14,0)
C      
      DO 200 I = 1,MAXX
         ICOL = (I-1)*7 + 7
         IELEM = STRTELEM + I - 1
         IF (IELEM.LE.NUMELEM) THEN
            CALL WRTVAL(VALARRAY(IELEM,ILINE),FLAGARRAY(IELEM,ILINE,1)
     +         ,IROW,ICOL)
         END IF
  200    CONTINUE
      RETURN
      END

$PAGE
***********************************************************************

      SUBROUTINE WRTHDR(STRTELEM)
C
C   ROUTINE TO PRINT THE ELEMENT ABBREVIATION ON TOP OF EACH COLUMN
C      OF THE DATA-ENTRY FORM
C
      PARAMETER (MAXX = 10)
$INCLUDE:'VAL1.INC'
      INTEGER*2 STRTELEM
      
      IROW = 4

      DO 200 I = 1,MAXX
         ICOL = (I-1)*7 + 7
         CALL WRTVAL(TBLEABRV(STRTELEM+I-1),'#',IROW,ICOL)
  200    CONTINUE
      RETURN
      END

