$STORAGE:2
      SUBROUTINE RDGEOG(SRCHID,SRCHDATE,STNABRV,COUNTRY,LAT,LON
     +                    ,ELEV,RTNCODE)
C
C   ROUTINE TO DO A BINARY SEARCH OF THE STNGEOG.INF FILE TO FIND 
C      THE INFORMATION FOR STATION SRCHID VALID ON SRCHDATE
C
C     RTNCODE = 0 IF STATION AND DATE FOUND
C             = 1 IF STATION BUT NOT DATE FOUND
C             = 2 IF STATION NOT FOUND
C
C             = O ON INPUT - OPEN FILE 35 NO MATTER WHAT
C             = C ON INPUT - CLOSE FILE 35
C
$INCLUDE: 'VAL1.INC'
$INCLUDE: 'GENMOIST.INC'
C
      CHARACTER*8 SRCHID,STNID,CHRDATE
      CHARACTER*7 LAT,RDLAT
      CHARACTER*8 LON,RDLON
      CHARACTER*20 COUNTRY,RDCNTRY 
      CHARACTER*24 STNABRV,RDABRV
      CHARACTER*2 CHRDAY,CHRMON
      CHARACTER*1 RTNCODE
      INTEGER*4 NUMREC,ITOP,IBOT,K
      INTEGER*4 BDATE,EDATE,SRCHDATE,IFACTOR,DATE1,DATE2,DATE3
      REAL ELEV,RDELEV
      LOGICAL FIRSTCALL,STNFND
      DATA FIRSTCALL /.TRUE./
C
C   IF THIS IS THE FIRST CALL OPEN THE STN GEOGRAPHY FILE
C
      IF (FIRSTCALL.OR.RTNCODE.EQ.'O') THEN
   20    CONTINUE
         OPEN (35,FILE='P:\DATA\STNGEOG.INF',STATUS='OLD',
     +         ACCESS='DIRECT',RECL=80,SHARE='DENYWR',MODE='READ'
     +         ,IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            CALL OPENMSG('P:\DATA\STNGEOG.INF   ','RDGEOG      ',IOCHK)
            GO TO 20
         END IF
         FIRSTCALL = .FALSE.
      END IF
C
      IF (RTNCODE.EQ.'C') THEN
         CLOSE(35)
         FIRSTCALL = .TRUE.
         RTNCODE = '0'
         RETURN
      END IF
C
      READ(35,REC=1) NUMREC
      ITOP = NUMREC + 2
      IBOT = 1
      RTNCODE = '0'
      STNFND = .FALSE.
      STNELEV = -9999.0
C
C   DETERMINE AMOUNT OF DATE TO USE
C
      WRITE(CHRDATE,'(I8.8)') SRCHDATE
      CHRDAY = CHRDATE(7:8)
      CHRMON = CHRDATE(5:6)
      IFACTOR = 1
      IF (CHRDAY.EQ.'00'.OR.CHRDAY.EQ.'99') THEN
         IFACTOR = 100
      END IF
      IF (CHRMON.EQ.'00'.OR.CHRMON.EQ.'99') THEN
         IFACTOR = 10000
      END IF
      DATE1 = SRCHDATE / IFACTOR
C
C   BEGIN THE BINARY SEARCH LOOP
C 
  100 CONTINUE
C
C   FIRST CHECK IF THE STATION HAS BEEN FOUND
C
      IF (ITOP-IBOT.GT.1) THEN
         K = (ITOP+IBOT)/2
         READ(35,REC=K) STNID,BDATE,EDATE,RDABRV,RDCNTRY,RDLAT,RDLON
     +      ,RDELEV
         IF (SRCHID.LT.STNID) THEN
            ITOP = K
            GO TO 100
         ELSE IF (SRCHID.GT.STNID) THEN
            IBOT = K
            GO TO 100
         END IF
      ELSE
         IF (SRCHID.NE.STNID) THEN
            GO TO 300
         END IF
      END IF
C
C   STATION HAS BEEN FOUND - LOCATE THE PROPER DATE
C
      STNFND = .TRUE.
      STNABRV = RDABRV
      COUNTRY = RDCNTRY
      LAT = RDLAT
      LON = RDLON
      ELEV = RDELEV
      STNELEV = RDELEV
      IF (ITOP-IBOT.GT.1) THEN
         DATE2 = BDATE / IFACTOR
         DATE3 = EDATE / IFACTOR
         IF (DATE1.LT.DATE2) THEN
            ITOP = K
            GO TO 100
         ELSE IF (DATE1.GT.DATE3) THEN
            IBOT = K
            GO TO 100
         END IF
      ELSE
         IF (DATE1.LT.DATE2.OR.DATE1.GT.DATE3) THEN
            GO TO 200
         END IF
      END IF
C
C   PROPER DATE HAS BEEN FOUND - FINISHED
C
      RETURN 
C
C   GET HERE IF STATION FOUND BUT DATE NOT FOUND
C
  200 CONTINUE
      RTNCODE = '1'
      RETURN
C
C   GET HERE IF STATION IS NOT FOUND
C
  300 CONTINUE
      IF (STNFND) THEN
         RTNCODE = '1'
      ELSE
         STNABRV = '   '
         COUNTRY = '   '
         LAT = '  '
         LON = '  '
         ELEV = 0.0
         RTNCODE = '2'      
      END IF
      RETURN
      END
