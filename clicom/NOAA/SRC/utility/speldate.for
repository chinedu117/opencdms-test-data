$STORAGE:2

      SUBROUTINE SPELDATE(ITYPE,ISPAN,DATETIME,MONNAME,TEXTDATE,LENTXT)
C
C   ROUTINE TO CONVERT A NUMERIC TIME (YEAR,MONTH,DAY,HOUR) STORED IN A 
C   CHARACTER VARIABLE INTO THE TEXT EQUIVALENT (0000 10 January 1988)
C   FOR EXAMPLE.  THE LEVEL OF DETAIL IN THE TIME IS DETERMINED BY THE 
C   VALUE OF ITYPE AND ISPAN.  THIS ROUTINE IS ASSOCIATED WITH GRAFINIT
C   AND ITS RELATED ROUTINES.
C
C    INPUT:
C      ITYPE......OBS-TYPE NUMBER (1-7 FOR MLY TO U-A)
C      ISPAN......CONTROLS LEVEL OF DATE DETAIL FOR THE OBS-TYPE SPECIFIED
C                 IF ISPAN = 0 THE DATE SHOULD BE THE MOST DETAILED DATE
C                 AVAILABLE FOR THAT ITYPE.
C                 IF ISPAN > 0 THEN THE DATE TEXT WILL REFER TO THE NEXT
C                 LESS DETAILED DATE TYPE
C                 (E.G. ITYPE=1,ISPAN=0  DATE=JANUARY 1988, 
C                       ITYPE=1,ISPAN=1  DATE=1988)
C      DATETIME...NUMERIC DATE-TIME (YEAR,MONTH,DAY,HOUR) STORED IN A 
C                 CHARACTER VARIABLE.  THE FOLLOWING HOUR RANGES ARE EXPECTED
C                             DATA TYPE   ITYPE   IN HOUR   OUT HOUR
C                             ---------   -----   -------   --------
C                             SYNOPTIC      4       1-8     READ FROM FILE
C                             HOURLY        5       1-24    READ FROM FILE
C                             15 MINUTE     6       1-96    0000, 0015, ETC.
C                             UPPER-AIR     7       0-23    0-23
C                             
C                         
C      MONNAME....MONTH NAMES (CHARACTER*12  DIMENSIONED 13)      
C      LENTXT.....LENGTH (IN CHARACTERS) OF TEXTDATE IN CALLING ROUTINE
C   OUTPUT:
C      TEXTDATE...THE OUTPUT TEXT FOR THIS DATE
C
      CHARACTER*(*) TEXTDATE
      CHARACTER*12 MONNAME(13)
      CHARACTER*10 DATETIME
      CHARACTER*2 CHRHOUR,HOURLBL(24)
      CHARACTER*5 CHRTIME
      INTEGER*2 MONLEN
C
      READ(DATETIME,'(4X,I2,2X,I2)') IMON,JHOUR
C
C   DETERMINE HOW MANY CHARACTERS ARE IN THE NAME OF THIS MONTH
C   IF TEXTDATE LENGTH IS LIMITTED USE ONLY THE 1ST 3 LETTERS
C
      IF (LENTXT.LT.20) THEN
         MONLEN = 3
      ELSE
         MONLEN = 0
         IF (IMON.GT.0.AND.IMON.LE.12) THEN
            MONLEN = LNG(MONNAME(IMON))
         END IF
      END IF
C
C   BUILD THE OUTPUT TEXT FIELD
C      
      TEXTDATE(1:LENTXT) = '  '
      IF (ITYPE.EQ.1) THEN
C          .. MONTHLY DATA      
         IF (ISPAN.EQ.0) THEN
            TEXTDATE(1:MONLEN) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+2:MONLEN+5) = DATETIME(1:4)
         ELSE
            TEXTDATE(1:4) = DATETIME(1:4)
         END IF
      ELSE IF (ITYPE.EQ.2) THEN
C          .. 10 DAY DATA      
         IF (ISPAN.EQ.0) THEN
            TEXTDATE(1:LENTXT) = 'Decade '//DATETIME(5:6)//','
            TEXTDATE(12:15) = DATETIME(1:4)
         ELSE
            TEXTDATE(1:4) = DATETIME(1:4)
         END IF
      ELSE IF (ITYPE.EQ.3) THEN
C          .. DAILY DATA     
         IF (ISPAN.EQ.0) THEN
            TEXTDATE(1:3) = DATETIME(7:8)
            TEXTDATE(4:MONLEN+3) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+5:MONLEN+8) = DATETIME(1:4)
         ELSE
            TEXTDATE(1:MONLEN) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+2:MONLEN+5) = DATETIME(1:4)
         END IF         
      ELSE IF (ITYPE.EQ.6) THEN
C          .. 15 MINUTE DATA      
         IF (ISPAN.EQ.0) THEN
            KHOUR = (JHOUR-1) / 4
            KMIN = MOD(JHOUR-1,4) * 15
            WRITE(CHRTIME,'(2I2.2)') KHOUR,KMIN                  
            TEXTDATE(1:4) = CHRTIME
            TEXTDATE(6:7) = DATETIME(7:8)
            TEXTDATE(9:MONLEN+8) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+10:MONLEN+13) = DATETIME(1:4)
         ELSE
            TEXTDATE(1:3) = DATETIME(7:8)
            TEXTDATE(4:MONLEN+3) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+5:MONLEN+8) = DATETIME(1:4)
         END IF         
      ELSE 
C          .. SYNOPTIC, HOURLY, AND UPPER AIR DATA
         IF (ISPAN.EQ.0) THEN
C             .. LONG FORM -- USE YEAR, MONTH, DAY, HOUR         
            IF (ITYPE.EQ.4) THEN
C                .. SYNOPTIC DATA
               CALL RDHRLBL(ITYPE,HOURLBL,RTNCODE)
               CHRHOUR = HOURLBL(JHOUR)
            ELSE IF (ITYPE.EQ.5) THEN            
C                .. HOURLY DATA            
               CALL RDHRLBL(ITYPE,HOURLBL,RTNCODE)
               CHRHOUR = HOURLBL(JHOUR)
            ELSE
C                .. UPPER AIR DATA            
               WRITE(CHRHOUR,'(I2.2)') JHOUR                  
            ENDIF
C            
C            TEXTDATE(1:3) = CHRHOUR//'Z'
C            TEXTDATE(5:6) = DATETIME(7:8)
C            TEXTDATE(8:MONLEN+7) = MONNAME(IMON)(1:MONLEN)
C            TEXTDATE(MONLEN+9:MONLEN+12) = DATETIME(1:4)
            TEXTDATE(1:4) = CHRHOUR//'00'
            TEXTDATE(6:7) = DATETIME(7:8)
            TEXTDATE(9:MONLEN+8) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+10:MONLEN+13) = DATETIME(1:4)
         ELSE
C             .. SHORT FORM -- USE YEAR, MONTH, DAY
            TEXTDATE(1:3) = DATETIME(7:8)
            TEXTDATE(4:MONLEN+3) = MONNAME(IMON)(1:MONLEN)
            TEXTDATE(MONLEN+5:MONLEN+8) = DATETIME(1:4)
         END IF         
      END IF
      RETURN
      END
