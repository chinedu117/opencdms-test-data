$STORAGE:2

      SUBROUTINE RDEXTR(ITYPE,OUTELEM,PREVID,HLDARRAY,MXDATCOL
     +                 ,NELEM,NUMEXT,BEGTIM,ENDTIM,MAPROW)
C
C   ROUTINE READS THE MEAN AND EXTREMES INFORMATION FOR THE STATION,
C   OBS-TYPE, AND DATE WANTED.  IT WILL READ MONTHLY, 10 DAY, OR DAILY
C   MEANS AND EXTREMES FILES.  THE MONTHLY DATA IS READ FROM THE FORTRAN
C   DIRECT FILE WHILE THE 10 DAY AND DAILY ARE READ DIRECTLY FROM THE 
C   APPROPRIATE DATAEASE FILES.  THE DATA ARE RETURNED IN OBSERVATION
C   FORMAT AS A 2 DIMENSIONAL ARRAY (HLDARRAY). 
C
C   THIS ROUTINE DOES A BINARY SEARCH TO FIND THE FIRST RECORD AND READS
C   THE REMAINING RECORDS SEQUENTIALLY.
C   NOTE:  THE ELEMENT CODE USED IN THE SEARCH IS A DUMMY VALUE THAT IS ONE
C          NUMBER LESS THAN THE LOWEST POSSIBLE CODE THAT COULD EXIST FOR THE
C          CURRENT DATA TYPE.  FOR EXAMPLE WITH MONTHLY DATA, THE LOWEST
C          VALUE IS 201 WHILE THE VALUE SEARCHED IS 200.  THE SEARCH WILL
C          NEVER FIND THE EXACT MATCH.  IT WILL END WITH A RTNCODE='1'
C          (TOP-BOT=1).  EITHER JREC OR JREC+1 WILL BE THE FIRST RECORD THAT
C          MATCHES THE STATION, OBS-TYPE, AND DATE WANTED.
C
C   INPUT:
C      ITYPE.....CLICOM RECORD TYPE NUMBER (1-7)
C      OUTELEM...INT*2 ARRAY OF ELEMENT CODES OF THE ELEMENTS WANTED
C      PREVID....THE STNID, MONTH, AND DAY OF THE RECORD WANTED AS
C                A CHAR*16 VARIABLE WITH THE FOLLOWING FORMAT
C                (A8,4X,2I2) STNID,MONTH,JDAY
C      MAXROW....OUTER DIMENSION OF THE OUTPUT DATA ARRAY
C      NELEM.....NUMBER OF ELEMENTS WANTED (AND SPECIFIED IN OUTELEM)
C      NUMEXT....THE NUMBER OF EXTREMES RECORDS IN 10D/DLY FILE
C      BEGTIM....MOST DETAILED TIME FOR THE GIVEN DATA TYPE SUCH AS DAYS FOR
C                DAILY DATA -- USED FOR MAPS WHICH RECORD EXTREMES AT ONE TIME 
C      ENDTIM....
C      MAPROW....ROW INDEX THAT INDICATES THE POSITION INTO WHICH THE EXTREME
C                VALUE WILL BE ENTERED
C   OUTPUT:
C      HLDARRAY..REAL*4 ARRAY DIMENSIONED (MXELEM,MAXROW).  CONTAINS
C                THE OUTPUT DATA READ
C
C   ---> NOTE: THE APPROPRIATE MEANS/EXTREMES FILE MUST BE OPENED AS
C              FILE 31 BEFORE THIS ROUTINE IS CALLED.  USE OPENEXT.
C                      ------ 
      INTEGER*2 MXELEM
      PARAMETER (MXELEM=24)
C
      INTEGER*2 ITYPE,MXDATCOL,NELEM,OUTELEM(MXELEM)
     +          ,ELEMWANTED(MXELEM),EXTWANTED(MXELEM),NWANTED
     +          ,JINDEX(MXELEM),MAXFLAG,MINFLAG,BEGTIM,ENDTIM
      INTEGER*4 NUMEXT,JREC4,MAXDATE,MINDATE
      REAL*4    HLDARRAY(MXDATCOL,*), VALMAX, VALMIN, MAXCHG
      REAL*8    MEANVAL,STDDEV
      CHARACTER*18 PREVID
      CHARACTER*12 BEGKEY,ENDKEY,INKEY
      CHARACTER*8 STNID,INSTN
      CHARACTER*1 RTNCODE, MAXFLG, MINFLG
C
      CALL LOCATE(24,55,IERR)
      CALL WRTSTR('Reading means/extremes',22,12,0)
C
      READ(PREVID,'(A8,4X,2I2)') STNID,MONTH,JDAY
C
C   DETERMINE THE ELEMENTS FOR WHICH MEANS/EXTREMES HAVE BEEN REQUESTED
C 
      NWANTED = 0
      IF (MAPROW.EQ.0) THEN
         IOFFSET = 0
      ELSE
         IOFFSET = 2
      ENDIF      
      DO 50 I1 = 1,NELEM      
         IF (OUTELEM(I1).GT.1000) THEN
            NWANTED = NWANTED + 1
            JELEM = OUTELEM(I1)/1000
            ELEMWANTED(NWANTED) = OUTELEM(I1) - JELEM*1000
            EXTWANTED(NWANTED) = JELEM
            JINDEX(NWANTED) = I1 + IOFFSET
         END IF
50    CONTINUE         
C
C       ** RETRIEVE MEAN/EXTREME RECORDS FOR MONTHLY DATA
C
      IF (ITYPE.EQ.1) THEN
         MON1 = BEGTIM
         IF (MAPROW.GT.0) THEN
            MON2 = BEGTIM
         ELSE
            MON2 = ENDTIM
         END IF
         WRITE(BEGKEY,600) STNID,ITYPE,MON1
         WRITE(ENDKEY,600) STNID,ITYPE,MON2
C
C     LOCATE THE FIRST EXTREMES RECORD FOR THE STATION, DATE, AND
C     OBS-TYPE WANTED.  THEN READ ALL THE VALUES REQUESTED.
C
         CALL FNDEXT(STNID,ITYPE,MON1,200,RTNCODE,JREC4,ISRC,IBYR
     +       ,IEYR,IYRS,VALMAX,MAXDATE,MAXFLG,VALMIN,MINDATE,MINFLG
     +       ,MEANVAL,STDDEV,MAXCHG)
100      CONTINUE
            READ(31,REC=JREC4,END=500,ERR=500)INSTN,INTYPE,INMONTH
     +          ,INELEM,ISRC,IBYR,IEYR,IYRS,VALMAX,MAXDATE,MAXFLAG
     +          ,VALMIN,MINDATE,MINFLAG,MEANVAL,STDDEV,MAXCHG
            WRITE(INKEY,600) INSTN,INTYPE,INMONTH
            IF (INKEY.LT.BEGKEY) THEN
               JREC4 = JREC4 + 1
            ELSE IF (INKEY.GT.ENDKEY) THEN
               GO TO 500
            ELSE   
               IF (MAPROW.GT.0) THEN
                  IDXROW = MAPROW
               ELSE
                  IDXROW = INMONTH
               ENDIF      
               DO 120 I1 = 1,NWANTED
                  IF (INELEM.EQ.ELEMWANTED(I1)) THEN
                     I2 = JINDEX(I1)
                     IF (EXTWANTED(I1).EQ.1) THEN
                        HLDARRAY(I2,IDXROW) = MEANVAL
                     ELSE IF (EXTWANTED(I1).EQ.2) THEN
                        HLDARRAY(I2,IDXROW) = VALMAX
                     ELSE IF (EXTWANTED(I1).EQ.3) THEN
                        HLDARRAY(I2,IDXROW) = VALMIN
                     END IF
                  END IF
120            CONTINUE
               JREC4 = JREC4 + 1
            END IF
C          .. END OF LOOP     
         GO TO 100
C
C       ** RETRIEVE MEAN/EXTREME RECORDS FOR 10 DAY DATA
C
      ELSE IF (ITYPE.EQ.2) THEN
         IMON = ((BEGTIM-1) / 3) + 1
         IDEC = MOD(BEGTIM,3)
         IF (IDEC.EQ.0) THEN
            IDEC = 3
         END IF
         MON1 = IMON
         IDEC1 = IDEC
         IF (MAPROW.GT.0) THEN
            MON2 = IMON
            IDEC2 = IDEC
         ELSE
            IMON = ((ENDTIM-1) / 3) + 1
            IDEC = MOD(ENDTIM,3)
            IF (IDEC.EQ.0) THEN
               IDEC = 3
            END IF
            MON2 = IMON
            IDEC2 = IDEC
         END IF
         WRITE(BEGKEY,600) STNID,MON1,IDEC1
         WRITE(ENDKEY,600) STNID,MON2,IDEC2
C
C      LOCATE THE FIRST EXTREMES RECORD FOR THIS STN, TYPE AND TIME
C      THEN READ ALL OF THE VALUES REQUESTED
C
         CALL FNDEXD(STNID,MON1,IDEC1,400,RTNCODE,JREC4,NUMEXT,IBYR
     +      ,IYRS,VALMAX,MAXYR,VALMIN,MINYR,MEANVAL)
200      CONTINUE
            CALL RDEXD(INSTN,INMONTH,INDEC,INELEM,RTNCODE,JREC4,IBYR
     +        ,IYRS,VALMAX,MAXYR,VALMIN,MINYR,MEANVAL)
            IF (RTNCODE.NE.'0') THEN
               GO TO 500
            ELSE
               WRITE(INKEY,600) INSTN,INMONTH,INDEC
               IF (INKEY.LT.BEGKEY) THEN
                  JREC4 = JREC4 + 1
               ELSE IF (INKEY.GT.ENDKEY) THEN
                  GO TO 500
               ELSE   
                  IF (MAPROW.GT.0) THEN
                     IDXROW = MAPROW
                  ELSE
                     IDXROW = (INMONTH-1)*3 + INDEC
                  ENDIF      
                  DO 220 I1 = 1,NWANTED
                     IF (INELEM.EQ.ELEMWANTED(I1)) THEN
                        I2 = JINDEX(I1)
                        IF (EXTWANTED(I1).EQ.1) THEN
                           HLDARRAY(I2,IDXROW) = MEANVAL
                        ELSE IF (EXTWANTED(I1).EQ.2) THEN
                           HLDARRAY(I2,IDXROW) = VALMAX
                        ELSE IF (EXTWANTED(I1).EQ.3) THEN
                           HLDARRAY(I2,IDXROW) = VALMIN
                        END IF
                     END IF
220               CONTINUE
                  JREC4 = JREC4 + 1
               END IF
            END IF
C          .. END OF LOOP            
         GO TO 200
C
C      RETRIEVE MEAN/EXTREME RECORD FOR DAILY DATA
C
      ELSE IF (ITYPE.EQ.3) THEN
         IDAY1 = BEGTIM
         IF (MAPROW.GT.0) THEN
            IDAY2 = BEGTIM
         ELSE
            IDAY2 = ENDTIM
         END IF
         WRITE(BEGKEY,600) STNID,MONTH,IDAY1
         WRITE(ENDKEY,600) STNID,MONTH,IDAY2
C
C      LOCATE THE FIRST EXTREMES RECORD FOR THIS STN, TYPE AND TIME
C      THEN READ ALL OF THE VALUES REQUESTED
C
         CALL FNDEXD(STNID,MONTH,IDAY1,000,RTNCODE,JREC4,NUMEXT,IBYR
     +      ,IYRS,VALMAX,MAXYR,VALMIN,MINYR,MEANVAL)
300      CONTINUE
            CALL RDEXD(INSTN,INMONTH,INDAY,INELEM,RTNCODE,JREC4,IBYR
     +         ,IYRS,VALMAX,MAXYR,VALMIN,MINYR,MEANVAL)
            WRITE(INKEY,600) INSTN,INMONTH,INDAY
            IF (RTNCODE.NE.'0') THEN
               GO TO 500
            ELSE IF (INKEY.LT.BEGKEY) THEN
               JREC4 = JREC4 + 1
            ELSE IF (INKEY.GT.ENDKEY) THEN
               GO TO 500
            ELSE   
               IF (MAPROW.GT.0) THEN
                  IDXROW = MAPROW
               ELSE
                  IDXROW = INDAY
               ENDIF      
               DO 320 I1 = 1,NWANTED
                  IF (INELEM.EQ.ELEMWANTED(I1)) THEN
                     I2 = JINDEX(I1)
                     IF (EXTWANTED(I1).EQ.1) THEN
                        HLDARRAY(I2,IDXROW) = MEANVAL
                     ELSE IF (EXTWANTED(I1).EQ.2) THEN
                        HLDARRAY(I2,IDXROW) = VALMAX
                     ELSE IF (EXTWANTED(I1).EQ.3) THEN
                        HLDARRAY(I2,IDXROW) = VALMIN
                     END IF
                  END IF
320            CONTINUE
               JREC4 = JREC4 + 1
            END IF   
C          .. END OF LOOP            
         GO TO 300
      END IF
C
500   CONTINUE
      CALL LOCATE(24,55,IERR)
      CALL WRTSTR('                      ',22,12,0)
      RETURN
C
C       ** FORMAT STMTS
C
  600 FORMAT(A8,I2.2,I2.2)
C        
      END
***********************************************************************
      SUBROUTINE FNDEXD(STNID,MONTH,DAY,ELEM,RTNCODE,IREC,NUMREC
     +     ,BYEAR,YEARS,MAXVALUE,MAXYR,MINVALUE,MINYR,MEANVALUE)
C
C   ROUTINE TO DO A BINARY SEARCH OF THE DAILY OR 10 DAY MEANS/EXTREMES
C   FILE (ALREADY OPENED AS FILE 31) TO FIND THE RECORD FOR THE STNID,
C   MONTH, DAY, AND ELEMENT WANTED.
C
      CHARACTER*15 SRCHKY,HLDKEY
      CHARACTER*8 STNID,INSTN
      CHARACTER*1 RTNCODE
      INTEGER*2 ELEM,INELEM,MONTH,INMONTH,DAY,INDAY,BYEAR,YEARS
     +         ,MAXYR, MINYR
      INTEGER*4 ITOP,IBOT,K,NUM4,IREC,NUMREC
      REAL*4    MAXVALUE,MINVALUE
      REAL*8    MEANVALUE
C
      DATA HLDKEY /'               '/
C
      NUM4 = NUMREC
C
C   INITIALIZE THE SEARCH VARIABLES - CHECK AGAINST PREVIOUS VALUES
C   TO SEE IF SEARCH CAN BE NARROWED
C
      WRITE(SRCHKY,'(A8,I2.2,I2.2,I3.3)') STNID,MONTH,DAY,ELEM
      IF (SRCHKY.GT.HLDKEY) THEN
         IBOT = K
         ITOP = NUM4 + INT4(1)
      ELSE
         IBOT = 0
         ITOP = K
      END IF 
C
C   BEGIN THE BINARY SEARCH LOOP FOR THE CURRENT STN/MONTH/DAY/ELEM
C 
  100 CONTINUE
         IF (ITOP-IBOT.GT.1) THEN
            K = (ITOP+IBOT) / 2
            CALL RDEXD(INSTN,INMONTH,INDAY,INELEM,RTNCODE,K,BYEAR,YEARS
     +          ,MAXVALUE,MAXYR,MINVALUE,MINYR,MEANVALUE)
            IF (RTNCODE.NE.'0') THEN
               GO TO 300
            END IF
C
            WRITE(HLDKEY,'(A8,I2.2,I2.2,I3.3)') INSTN,INMONTH,INDAY
     +            ,INELEM
C
            IF (SRCHKY.LT.HLDKEY) THEN
               ITOP = K
               GO TO 100
            ELSE IF (SRCHKY.GT.HLDKEY) THEN
               IBOT = K
               GO TO 100
            END IF
         ELSE
            IF (SRCHKY.NE.HLDKEY) THEN
               GO TO 300
            END IF
         END IF
C
C     PROPER STN/MONTH/ELEM HAS BEEN FOUND - FINISHED
C
         RTNCODE = '0'
         IREC = K
         RETURN
C
C     GET HERE IF STATION/OBS/MONTH/ELEM NOT FOUND
C
  300 CONTINUE
      RTNCODE = '1'
      IREC = K
      RETURN
      END
************************************************************************
      SUBROUTINE RDEXD(STNID,MONTH,DAY,ELEM,RTNCODE,IREC,BYEAR
     +     ,YEARS,MAXVALUE,MAXYR,MINVALUE,MINYR,MEANVALUE)
C
C   ROUTINE TO READ RECORD IREC FROM THE DAILY OR 10DAY MEANS/EXTREMES
C   FILE.  THE FILE MUST ALREADY BE OPEN AS FILE 31.  IF NO RECORD
C   IS FOUND AT THAT POSITION RTNCODE='1', ELSE RTNCODE='0'.
C
      INTEGER*2 MONTH,DAY,ELEM,BYEAR,YEARS,MAXYR,MINYR
      REAL*4  MAXVALUE,MINVALUE
      REAL*8  MEANVALUE
      CHARACTER*1 RTNCODE
      CHARACTER*8 STNID
C
      CHARACTER*51 INREC
      INTEGER*2 HEADER(2)
      INTEGER*4 IREC
      EQUIVALENCE (INREC,HEADER)
C
      RTNCODE = '0'
10    CONTINUE
      READ(31,REC=IREC,ERR=100) INREC
      IF(HEADER(1).NE.12.AND.HEADER(1).NE.14) GO TO 10
      STNID = INREC(5:12)
      READ(INREC,20) ELEM,MONTH,DAY,BYEAR,MAXYR,MINYR
20    FORMAT(12X,I3,2I2,I4,7X,I4,5X,I4)
      YEARS = INREC(24:25)
      MAXVALUE = INREC(26:29)
      MINVALUE = INREC(35:38)
      MEANVALUE = INREC(44:51)
      RETURN   
C      
  100 CONTINUE
      RTNCODE = '1'
      RETURN
      END
