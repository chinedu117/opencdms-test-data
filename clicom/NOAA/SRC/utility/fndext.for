$STORAGE:2

      SUBROUTINE FNDEXT(STNID,ITYPE,MONTH,ELEM,RTNCODE
     +     ,IREC,SOURCE,BYEAR,EYEAR,YEARS,MAXVALUE,MAXDATE,MAXFLAG
     +     ,MINVALUE,MINDATE,MINFLAG,MEANVALUE,STNDRDDEV,MAXCHNG)
C
C   ROUTINE TO DO A BINARY SEARCH OF THE ELEM.LIM FILE TO FIND THE
C      THE RECORD FOR THE INDICATED STN, OBSTYPE,MONTH, ELEMENT  
C
      CHARACTER*15 SRCHKY,HLDKEY
      CHARACTER*8 STNID,INSTN
      CHARACTER*1 RTNCODE
      INTEGER*2 ELEM,INELEM,MONTH,INMONTH,SOURCE,BYEAR,EYEAR,YEARS
     +         ,MAXFLAG,MINFLAG
      INTEGER*4 MAXDATE,MINDATE,IREC,K,ITOP,IBOT,NUMREC
      REAL*4    MAXVALUE,MINVALUE,MAXCHNG
      REAL*8    MEANVALUE,STNDRDDEV
C
C   INITIALIZE THE SEARCH VARIABLES
C
      WRITE(SRCHKY,'(A8,I2.2,I2.2,I3.3)') STNID,ITYPE,MONTH,ELEM
      READ(31,REC=1) NUMREC
      ITOP = NUMREC + 2
      IBOT = 1
C
C   BEGIN THE BINARY SEARCH LOOP FOR THE CURRENT STN/OBSTYPE/MONTH/ELEM
C 
  100 CONTINUE
         IF (ITOP-IBOT.GT.1) THEN
            K = (ITOP+IBOT) / 2
            READ(31,REC=K)INSTN,IOBS,INMONTH,INELEM,SOURCE,
     +                 BYEAR,EYEAR,YEARS,MAXVALUE,MAXDATE,
     +                 MAXFLAG,MINVALUE,MINDATE,MINFLAG,
     +                 MEANVALUE,STNDRDDEV,MAXCHNG
C
            WRITE(HLDKEY,'(A8,I2.2,I2.2,I3.3)') INSTN,IOBS,INMONTH
     +            ,INELEM
C
            IF (SRCHKY.LT.HLDKEY) THEN
               ITOP = K
               GO TO 100
            ELSE IF (SRCHKY.GT.HLDKEY) THEN
               IBOT = K
               GO TO 100
            END IF
         ELSE
            IF (SRCHKY.NE.HLDKEY) THEN
               GO TO 300
            END IF
         END IF
C
C     PROPER STN/MONTH/ELEM HAS BEEN FOUND - FINISHED
C
         RTNCODE = '0'
         IREC = K
         RETURN
C
C     GET HERE IF STATION/OBS/MONTH/ELEM NOT FOUND
C
  300 CONTINUE
      IREC = K
      RTNCODE = '1'
      RETURN
      END