$STORAGE:2
C
      SUBROUTINE GENRH2(ASPTWB,DB,WB,SP,RH)
C
C  CALCULATE RELATIVE HUMIDITY
C  ASPTWB -- FLAG TO INDICATE METHOD USED TO MEASURE WET BULB TEMPERATURE
C            0=NON-ASPIRATED  1=ASPIRATED
C  DRY BULB TEMP, WET BULB TEMP AND STATION PRESSURE ARE REQUIRED.
C  DB, WB = TEMPS = DEG C.
C  SP = STATION PRESS = MILLIBARS
C  RH = REL HUM IN WHOLE PERCENT
C
      INTEGER*2   ASPTWB
      PP = SP
C
C  ** CALCULATE RH FROM DB, WB AND SP **
C
C  NOW CALCULATE ES WITH RESPECT TO DB
C
C  ESDB = 33.8639[(.00738DB + .8072)**8 - .000019|1.8DB + 48| + .001316]
C        NOTE! |XX| = ABSOLUTE VALUE OF XX
C
      AA = (.00738 * DB + .8072)**8
      BB = .000019 * ABS(1.8 * DB + 48)
      ESDB = 33.8639 * (AA - BB + .001316)
C
C  NOW CALCULATE ES WITH RESPECT TO WB
C
C  ESWB = 33.8639[(.00738WB + .8072)**8 - .000019|1.8WB + 48| + .001316]
C        NOTE! |XX| = ABSOLUTE VALUE OF XX
C
      AA = (.00738 * WB + .8072)**8
      BB = .000019 * ABS(1.8 * WB + 48)
      ESWB = 33.8639 * (AA - BB + .001316)
C
C  NOW CALCULATE E
C
C  E = ESWB - .000660PP(1 + .00115WB)(DB - WB)   ---- ASPIRATED TEMPERATURE
C  E = ESWB - .000799PP(1 + .00115WB)(DB - WB)   ---- NON-ASPIRATED TEMPERATURE
C
      IF (ASPTWB.EQ.1) THEN
         FAC = .000660
      ELSE 
         FAC = .000799
      ENDIF      
      CC = 1 + .00115 * WB
      WBDEP = DB - WB
      DD = FAC * PP * CC * WBDEP
C
      E = ESWB - DD
C
C  NOW CALCULATE RH
C
C  RH = E / ESDB
C
      RH = E / ESDB
      RH = RH * 100
C
      RETURN
      END
C

