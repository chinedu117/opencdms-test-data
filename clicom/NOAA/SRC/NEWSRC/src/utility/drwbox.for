$STORAGE:2

      SUBROUTINE DRWBOX(STRTCOL,STRTROW,ENDCOL,ENDROW,BOXTYP
     +                 ,FGCOLOR,BGCOLOR)
C
C   ROUTINE TO DRAW A BOX OF THE GIVEN COLOR WITH SINGLE OR
C     DOUBLE LINES.  THE BOX IS DRAWN FROM STRTCOL,STRTROW TO 
C     ENDCOL,ENDROW
C
C     BOXTYPE  - 1 = SINGLE LINES
C              - 2 = DOUBLE LINES
C              - 3 = SOLID LINES
C              - 4 = BLANKS (BACKGROUND COLOR)
C
      INTEGER*2 STRTCOL,STRTROW,ENDCOL,ENDROW,BOXTYP,FGCOLOR,BGCOLOR
C
      INTEGER*2 TLCORN(4),TRCORN(4),BLCORN(4),BRCORN(4),HLINE(4)
     +         ,VLINE(4)
      DATA TLCORN,TRCORN,BLCORN,BRCORN,HLINE,VLINE /218,201,220,32
     +    ,191,187,220,32, 192,200,219,32, 217,188,219,32
     +    ,196,205,220,32, 179,186,219,32/
C
C       .. UPPER LEFT CORNER
      CALL LOCATE(STRTROW,STRTCOL,IERR)
      JCHAR = TLCORN(BOXTYP)
      CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
C      
C          .. UPPER HORIZONTAL LINE
      IF (ENDCOL.GT.STRTCOL+1) THEN
         CALL LOCATE(STRTROW,STRTCOL+1,IERR)
         JCHAR = HLINE(BOXTYP)
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,ENDCOL-STRTCOL-1)
      END IF
C      
C          .. UPPER RIGHT CORNER         
      IF (ENDCOL.GT.STRTCOL) THEN
         CALL LOCATE(STRTROW,ENDCOL,IERR)
         JCHAR = TRCORN(BOXTYP)
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
      END IF
C      
C          .. LEFT AND RIGHT VERTICAL LINES      
      IF (ENDROW.GT.STRTROW+1) THEN
         JCHAR = VLINE(BOXTYP)
         DO 120 I = STRTROW+1,ENDROW-1
            CALL LOCATE(I,STRTCOL,IERR)
            CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
            CALL LOCATE(I,ENDCOL,IERR)
            CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
  120    CONTINUE
      END IF
C      
C          .. BOTTOM RIGHT AND LEFT CORNERS
      IF (ENDROW.GT.STRTROW) THEN
         CALL LOCATE(ENDROW,ENDCOL,IERR)
         JCHAR = BRCORN(BOXTYP)
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
         CALL LOCATE(ENDROW,STRTCOL,IERR) 
         JCHAR = BLCORN(BOXTYP)
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,1)
      END IF
C      
C          .. BOTTOM HORIZONTAL LINE      
      IF (ENDCOL.GT.STRTCOL+1) THEN
         CALL LOCATE(ENDROW,STRTCOL+1,IERR)
         JCHAR = HLINE(BOXTYP)
         CALL CHRWRT(JCHAR,BGCOLOR,FGCOLOR,ENDCOL-STRTCOL-1)
      END IF
      RETURN
      END
