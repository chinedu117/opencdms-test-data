$STORAGE:2 
      SUBROUTINE SELAREA(I1WRK,I1WRKD,LOWLAT,HILAT,LOWLON,HILON,
     +                   MXDATROW,RTNFLAG)
C
C       ** OBJECTIVE:   ALLOWS THE USER TO SPECIFY AN AREA TO BE MAPPED BY
C                       SETTING LATITUDE AND LONGITUDE BOUNDARIES.  IT THEN 
C                       RETURNS THE BOUNDARY COORDINATES, A LIST OF CURRENTLY
C                       DEFINED STATIONS WHOSE LAT,LONS FALL WITHIN THE 
C                       BOUNDARIES GIVEN, AND A LIST OF THE LATITUDES AND
C                       LONGITUDES OF EACH OF THE STATIONS FOUND.
C                       THE STATIONS ARE SELECTED FROM EITHER THE STNGEOG.INF
C                       FILE OR A FILE CONTAINING A LIST OF USER SPECIFIED
C                       STATIONS
C       ** INPUT:
C             I1WRK..........INTEGER*1 WORK ARRAY
C             I1WRKD.........SIZE OF WORK ARRAY (I*4)
C             LOWLAT,HILAT...LATITUDE RANGE WANTED (FLOATING PT)
C             LOWLON,HILON...LONGITUDE RANGE WANTED (FLOATING PT)
C             MXDATROW.......MAXIMUM NUMBER OF STATIONS THAT CAN BE SELECTED
C                            FOR THE CURRENT PLOT (INTEGER*2)
C       ** OUTPUT:
C            *STNLIST........LIST OF THE STATION-ID'S FOUND 
C                            (CHAR*8 DIMENSIONED MAXROW)
C            *LATLIST........LATITUDES OF THE STATIONS FOUND (REAL (MAXROW))
C            *LONLIST........LONGITUDES OF THE STATIONS FOUND (REAL (MAXROW))
C             RTNFLAG........'4F' IF USER HAS PRESSED F4 OR NO STATIONS FOUND 
C
C       ** NOTE:  STNLIST, LATLIST, LONLIST ARE IN COMMON /LTLNSTN/
C
      INTEGER*4    I1WRKD
      INTEGER*2    MXDATROW
      INTEGER*1    I1WRK(I1WRKD)
      REAL*4       LOWLAT,HILAT,LOWLON,HILON
      CHARACTER*2  RTNFLAG
C      
C       .. MAXROW, DEFINED IN GRFPARM.INC, IS THE DIMENSION OF LATLIST/LONLIST
C          LATLIST AND LONLIST ARE DEFINED IN ROUTINE SELAREA.  THEY ARE 
C          NOT PASSED AS ARGUMENTS BUT RATHER THE COMMON BLOCK /LTLNSTN/
C          IS INCLUDED IN SELAREA.  THIS ALLOWS I2WRK TO BE PASSED AS AN
C          ARGUMENT TO THE ROUTINES THAT PRODUCE A USER DEFINED STATION
C          SELECTION LIST.  THE ROUTINES THAT USE I2WRK COMPLETE THEIR
C          PROCESSING BEFORE VALUES ARE DEFINED FOR LATLIST AND LONLIST.
C      
C       .. I2WRK IS A WORK ARRAY USED BY THE ROUTINE DFSTNLST; TO REDUCE 
C          EXECUTABLE SIZE, IT OCCUPIES THE SAME SPACE AS LATLIST AND LONLIST
$INCLUDE:  'GRFPARM.INC'
$INCLUDE: 'LTLNSTN.INC'
C      
      INTEGER*4    BDATE,EDATE,NRECG
      CHARACTER*28 MSGTXT
      CHARACTER*24 STNABRV
      CHARACTER*22 STNFILE
      CHARACTER*20 COUNTRY,LSTFIL
      CHARACTER*8  STNID,PREVID,LSTNAM
      CHARACTER*8  INLON
      CHARACTER*7  INLAT
      CHARACTER*1  RTNCODE
      LOGICAL      LSTFLG
      DATA STNFILE/'P:\DATA\STNGEOG.INF   '/
C
      CALL POSLIN(IROW,ICOL)      
C
C       ** ASK USER FOR THE LAT LON BOUNDARIES OF THE AREA WANTED
C
   10 CONTINUE
      CALL LOCATE(IROW,ICOL,IERR)
      CALL MAPCOORD(0,LOWLAT,HILAT,LOWLON,HILON,RTNFLAG)
      IF (RTNFLAG.EQ.'4F') THEN
         RETURN
      END IF
C      
C       ** DETERMINE THE MAXIMUM NUMBER OF STNGEOG RECORDS THAT CAN BE
C          PROCESSED FOR A STATION SELECTION LIST; THE NUMBER MUST BE
C          REPRESENTED BY I*2 AND FIT IN THE WORK ARRAY I1WRKD.  NOTE THAT
C          THE NUMBER OF RECORDS IS ONE MORE THAN THE NUMBER OF STATIONS.
C            MXRSTNG = MINIMUM(NUMSTN,32000,I1WRKD-1) + 1
   12 OPEN (35,FILE=STNFILE,STATUS='OLD',ACCESS='DIRECT',RECL=80,
     +         SHARE='DENYWR',MODE='READ',IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         CALL OPENMSG(STNFILE,'GRAFINIT    ',IOCHK)
         GO TO 12
      END IF
      READ(35,REC=1,IOSTAT=IOCHK) NUMSTN
      IF (IOCHK.NE.0) THEN
         LGTH = LNG(STNFILE)
         CALL WRTMSG(3,191,12,1,1,STNFILE,LGTH)
         GO TO 10
      ENDIF         
      CLOSE(35)
      MXRSTNG = MIN0(MIN0(I1WRKD-1,32000),NUMSTN) + 1
C
C       ** GET THE NAME OF A FILE CONTAINING A USER SPECIFIED LIST OF STATIONS
C          DEFAULT LIST NAME IS STNGEOG 
C
      LSTNAM = 'STNGEOG'
   15 CONTINUE
C       .. I2WRK(I*2) IS EQUIVALENCED TO LATLIST/LONLIST(R*4)--INITIAL AS I*2
      DO 17 I=1,I2WRKD
         I2WRK(I) = 0
   17 CONTINUE         
C       .. CHECK FOR THE REMOTE CHANCE THAT I1WRK MIGHT BE DIMENSIONED TOO
C          SMALL FOR THE MAXIMUM NUMBER OF STATIONS ALLOWED
      MAXLST=MIN0(MAXROW,I1WRKD)
      CALL LOCATE(IROW+9,ICOL,IERR)
      CALL GTSTNLST(I1WRK,I2WRK,MXRSTNG,I2WRKD,MAXLST,LSTNAM,RTNCODE)
C       .. I2WRK(I*2) IS EQUIVALENCED TO LATLIST/LONLIST(R*4)--INITIAL AS R*4
      DO 18 I=1,MAXROW
         LATLIST(I) = 0.
         LONLIST(I) = 0.
   18 CONTINUE         
      IF (RTNCODE.NE.'0') GO TO 10
      LSTFLG=.FALSE.
      NRECL=0
C
C       **  OPEN THE STATION SELECTION FILE; GET NUMBER OF RECORDS IN FILE
C
      IF (LSTNAM.NE.'STNGEOG') THEN
         LSTFIL = 'O:\DATA\'//LSTNAM
         NCHR = LNG(LSTFIL)
         LSTFIL(NCHR+1:) = '.LST'
         OPEN (51,FILE=LSTFIL,STATUS='OLD',
     +            ACCESS='DIRECT',RECL=36,IOSTAT=IOCHK)
         IF (IOCHK.NE.0) THEN
            NCHR=LNG(LSTFIL)
            CALL WRTMSG(3,44,12,1,1,LSTFIL,NCHR)
            GO TO 15
         ENDIF   
         READ(51,REC=1,IOSTAT=ISTAT) NRECL
         IF (ISTAT.NE.0) THEN
            WRITE(MSGTXT,'(A,2X,I4.4)') LSTFIL,ISTAT
            LGTH = LNG(MSGTXT)
            CALL WRTMSG(3,191,12,1,1,MSGTXT,LGTH)
            GO TO 15
         ENDIF         
C         
C          .. CHECK IF RECORD NUMBERS AND STATION ID'S SPECIFIED IN THE LIST
C             FILE AGREE WITH THE STNGEOG.INF FILE;  ADJUST RECORD NUMBERS
C             IN LIST FILE SO THE FILES AGREE.  REJECT USER LIST FILE IF A 
C             RECORD DOES NOT HAVE A MATCH IN THE STNGEOG.INF FILE
         RTNCODE='0'
         CALL CKRECNBR(RTNCODE)
         IF (RTNCODE.NE.'0') THEN
            RTNCODE='0'
            CALL GTRECNBR(MXRSTNG,RTNCODE)
            IF (RTNCODE.NE.'0') THEN 
               CALL WRTMSG(3,582,12,1,1,' ',0)
               GO TO 15
            ENDIF   
         ENDIF   
         LSTFLG=.TRUE.
C          .. READ THE NUMBER OF STATIONS AGAIN IN CASE THE FILE WAS
C             CHANGED BY ROUTINE GTRECNBR
         READ(51,REC=1) NRECL
      ENDIF
C
C       ** OPEN THE STNGEOG.INF FILE AND INITIALIZE
C
20    OPEN (35,FILE=STNFILE,STATUS='OLD',ACCESS='DIRECT',RECL=80,
     +         SHARE='DENYWR',MODE='READ',IOSTAT=IOCHK)
      IF (IOCHK.NE.0) THEN
         CALL OPENMSG(STNFILE,'GRAFINIT    ',IOCHK)
         GO TO 20
      END IF
      READ(35,REC=1) NRECG
      INDEX = 0
      PREVID = 'XXXXXXXX'
C
C       ** IF STATIONS ARE SELECTED FROM THE STNGEOG.INF FILE, READ THROUGH
C          THE FILE SEQUENTIALLY.  IF STATIONS ARE SELECTED FROM THE USER 
C          LIST FILE, READ THE USER LIST FILE SEQUENTIALLY AND THEN READ
C          THE EQUIVALENT RECORD FROM THE STNGEOG FILE.  IDENTIFY ALL STATIONS
C          WHICH FALL WITHIN THE BOUNDARIES SPECIFIED, AND WRITE THEIR ID'S AND
C          LATEST LAT LON COORDINATES INTO THE OUTPUT ARRAYS.
C
      CALL WRTMSG(10,279,12,0,0,' ',0)
      IF (LSTFLG) THEN
         NRECRD=NRECL
      ELSE
         NRECRD=MIN0(NRECG,32000)
      ENDIF            
      DO 100 KREC = 2,NRECRD+1
         IF (LSTFLG) THEN
            READ(51,REC=KREC) IRECG,STNID,STNABRV
         ELSE
            IRECG=KREC
         ENDIF   
         READ(35,END=110,REC=IRECG) STNID,BDATE,EDATE,STNABRV,COUNTRY,
     +                              INLAT,INLON
         CALL LAT2REAL(INLAT,INLON,XLAT,XLON)
         IF (XLAT.GE.LOWLAT.AND.XLAT.LE.HILAT.AND.XLON.GE.LOWLON.AND.
     +         XLON.LE.HILON) THEN
            IF (STNID.NE.PREVID) THEN
               INDEX = INDEX + 1
               IF (INDEX.GT.MXDATROW) THEN
                  CALL WRTMSG(4,184,12,0,0,STNID,8)
                  WRITE(MSGTXT,'(I5)') MXDATROW
                  CALL WRTMSG(3,593,12,1,1,MSGTXT,5)
                  CALL CLRMSG(4)
                  GO TO 110
               END IF
            END IF
            STNLIST(INDEX) = STNID
            LATLIST(INDEX) = XLAT
            LONLIST(INDEX) = XLON
            CALL LOCATE(15,20,IERR)
            CALL WRTSTR(STNID,8,11,0)
         END IF
         PREVID = STNID
100   CONTINUE
110   CONTINUE
C
C      ** CLOSE STNGEOG.INF, WRITE WARNING IF NO STATIONS FOUND AND RETURN
C
      IF (LSTFLG) CLOSE(51)
      CLOSE(35)
      IF (INDEX.EQ.0) THEN
         CALL WRTMSG(3,376,12,1,1,' ',0)
         RTNFLAG = '4F'
      END IF
      CALL CLRMSG(10)
      RETURN
      END
