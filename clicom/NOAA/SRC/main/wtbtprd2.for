$STORAGE:2
C
C     PROGRAM WTBTPRD2
C
C     ** OBJECTIVE:  PROGRAM CONSTRUCTS THE CALL FROM DATAPROD.BAT TO 
C                    DATAPRD2.BAT BASED ON THE PRODUCT CHOSEN AND WRITES 
C                    THE CALL IN THE FILE CALLPRD2.BAT
C                    
C     NOTE:  THE DATA-TYPE CODE (MLY,DLY...) FOR THE OUTPUT PRODUCT MUST 
C            BE INCLUDED ON THE COMMAND LINE CALLING THIS ROUTINE
C            EXAMPLE: "WTBTPRD2 DLY"
C
      INTERFACE TO SUBROUTINE CMDLIN(ADDRES,LENGTH,RESULT)
      INTEGER*4 ADDRES[VALUE],LENGTH[VALUE]
      CHARACTER*1 RESULT
      END
C
C----------------------------------------------------------------------
C
      PROGRAM WTBTPRD2
C
      INTEGER*4   NUMREC
      CHARACTER*1 NULL
      CHARACTER*3 RECTYPE
      CHARACTER*6 OUTFRMT
      CHARACTER*8 DEASFIL
      CHARACTER*21 OUTFIL
      CHARACTER*22 FILNAME
      CHARACTER*60 LINE
C
      PARAMETER (NTYPD=5)
      CHARACTER*3  TBLRECTYP(NTYPD)
      CHARACTER*8  TBLPROGRM(NTYPD)
      CHARACTER*24 TBLFRMNAM(NTYPD),TBLFRMARG(NTYPD)
C
      CHARACTER*64 RESULT
      INTEGER*4 PSP,PSPNCHR,OFFSET
C
C       ** FOLLOWING STATEMENT REQUIRED FOR FORTRAN 3.3 - NOT ALLOWED IN FTN 4
C
C      EXTERNAL WTBTPRD2
C
      DATA TBLRECTYP/'MLY', '10D', '10X', 'DLX', 'NML'/
      DATA TBLPROGRM/'DLY2MLY ','DLY210D ','10DEXTR ','DLYEXTR2',
     +               'NORMAL  '/
      DATA TBLFRMNAM/
     +          'MONTHLY DATA            ', 'TEN DAY DATA            ',
     +          'Means/Extremes (10D)    ', 'Daily Means/Extremes    ',
     +          'NORMALS                 '/
      DATA TBLFRMARG/
     +          'MONTHLY-DATA            ', 'TEN-DAY-DATA            ',
     +          'MEANS/EXTREMES-(10D)    ', 'DAILY-MEANS/EXTREMES    ',
     +          'NORMALS                 '/
C
      NULL = CHAR(0)
C
C       ** LOCATE SEGMENTED ADDRESS OF THE BEGINNING OF THIS PROGRAM
C
      OFFSET = #00100000
      PSP = LOCFAR(WTBTPRD2)
C
C       ** COMPUTE THE BEGINNING OF THE PROGRAM SEGMENT PREFIX (PSP)
C
      PSP = (PSP - MOD(PSP,#10000)) - OFFSET 
C
C       ** LOCATE POSITION OF COMMAND PARAMETERS WITHIN THE PSP
C
      PSPNCHR = PSP + #80
      PSP = PSP + #81
C
C       ** PASS THE ADDRESS OF THE COMMAND PARAMTERS TO CMDLIN WHICH DECODES
C          THE COMMAND AND RETURNS IT AS RESULT.
C
      CALL CMDLIN(PSP,PSPNCHR,RESULT)
C
C       ** PULL THE COMMAND (REC-TYPE) OUT OF THE RESULT
C
      RECTYPE = RESULT(1:3)
C
C       ** CONVERT LOWERCASE LETTERS TO UPPERCASE LETTERS
C
      DO 10 I=1,3
         IF (RECTYPE(I:I).LT.'a' .OR. RECTYPE(I:I).GT.'z') GO TO 10
         IVAL = ICHAR(RECTYPE(I:I)) - 32
         RECTYPE(I:I) = CHAR(IVAL)
   10 CONTINUE            
C
C       ** FIND THE RELATIVE NUMBER OF THE DATATYPE WANTED -- USED AS AN INDEX
C          TO THE DATAEASE FORM NAME 
C
      DO 20 I = 1,NTYPD
         IF (RECTYPE.EQ.TBLRECTYP(I)) THEN
            ITYP = I
            GO TO 30
         END IF
   20 CONTINUE
      STOP 3
   30 CONTINUE
C
C       ** GET ACTUAL FILE NAME ASSOCIATED WITH THE DATAEASE FORM NAME
C
      CALL FNDFIL(TBLFRMNAM(ITYP),FILNAME,NUMREC)
      DEASFIL = FILNAME(3:10)
C
C       ** OPEN BATCH FILE THAT WILL CONTAIN THE CALL TO DATAPRD2.BAT
C
      OUTFIL = 'P:\BATCH\CALLPRD2.BAT'
      OPEN (63,FILE=OUTFIL,STATUS='UNKNOWN',FORM='FORMATTED')
C 
C       ** WRITE THE CALL TO DATAPRD2.BAT WITH THE ACTUAL DATAEASE FILE NAME
C             DATAPRD2 DLY2MLY  MLY MONTAAAx MONTHLY-DATA
C             DATAPRD2 DLY210D  10D TENDAAAx 10-DAY-DATA 
C             DATAPRD2 10DEXTR  10X MEANAAAx MEANS/EXTREMES-(10D)
C             DATAPRD2 DLYEXTR2 DLX DAILAAAx DAILY-MEANS/EXTREMES
C             DATAPRD2 NORMAL   NML NORMAAAx NORMALS
C
      WRITE(LINE,500) TBLPROGRM(ITYP),RECTYPE,DEASFIL,TBLFRMARG(ITYP)
  500 FORMAT('DATAPRD2 ',A8,1X,A3,1X,A8,1X,A24)
      CALL GETFRMT(LINE,OUTFRMT)
      WRITE(63,OUTFRMT) LINE
      CLOSE(63)
C
      STOP ' '
      END