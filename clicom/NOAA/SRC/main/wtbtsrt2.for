$STORAGE:2
C
C     PROGRAM WTBTSRT2
C
C     ** OBJECTIVE:  PROGRAM CONSTRUCTS THE CALL FROM DSORT.BAT TO SORT2.BAT 
C                    BASED ON THE DATA-TYPE OF THE FILE TO BE SORTED AND 
C                    WRITES THE CALL IN THE FILE CALLSRT2.BAT
C                    
C     ** NOTE:  THE DATA-TYPE CODE (MLY,DLY...) FOR THE FILE TO BE SORTED AND
C               THE CHARACTERS 'APPL' IF DSORT IS CALLED FROM APPL.BAT MUST 
C               BE INCLUDED ON THE COMMAND LINE CALLING THIS ROUTINE
C               EXAMPLE: "WTBTSRT2 DLY APPL"
C
      INTERFACE TO SUBROUTINE CMDLIN(ADDRES,LENGTH,RESULT)
      INTEGER*4 ADDRES[VALUE],LENGTH[VALUE]
      CHARACTER*1 RESULT
      END
C
C----------------------------------------------------------------------
C
      PROGRAM WTBTSRT2
C
      INTEGER*4   NUMREC
      CHARACTER*1 NULL
      CHARACTER*3 RECTYPE
      CHARACTER*4 APPLFLG
      CHARACTER*6 OUTFRMT
      CHARACTER*8 DEASFIL
      CHARACTER*21 OUTFIL
      CHARACTER*22 FILNAME
      CHARACTER*40 LINE
C
      PARAMETER (NTYPD=8)
      CHARACTER*3  TBLRECTYP(NTYPD)
      CHARACTER*10 TBLTYPNAM(NTYPD)
      CHARACTER*24 TBLFRMNAM(NTYPD)
C
      CHARACTER*64 RESULT
      INTEGER*4    PSP,PSPNCHR,OFFSET
C
C       ** FOLLOWING STATEMENT REQUIRED FOR FORTRAN 3.3 - NOT ALLOWED IN FTN 4
C
C      EXTERNAL WTBTSRT2
C
      DATA TBLRECTYP/'MLY','10D','DLY','SYN','HLY','15M','U-A','NML'/
      DATA TBLFRMNAM/ 
     +           'MONTHLY DATA'  ,'TEN DAY DATA','DAILY DATA',
     +           'SYNOPTIC DATA' ,'HOURLY DATA' ,'FIFTEEN MINUTE DATA',
     +           'UPPER-AIR DATA','NORMALS'/
      DATA TBLTYPNAM/ 'Monthly   ','10-DAY    ','Daily     ', 
     +                'Synoptic  ','Hourly    ','15-Minute ',
     +                'Upper-air ', 'Normal    '/
C
      NULL = CHAR(0)
C
C       ** LOCATE SEGMENTED ADDRESS OF THE BEGINNING OF THIS PROGRAM
C
      OFFSET = #00100000
      PSP = LOCFAR(WTBTSRT2)
C
C       ** COMPUTE THE BEGINNING OF THE PROGRAM SEGMENT PREFIX (PSP)
C
      PSP = (PSP - MOD(PSP,#10000)) - OFFSET 
C
C       ** LOCATE POSITION OF COMMAND PARAMETERS WITHIN THE PSP
C
      PSPNCHR = PSP + #80
      PSP = PSP + #81
C
C       ** PASS THE ADDRESS OF THE COMMAND PARAMTERS TO CMDLIN WHICH DECODES
C          THE COMMAND AND RETURNS IT AS RESULT.
C
      CALL CMDLIN(PSP,PSPNCHR,RESULT)
C
C       ** PULL THE COMMAND (REC-TYPE) OUT OF THE RESULT
C
      RECTYPE = RESULT(1:3)
      APPLFLG = RESULT(5:8)
C
C       ** CONVERT LOWERCASE LETTERS TO UPPERCASE LETTERS
C
      DO 10 I=1,3
         IF (RECTYPE(I:I).LT.'a' .OR. RECTYPE(I:I).GT.'z') GO TO 10
         IVAL = ICHAR(RECTYPE(I:I)) - 32
         RECTYPE(I:I) = CHAR(IVAL)
   10 CONTINUE            
C
C       ** FIND THE RELATIVE NUMBER OF THE DATATYPE WANTED -- USED AS AN INDEX
C          TO THE DATAEASE FORM NAME 
C
      DO 20 I = 1,NTYPD
         IF (RECTYPE.EQ.TBLRECTYP(I)) THEN
            ITYP = I
            GO TO 30
         END IF
   20 CONTINUE
      STOP 3
   30 CONTINUE
C
C       ** GET ACTUAL FILE NAME ASSOCIATED WITH THE DATAEASE FORM NAME
C
      CALL FNDFIL(TBLFRMNAM(ITYP),FILNAME,NUMREC)
      DEASFIL = FILNAME(3:10)
C
C       ** OPEN BATCH FILE THAT WILL CONTAIN THE CALL TO SORT2.BAT
C
      OUTFIL = 'P:\BATCH\CALLSRT2.BAT'
      OPEN (63,FILE=OUTFIL,STATUS='UNKNOWN',FORM='FORMATTED')
C 
C       ** WRITE THE CALL TO MERGE2.BAT WITH THE ACTUAL DATAEASE FILE NAME
C             SORT2 MLY Monthly   MONTAAAx APPL
C             SORT2 10D 10-DAY    TENDAAAx APPL
C             SORT2 DLY Daily     DAILAAAx APPL
C             SORT2 SYN Synoptic  SYNOAAAx APPL 
C             SORT2 HLY Hourly    HOURAAAx APPL
C             SORT2 15M 15-Minute FIFTAAAx APPL 
C             SORT2 U-A Upper-air UPPEAAAx APPL
C
      WRITE(LINE,500) RECTYPE,TBLTYPNAM(ITYP),DEASFIL,APPLFLG
  500 FORMAT('SORT2 ',A3,1X,A10,1X,A8,1X,A4)
      CALL GETFRMT(LINE,OUTFRMT)
      WRITE(63,OUTFRMT) LINE
      CLOSE(63)
C
      STOP ' '
      END